# Suomi-malaga, suomen kielen muoto-opin kuvaus.
#
# Tekijänoikeus © 2006 Hannu Väisänen (Etunimi.Sukunimi@joensuu.fi)
#
# Tämä ohjelma on vapaa; tätä ohjelmaa on sallittu levittää
# edelleen ja muuttaa GNU yleisen lisenssin (GPL lisenssin)
# ehtojen mukaan sellaisina kuin Free Software Foundation
# on ne julkaissut; joko Lisenssin version 2, tai (valinnan
# mukaan) minkä tahansa myöhemmän version mukaisesti.
#
# Tätä ohjelmaa levitetään siinä toivossa, että se olisi
# hyödyllinen, mutta ilman mitään takuuta; ilman edes
# hiljaista takuuta kaupallisesti hyväksyttävästä laadusta tai
# soveltuvuudesta tiettyyn tarkoitukseen. Katso GPL
# lisenssistä lisää yksityiskohtia.
#
# Tämän ohjelman mukana pitäisi tulla kopio GPL
# lisenssistä; jos näin ei ole, kirjoita osoitteeseen Free
# Software Foundation Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
#
# Tämän ohjeman linkittäminen staattisesti tai dynaamisesti
# muihin moduuleihin on ohjelmaan perustuvan teoksen
# tekemistä, joka on siis GPL lisenssin ehtojen alainen.
#
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; see the file COPYING.  If not, write to the
# Free Software Foundation, Inc., 59 Temple Place -  Suite 330, Boston, MA
# 02111-1307 USA.
#
# Linking this program statically or dynamically with other modules is
# making a combined work based on this program.  Thus, the terms and
# conditions of the GNU General Public License cover the whole
# combination.
#
# This file has been modified by the contributors of Hunspell-fi project.
# Last change was on $Date$ by $Author$.


initial <>, rules nimisana, erisnimi, laatusana, asemosana, apusana, lyhennesääntö,
                  lukusana, etuliite, teonsana, kieltosana, tavuviiva,
		  laatusana_laatusanasta, seikka_tai_suhdesana;

include "suomi.inc";
include "inc/mor.inc";


define @ehtotapa := <ehtotapa, ehtotapa_ttA, ehtotapa_tA>;

define @mahtotapa := <mahtotapa_le, mahtotapa_ne, mahtotapa_re, mahtotapa_se,
                      mahtotapa_le_kielto, mahtotapa_ne_kielto,
                      mahtotapa_re_kielto, mahtotapa_se_kielto,
                      mahtotapa_ttA, mahtotapa_tA>;

define @käskytapa := <käskytapa, käskytapa_kielto, käskytapa_ttA,
                      käskytapa_tA>;

define @nimitapa_1 := <nimitapa_1_A,  nimitapa_1_dA, nimitapa_1_lA,
                       nimitapa_1_nA, nimitapa_1_rA, nimitapa_1_tA>;
define @nimitapa_2 := <nimitapa_2, nimitapa_2_ttA, nimitapa_2_tA>;
define @nimitapa_3 := <nimitapa_3, nimitapa_3_ttA, nimitapa_3_tA>;

# Johtimet teonsanoista

define @johdin_tU := <johdin_dU, johdin_lU, johdin_nU, johdin_rU, johdin_tU, johdin_tU_lU_oltu>;

define @johtimet_nimisana_teonsanasta :=
       <johdin_mA, johdin_UUs, johdin_Us, johdin_ntA, johdin_nti, johdin_Us_ksen>
       + @johdin_jA;

define @johtimet_laatusana_teonsanasta :=
       <johdin_tOn, johdin_vA, johdin_vE, johdin_vAinen, johdin_tAvA, johdin_ttAvA,
        johdin_llinen, johdin_stU, johdin_ttU> + @johdin_nUt + @johdin_tU;

define @johtimet_seikkasana_teonsanasta := <johdin_ittAin>; #FIXME: onko tämä oikein?

define @johdin_teonsanasta := @johtimet_nimisana_teonsanasta
                            + @johtimet_laatusana_teonsanasta
                            + @johtimet_seikkasana_teonsanasta;


# Johtimet nimisanoista

define @johtimet_nimisana_nimisanasta := <johdin_tAr, johdin_lAinen>;

define @johtimet_laatusana_nimisanasta := <johdin_llinen, johdin_inen, johdin_tOn,
                                           johdin_mAinen, johdin_lAinen>;

define @johtimet_seikkasana_nimisanasta := <johdin_ittAin>;

define @johdin_nimisanasta := @johtimet_nimisana_nimisanasta
                            + @johtimet_laatusana_nimisanasta
                            + @johtimet_seikkasana_nimisanasta
                            + @johdin_jA; #FIXME: onko tämä oikein?


# Johtimet laatusanoista
define @johtimet_nimisana_laatusanasta := <johdin_Us, johdin_UUs>;
define @johtimet_laatusana_laatusanasta := <johdin_nlainen>;
define @johtimet_laatusanasta := @johtimet_nimisana_laatusanasta
                               + @johtimet_laatusana_laatusanasta;


define @johdin := @johdin_teonsanasta + @johdin_nimisanasta + @johtimet_laatusanasta;


combi_rule etuliite ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = etuliite;

  if ($index greater 1) then
    ? etuliite in $vasen.($index - 1).jatko;
    ? yhdyssana_oikein ($vasen, $oikea, $index - 1);
  end;

  if (nimisana in $oikea.jatko or teonsana in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>,
           rules nimisana_teonsanasta;
  end;

  if (kieltosana in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules kieltosana;
  else
    result $vasen + <[alku: $sana] + $oikea>,
           rules etuliite, nimisana, laatusana, teonsana, tavuviiva;
  end;
end;

combi_rule erisnimi ($vasen, $oikea, $sana):
  ? $oikea.luokka in @erisnimi;

  result $vasen + <[alku: $sana] + $oikea>,
         rules erisnimen_sijapääte,
	       omistusliite,
	       liitesana,
	       nimisanan_johdos, # -lainen
	       yhdysviiva_erisnimen_jälkeen,
	       loppu;
end;


combi_rule nimisana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in <nimisana, nimi_laatusana>;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($index greater 1) then
    define $edellinen := $vasen.($index - 1);
    ? (($oikea.luokka in $edellinen.jatko) or
       ((nimisana in $edellinen.jatko) and
        ($oikea.luokka in @nimisana))      or
       ($edellinen.luokka = lukusana)      or
       ($oikea.lähtöluokka = teonsana and teonsana in $edellinen.jatko));

    ? yhdyssana_oikein ($vasen, $oikea, $index - 1);

    if $edellinen.luokka in <laatusana, nimi_laatusana> then
      result $res, rules nimisanan_johdos;
      if $edellinen.luokka = laatusana then stop; end;
    elseif $edellinen.lukutyyppi /= nil then
      result $res,
             rules nimisanan_johdos, nimisana, etuliite, nimisana_laatusanasta, nimisana_teonsanasta, tavuviiva;
      stop;
    end;
  end;

  if (nimisana in $oikea.jatko) then
    result $res, rules inen_päätteinen_laatusana; # Kieli=poliittinen
  end;
  result $res,
         rules sijapääte, omistusliite,
	       nimisana, etuliite,
               liitesana,
               nimisanan_johdos,
               nimisana_laatusanasta, nimisana_teonsanasta,
               tavuviiva,
               loppu;
end;

###### JOHDETTUJEN SANOJEN ALOITUSSÄÄNNÖT ALKAVAT

# Teonsanasta johdettu nimisana, esim. sanoma <- sanoa.
combi_rule nimisana_teonsanasta ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = teonsana;
  define $edellinen := $vasen.($index - 1);
  if ($index greater 1) then
    ? vokaaliehdollinen_yhdysviiva_ok($vasen, $sana);
    ? $edellinen.perusmuoto matches ".*nen" or $edellinen.perusmuoto = $edellinen.alku; # FIXME: esiehto
  end;
  result $vasen + <[alku: $sana] + $oikea>,
         rules teonsanan_johdos, # FIXME: johdetun muodon on oltava nimisana
	       nimitavat;
end;

# Laatusanasta johdettu nimisana, esim. vapaus <- vapaa.
combi_rule nimisana_laatusanasta ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @laatusana;
  define $edellinen := $vasen.($index - 1);
  ? $edellinen.perusmuoto matches ".*nen" or $edellinen.perusmuoto = $edellinen.alku;
  result $vasen + <[alku: $sana] + $oikea>,
         rules laatusanan_johdos; # FIXME: johdetun muodon on oltava nimisana
end;

# Laatusanasta johdettu laatusana, esim. paha+nlai+nen <- paha.
combi_rule laatusana_laatusanasta ($vasen, $oikea, $sana):
  ? $oikea.luokka in @laatusana;
  if (omanto_n in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules johdin_nlainen;
  end;
end;

# inen-johdinta edeltävä nimisana
combi_rule inen_johdos_nimisanasta ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in <nimisana, nimi_laatusana>;
  ? johdin_inen in $oikea.jatko;
  ? yhdyssana_oikein ($vasen, $oikea, $index - 1);

  result $vasen + <[alku: $sana] + $oikea>, rules johdin_inen;
end;

###### JOHDETTUJEN SANOJEN ALOITUSSÄÄNNÖT PÄÄTTYVÄT

###### JOHDINSÄÄNNÖT ALKAVAT

combi_rule johdin_nlainen ($vasen, $oikea, $sana):
  ? $oikea.luokka = johdin_nlainen;
  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
	       liitesana,
	       nimisana, laatusana,
	       laatusanan_johdos,
	       voitto_yliaste,
	       tavuviiva,
	       etuliite,
	       loppu;
end;

combi_rule johdin_inen ($vasen, $oikea, $sana):
  ? $oikea.luokka = johdin_inen;
    result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
	       liitesana,
	       nimisana, laatusana,
	       laatusanan_johdos,
	       voitto_yliaste,
	       tavuviiva,
	       etuliite,
	       loppu;
end;

###### JOHDINSÄÄNNÖT PÄÄTTYVÄT


combi_rule laatusana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @laatusana;

  if ($index greater 1) then
    ? (laatusana in $vasen.($index - 1).jatko);
    ? yhdyssana_oikein ($vasen, $oikea, $index - 1);
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
               liitesana,
               nimisana, laatusana,
               laatusanan_johdos,
               voitto_yliaste,
               tavuviiva,
               etuliite,
               loppu;
end;


combi_rule inen_päätteinen_laatusana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @laatusana;
  ? $oikea.perusmuoto matches ".*inen";

  ? yhdyssana_oikein ($vasen, $oikea, $index - 1);

  # nimisana, laatusana ja etuliite jätetty pois, koska todennäköinen käyttö vähäistä
  # (teoriassa voi olla OK)
  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
	       liitesana,
	       laatusanan_johdos,
	       tavuviiva,
	       loppu;
end;

combi_rule asemosana ($vasen, $oikea, $sana):
  ? $oikea.luokka = asemosana;

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, liitesana, tavuviiva, loppu;
end;


combi_rule lyhennesääntö ($vasen, $oikea, $sana):
  ? $oikea.luokka = lyhenne;

  define $res := $vasen + <[alku: $sana] + $oikea>;
  if (kaksoispiste in $oikea.jatko) then result $res, rules kaksoispiste; end;
  if (tavuviiva in $oikea.jatko) then result $res, rules tavuviiva; end;
  if (loppu in $oikea.jatko) then result $res, rules loppu; end;
end;


combi_rule apusana ($vasen, $oikea, $sana):
  ? $oikea.luokka in <huudahdussana, sidesana>;

  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, tavuviiva, loppu;
end;


combi_rule kaksoispiste ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = kaksoispiste;

  result $vasen + <[alku: $sana] + $oikea + [äs: $vasen.($index - 1).äs]>,
         rules sijapääte;
end;


combi_rule tavuviiva ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = tavuviiva;

  ? (($index = 1) or (tavuviiva in $vasen.($index - 1).jatko));

  result $vasen + <[alku: $sana] + $oikea>,
         rules nimisana, erisnimi, laatusana, asemosana, lyhennesääntö,
               etuliite, teonsana, kieltosana, loppu;
end;


define @sti_edeltäjä := <laatusana, nimi_laatusana, voittoaste, yliaste,
                         johdin_tAvA, johdin_ttAvA, johdin_ttU,
                         johdin_vA, johdin_vAinen, johdin_vE,
                         johdin_stU>
                        + @johdin_nUt + @johdin_tU
                        + @johtimet_laatusana_nimisanasta
                        + @johtimet_laatusana_laatusanasta;

###### SIJAPÄÄTESÄÄNNÖT ALKAVAT

combi_rule erisnimen_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;

  define $edellinen := $vasen.($index - 1);

  ? $oikea.sija in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  if ($oikea.sija in <sisätulento_Vn, sisätulento_hVn>) then
    require (sisätulento_oikein ($oikea.sija, $edellinen.alku, $sana));
  end;

  define $res := $vasen + <[alku: $sana] + $oikea>;
  if ($oikea.sija = kerronto_sti) then
    ? $edellinen.luokka in @sti_edeltäjä;
    result $res, rules liitesana, loppu;
  else
    result $res, rules tavuviiva, omistusliite, liitesana, loppu;
  end;
end;

combi_rule voitto_yliasteen_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;

  define $edellinen := $vasen.($index - 1);

  ? $oikea.sija in $edellinen.jatko;
  ? $oikea.sija /= kerronto_sti or $edellinen.luokka in @sti_edeltäjä;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  if ($oikea.sija in <sisätulento_Vn, sisätulento_hVn>) then
    require (sisätulento_oikein ($oikea.sija, $edellinen.alku, $sana));
  end;

  define $kantasana := $vasen.(length($vasen)-1);
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($oikea.sija = kerronto_sti) then
    ? $edellinen.luokka in @sti_edeltäjä;
    result $res, rules liitesana, loppu;
  else
    result $res, rules omistusliite, liitesana, loppu;
    if (sijamuoto_sijasta($oikea.sija) = omanto and
        ($kantasana.tiedot = nil or
         not (ei_ysa in $kantasana.tiedot and ei_ys in $kantasana.tiedot))) then
      result $res, rules inen_johdos_nimisanasta;
    end;
  end;
end;

###### SIJAPÄÄTESÄÄNNÖT PÄÄTTYVÄT

combi_rule sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;

  define $edellinen := $vasen.($index - 1);
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($edellinen.luokka = kaksoispiste) then
    ? $oikea.sija /= keinonto_n;
    ? ($edellinen.äs = aä) or ($oikea.äs = aä) or ($edellinen.äs = $oikea.äs);
    result $res,
           rules omistusliite, liitesana, loppu;
  else
    ? $oikea.sija in $edellinen.jatko;
    ? ääntiösointu_oikein ($vasen, $oikea, $sana);

    if ($oikea.sija in <sisätulento_Vn, sisätulento_hVn>) then
      require (sisätulento_oikein ($oikea.sija, $edellinen.alku, $sana));
    end;

    if ($oikea.sija = kerronto_sti) then
      ? $edellinen.luokka in @sti_edeltäjä;
      result $res,
             rules liitesana, loppu;
    else
      result $res,
             rules omistusliite, liitesana, loppu;
      if ($edellinen.tiedot = nil or not (ei_ysa in $edellinen.tiedot)) then
        result $res,
               rules nimisana, laatusana, teonsana, tavuviiva;
      end;
    end;
  end;
end;


###### LUKUSANASÄÄNNÖT ALKAVAT

combi_rule lukusana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  define $sijamuoto := nil;
  define $luku := nil;

  if ($oikea.alaluokka = erikoisluku) then
    define $erikoisres := $vasen + <[alku: $sana] + $oikea>;
    if (loppu in $oikea.jatko) then
      result $erikoisres, rules loppu;
    end;
    if (liitesana in $oikea.jatko) then
      result $erikoisres, rules liitesana;
    end;
    stop;
  end;

  if ($index greater 1) then
    define $edellinen := $vasen.($index - 1);
    ? $edellinen.lukutyyppi = nil or $edellinen.lukutyyppi = $oikea.lukutyyppi;
    $sijamuoto := $edellinen.sijamuoto;
    $luku := $edellinen.luku;
    # Estetään peräkkäiset "satasata", "tuhattuhat" jne.
    if ($edellinen.luokka = lukusana) then
      ? not ($oikea.alaluokka = $edellinen.alaluokka);
    end;
  end;
  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, luku: $luku] + $oikea>;

  if ($oikea.alaluokka = yksiyhdeksän) then
    if (nimentö in $oikea.jatko) then
      result $res, rules lukusana_toista, lukusana_sisäkerroin,
                   omistusliite, liitesana,
                   nimisana, inen_päätteinen_laatusana, loppu;
    end;
  elseif ($oikea.alaluokka in <kymmenen, sata>) then
    if (nimentö in $oikea.jatko) then
      result $res, rules lukusana_sisäkerroin,
                   omistusliite, liitesana,
                   nimisana, inen_päätteinen_laatusana, loppu;
    end;
  end;
  if ($oikea.alaluokka in <tuhat, miljoona, miljardi> and nimentö in $oikea.jatko) then
    result $res, rules omistusliite, liitesana,
                 nimisana, inen_päätteinen_laatusana, loppu;
  end;
  if ($oikea.alaluokka in <sata, tuhat>) then
    if (nimentö in $oikea.jatko) then
      result $res, rules lukusana;
    end;
  end;
  result $res, rules lukusana_sijapääte;

end;

combi_rule lukusana_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  define $sijamuoto := sijamuoto_sijasta($oikea.sija);
  ? $edellinen.sijamuoto = nil or $edellinen.sijamuoto = $sijamuoto;
  ? $edellinen.luku = nil or $edellinen.luku = $oikea.luku;

  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  if ($edellinen.alaluokka = yksiyhdeksän) then
    result $res, rules lukusana_toista, lukusana_sisäkerroin,
                 omistusliite, liitesana,
                 nimisana, inen_päätteinen_laatusana, loppu;
  elseif ($edellinen.alaluokka in <kymmenen, sata>) then
    result $res, rules lukusana_sisäkerroin,
                 omistusliite, liitesana, loppu;
  else
    result $res, rules nimisana, inen_päätteinen_laatusana, loppu;
  end;
  if ($edellinen.alaluokka in <sata, tuhat>) then
    result $res, rules lukusana;
  end;
end;

combi_rule lukusana_sisäkerroin ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  ? $oikea.alaluokka in <kymmenen, sata, tuhat, miljoona, miljardi, biljoona>;

  define $edellinen := $vasen.($index - 1);
  ? $edellinen.lukutyyppi = $oikea.lukutyyppi;
  define $res := $vasen + <[alku: $sana, sijamuoto: $edellinen.sijamuoto, luku: $edellinen.luku] + $oikea>;

  if ($edellinen.sijamuoto in <nil, nimentö>) then
    result $res, rules lukusana_sisäkerroin_osanto;
  else
    result $res, rules lukusana_sisäkerroin_sijapääte;
  end;
  
  if ($oikea.lukutyyppi = järjestysluku) then
    result $res, rules lukusana;
  end;
  
  if ($oikea.lukutyyppi = järjestysluku and loppu in $oikea.jatko) then
    result $res, rules omistusliite, liitesana, loppu;
  end;
end;

combi_rule lukusana_sisäkerroin_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  define $sijamuoto := sijamuoto_sijasta($oikea.sija);
  ? $edellinen.sijamuoto = nil or $edellinen.sijamuoto = $sijamuoto;
  ? $edellinen.luku = nil or $edellinen.luku = $oikea.luku;

  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  result $res, rules lukusana,
               omistusliite, liitesana,
               nimisana, inen_päätteinen_laatusana, loppu;
end;

combi_rule lukusana_sisäkerroin_osanto ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? sijamuoto_sijasta($oikea.sija) = osanto;
  ? ääntiösointu_oikein($vasen, $oikea, $sana);
  define $res := $vasen + <[alku: $sana, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;

  result $res, rules lukusana,
               omistusliite, liitesana,
               nimisana, inen_päätteinen_laatusana, loppu;
end;

combi_rule lukusana_toista ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  ? $oikea.alaluokka = toista;
  define $edellinen := $vasen.($index - 1);
  define $res := $vasen + <[alku: $sana, sijamuoto: $edellinen.sijamuoto,
                            luku: $edellinen.luku, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;

  result $res, rules lukusana_sisäkerroin,
                     liitesana,
                     nimisana, inen_päätteinen_laatusana, loppu;
end;

###### LUKUSANASÄÄNNÖT PÄÄTTYVÄT

###### SEIKKA- JA SUHDESANASÄÄNNÖT ALKAVAT

combi_rule seikka_tai_suhdesana ($vasen, $oikea, $sana):
  ? $oikea.luokka in <seikkasana, suhdesana>;
  define $res := $vasen + <[alku: $sana] + $oikea>;
  
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  result $res, rules sijapääte, omistusliite; # FIXME: esiehto testaamatta
end;

###### SEIKKA- JA SUHDESANASÄÄNNÖT PÄÄTTYVÄT

###### YHDYSVIIVASÄÄNNÖT ALKAVAT

combi_rule yhdysviiva_erisnimen_jälkeen ($vasen, $oikea, $sana):
  ? $oikea.luokka = tavuviiva;
  result $vasen + <$oikea>, rules
	 nimisana, erisnimi, laatusana,
         etuliite, teonsana, loppu;
end;

###### YHDYSVIIVASÄÄNNÖT PÄÄTTYVÄT

combi_rule teonsana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = teonsana;

  if ($index greater 1) then
    ? $oikea.luokka in $vasen.($index - 1).jatko;
    ? yhdyssana_oikein ($vasen, $oikea, $index - 1);
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules tekijämuodot, nimitavat,
               liitesana,
               teonsanan_johdos,
               tavuviiva, loppu;
end;


combi_rule tekijämuodot ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @kestämän_tekijäpääte + @tositavan_tekijäpääte_4
                     + @kertoman_tekijäpääte
                     + @ehtotapa + @mahtotapa + @käskytapa;

  define $edellinen := $vasen.($index - 1);
  ? $edellinen.luokka = teonsana;
  ? $oikea.luokka in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);
  ? yhdysteonsana_oikein ($vasen, $index - 1);

  if (($oikea.luokka = kestämän_tekijäpääte_y3)
      and ($edellinen.alku matches ".*" + @ääntiö)) then
    require (viimeinen_kirjain ($edellinen.alku) = $sana);
  end;


  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, loppu;
end;


combi_rule nimitavat ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @nimitapa_1 + @nimitapa_2 + @nimitapa_3 + <nimitapa_4, nimitapa_5>;

  define $edellinen := $vasen.($index - 1);
  ? $edellinen.luokka = teonsana;
  ? $oikea.luokka in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  if ($oikea.luokka in @nimitapa_1 + @nimitapa_2 + @nimitapa_3) then
    ? yhdysteonsana_oikein ($vasen, $index - 1);
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules nimitapa_1_pitkä, sijapääte, omistusliite,
               liitesana,
               nimisana, laatusana, teonsana,
               loppu;
end;


combi_rule nimitapa_1_pitkä ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = nimitapa_1_pitkä;

  define $edellinen := $vasen.($index - 1);
  ? $edellinen.luokka in @nimitapa_1;
  ? nimitapa_1_pitkä in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  result $vasen + <[alku: $sana] + $oikea>, rules omistusliite;
end;


combi_rule kieltosana ($vasen, $oikea, $sana):
  ? $oikea.luokka = kieltosana;

  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, liitesana_kä, tavuviiva, loppu;
end;


combi_rule liitesana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in <liitesana, liitesana_pi, liitesana_s>;

  define $edellinen := $vasen.($index - 1);
  ? $oikea.luokka in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  if (($edellinen.luokka = teonsana) and
      ($edellinen.tapaluokka = käskytapa) and
      ($edellinen.tekijä = 2) and
      ($edellinen.luku = yksikkö)) then
    ? not ($sana in <"ko", "kö">);
  end;

  result $vasen + <[alku: $sana] + $oikea>, rules loppu;
end;


combi_rule liitesana_kä ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = liitesana_kä;

  ? liitesana_kä in $vasen.($index - 1).jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  result $vasen + <[alku: $sana] + $oikea>, rules liitesana, loppu;
end;


combi_rule omistusliite ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = omistusliite;

  define $edellinen := $vasen.($index - 1);
  ? omistusliite in $edellinen.jatko;

  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  define $result := $vasen + <[alku: $sana] + $oikea>;

  if ($index greater 2) then
    ? (not ($vasen.<1,luokka> = asemosana));
  end;

  if ($sana in <"ni", "si", "s", "nsa", "nsä", "mme", "nne">) then
    result $result,
           rules liitesana, loppu;
  else
    if ($edellinen.luokka in <seikkasana, suhdesana> and omistusliite3_oikein ($edellinen.alku, $sana)) then
      if (not (strcat ($vasen) matches ".*(aa|ee|ii|oo|uu|yy|ää|öö)")) then
          result $result,
                 rules liitesana, loppu;
      end;
    elseif ($edellinen.luokka = sijapääte) then
      if (($edellinen.sija in <osanto_A, osanto_iA,
                              osanto_tA, osanto_ttA, osanto_itA,
                              osanto_jA,
                              olento_nA, olento_inA,
                              tulento_ksi, tulento_iksi,
                              sisäolento_ssA, sisäolento_issA,
                              sisäeronto_stA, sisäeronto_istA,
                              ulko_olento_llA, ulko_olento_illA,
                              ulkoeronto_ltA, ulkoeronto_iltA,
                              ulkotulento_lle, ulkotulento_ille,
                              vajanto_ttA, vajanto_ittA,
                              seuranto_ine>) and
          omistusliite3_oikein ($edellinen.alku, $sana)) then
        if (not (strcat ($vasen) matches ".*(aa|ee|ii|oo|uu|yy|ää|öö)")) then
          result $result,
                 rules liitesana, loppu;
        end;
      end;
    elseif (($edellinen.luokka in <nimitapa_1_pitkä, nimitapa_2, nimitapa_3, nimitapa_5>) and
            omistusliite3_oikein ($edellinen.alku, $sana)) then
      result $result,
             rules liitesana, loppu;
    end;
  end;
end;


combi_rule voitto_yliaste ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in <voittoaste, yliaste>;

  define $edellinen := $vasen.($index - 1);

  ? $edellinen.luokka in @laatusana + <johdin_vAinen, johdin_ttAvA, johdin_inen,
                         johdin_tOn, johdin_llinen, johdin_mAinen, johdin_vA>;

  ? (voittoaste in $edellinen.jatko) or (yliaste in $edellinen.jatko);
  ? $oikea.luokka in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  define $res := $vasen + <[alku: $sana] + $oikea>;
  result $res,
         rules voitto_yliasteen_sijapääte, liitesana, omistusliite,
               loppu;
  if ($oikea.luokka = voittoaste) then
    result $res, rules inen_johdos_nimisanasta;
  end;
end;


combi_rule teonsanan_johdos ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @johdin_teonsanasta + <johdin_inen>;

  define $edellinen := $vasen.($index - 1);
  ? $oikea.luokka in $edellinen.jatko;
  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  if ($edellinen.luokka = teonsana) then
    $vasen.($index - 1) :=+ [lähtöluokka: teonsana];
    $vasen.($index - 1).luokka := nimisana;
  end;

  if ($oikea.luokka in <johdin_UUs, johdin_Us, johdin_ntA, johdin_nti, johdin_Us_ksen,
                        johdin_mA> + @johdin_jA) or
     ($oikea.luokka in <johdin_vA, johdin_vE> and $oikea.perusmuoto in <"vuus", "vyys">) then
    result $vasen + <[alku: $sana] + $oikea>, rules nimisana, nimisana_teonsanasta,
                                                    nimisana_laatusanasta;
  end;

  result $vasen + <[alku: $sana] + $oikea>,
          rules sijapääte, omistusliite, liitesana,
                teonsanan_johdos,
                voitto_yliaste,
                etuliite,
                tavuviiva, loppu;
end;


combi_rule nimisanan_johdos ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in @johdin_nimisanasta;

  define $edellinen := $vasen.($index - 1);
  ? $oikea.luokka in $edellinen.jatko;
  if $oikea.luokka = johdin_lAinen then
    ? $edellinen.luokka = paikannimi;
  else
    ? $edellinen.luokka in <nimisana, nimi_laatusana> + @johtimet_nimisana_nimisanasta
      + @johtimet_nimisana_laatusanasta + @johtimet_nimisana_teonsanasta;
  end;

  ? ääntiösointu_oikein ($vasen, $oikea, $sana);
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($oikea.luokka = johdin_inen) then
    result $res,
           rules sijapääte, omistusliite, liitesana,
                 voitto_yliaste, laatusanan_johdos,
                 tavuviiva, loppu;
    stop;
  end;

  result $res,
          rules sijapääte, omistusliite, liitesana,
                nimisana, laatusana, teonsana,
                voitto_yliaste,
                laatusanan_johdos, nimisanan_johdos,
                etuliite,
                tavuviiva,
                loppu;
end;


combi_rule laatusanan_johdos ($vasen, $oikea, $sana, $index):
# Laatusana voi olla myös nimi_laatusana, siksi @johdin_nimisanasta.
  ? $oikea.luokka in @johtimet_nimisana_laatusanasta + @johdin_nimisanasta;

  if ($index greater 1) then
    define $edellinen := $vasen.($index - 1);
    ? $oikea.luokka in $edellinen.jatko;

    # Ilman tätä kulkea => kulki+ja+tar -johdokset tulevat kahdesti.
    if ($edellinen.luokka in @johdin_jA) then
      ? $oikea.luokka /= johdin_tAr;
    end;
  end;

  ? ääntiösointu_oikein ($vasen, $oikea, $sana);

  result $vasen + <[alku: $sana] + $oikea>,
          rules sijapääte, omistusliite, liitesana,
                nimisana, laatusana, teonsana,
                laatusanan_johdos, nimisanan_johdos,
                voitto_yliaste,
                etuliite,
                tavuviiva, loppu;
end;


subrule strcat ($vasen):
  define $s := "";

  foreach $k in $vasen:
    $s :=+ $k.alku;
  end;

  return $s;
end;


subrule sisätulento_Vn_oikein ($sija, $alku, $sana):
  return (($sija = sisätulento_Vn)
          and ((($alku matches ".*" + @a) and ($sana matches "an?")) or
               (($alku matches ".*" + @e) and ($sana matches "en?")) or
               (($alku matches ".*" + @i) and ($sana matches "in?")) or
               (($alku matches ".*" + @o) and ($sana matches "on?")) or
               (($alku matches ".*" + @u) and ($sana matches "un?")) or
               (($alku matches ".*" + @y) and ($sana matches "yn?")) or
               (($alku matches ".*" + @ä) and ($sana matches "än?")) or
               (($alku matches ".*" + @ö) and ($sana matches "ön?"))));
end;


subrule sisätulento_hVn_oikein ($sija, $alku, $sana):
  return (($sija = sisätulento_hVn)
          and ((($alku matches ".*" + @a) and ($sana matches "han?")) or
               (($alku matches ".*" + @e) and ($sana matches "hen?")) or
               (($alku matches ".*" + @i) and ($sana matches "hin?")) or
               (($alku matches ".*" + @o) and ($sana matches "hon?")) or
               (($alku matches ".*" + @u) and ($sana matches "hun?")) or
               (($alku matches ".*" + @y) and ($sana matches "hyn?")) or
               (($alku matches ".*" + @ä) and ($sana matches "hän?")) or
               (($alku matches ".*" + @ö) and ($sana matches "hön?")) or
               (($alku matches ".*'")))); # Parfait'hen yms.
end;


subrule sisätulento_oikein ($sija, $alku, $sana):
  return (sisätulento_Vn_oikein  ($sija, $alku, $sana) or
          sisätulento_hVn_oikein ($sija, $alku, $sana) or
          ($sija = sisätulento_seen) or
          ($sija = sisätulento_isiin));
end;


subrule ääntiösointu_oikein ($vasen, $oikea, $sana):
  define $äs := "?";

  # Etu- tai takaääntiöys määräytyy yhdyssanan viimeisen
  # osan mukaan, esimerkiksi: hevose+sta, miehe+stä, hevos+miehe+stä.
  #
  foreach $j in $vasen:
    if (value_type ($j) = list) then
      foreach $i in $j:
        if ($i.perusmuoto /= nil) then
          $äs := $i.äs;
        end;
      end;
    elseif ($j.perusmuoto /= nil) then
      $äs := $j.äs;
    elseif ($j.luokka = lyhenne) then
      $äs := $j.äs;
    end;
  end;

#define $ä1 := transmit (<"ä1"> + <$vasen>);
#define $ä2 := transmit (<"ä2"> + <$oikea>);
#define $ä3 := transmit (<"ä3"> + <$sana>);
#define $ä4 := transmit (<"ä4"> + <$äs> + <$oikea.äs>);

  if ($äs = aä) then
    return yes;
  elseif ($äs = a) then
    return ($oikea.äs in <a, aä>);
  elseif ($äs = ä) then
    return ($oikea.äs in <ä, aä>);
  else
    define $a := transmit (<$äs> + <$oikea.äs>);
    error "subrule ääntiösointu_oikein: muuttujalla $äs tai $oikea.äs on väärä arvo.";
  end;
end;


# Hyväksytäänkö teonsanat tyyppiä "ohjelma+listata".
#
subrule yhdysteonsana_oikein ($vasen, $i):
# Ota kommentti pois seuraavan rivin alusta, jos haluat hyväksyä yhdysteonsanat.
#  return yes;

  if ($i greater 1) then
    foreach $k in $vasen:
      if (not ($k.luokka in <teonsana, etuliite>)) then
        stop;
      end;
    end;
  end;
  return yes;
end;


subrule omistusliite3_oikein ($sana, $liite):
  return ((($sana matches ".*" + @a) and ($liite = "an")) or
          (($sana matches ".*" + @e) and ($liite = "en")) or
          (($sana matches ".*" + @i) and ($liite = "in")) or
          (($sana matches ".*" + @o) and ($liite = "on")) or
          (($sana matches ".*" + @u) and ($liite = "un")) or
          (($sana matches ".*" + @y) and ($liite = "yn")) or
          (($sana matches ".*" + @ä) and ($liite = "än")) or
          (($sana matches ".*" + @ö) and ($liite = "ön")));
end;


subrule viimeinen_kirjain ($sana):
  if     ($sana matches ".*" + @a) then return "a";
  elseif ($sana matches ".*" + @e) then return "e";
  elseif ($sana matches ".*" + @i) then return "i";
  elseif ($sana matches ".*" + @o) then return "o";
  elseif ($sana matches ".*" + @u) then return "u";
  elseif ($sana matches ".*" + @y) then return "y";
  elseif ($sana matches ".*" + @ä) then return "ä";
  elseif ($sana matches ".*" + @ö) then return "ö";
  else
    error "Viimeinen kirjain ei ole ääntiö sanassa " + $sana;
  end;
end;



subrule yhdyssana_oikein ($vasen, $oikea, $i):
  # Erisnimet eivät voi olla yhdyssanojen osana.
  #
  define $nimet := <etunimi, sukunimi, nimi>;

  if (switch (malli) = voikko) then
    $nimet := <etunimi, sukunimi, paikannimi, nimi>;
    #
    # Yhdysviivan käsittely
    #
    if ($i greater_equal 2) and ($vasen.$i.luokka = tavuviiva) then

      # Yhdysviiva aina sallittu erisnimien ja lyhenteiden jälkeen
      if ($vasen.($i - 1).luokka in $nimet + <lyhenne, huudahdussana>) then
        return yes;

      # Yhdysviiva sallittu, kun alkuosa loppuu samaan vokaaliin jolla loppuosa alkaa
      elseif
             ((($vasen.($i - 1).alku) matches ".*" + @ääntiö) and
              (last($vasen.($i - 1).alku) = first($oikea.perusmuoto))) then
        return yes;

      # "Ruotsin-laiva"
      elseif ($i greater_equal 3) and ($vasen.($i - 1).luokka = sijapääte) and
             ($vasen.($i - 1).sija = omanto_n) and ($vasen.($i - 2).luokka in $nimet) then
        return yes;

      # is- ("teknillis-tieteellinen", "Kaakkois-Suomi")
      # FIXME: tämä ei ole tarkalleen ottaen oikein, mutta toistaiseksi riittävän hyvä
      elseif (($vasen.($i - 1).alku) matches ".*is") then
        return yes;

      end;
      # Yhdysviiva muutoin kielletty
      return no;

    end;
  elseif (($i greater_equal 2) and
          ($vasen.$i.luokka = tavuviiva) and
          (($vasen.($i - 1).luokka in $nimet) or ($oikea.luokka in $nimet))) then
    #
    # Hyväksytään kaksiosainen erisnimi ("Anna-Mari") tai erisnimi viivan
    # kanssa yhdyssanassa ("Anna-niminen").
    #
    return yes;
  end;


  if ($oikea.luokka in $nimet) then
    return no;
  end;


  # Jotkut sanat eivät voi olla yhdyssanojen osina.
  #
  if ((($vasen.$i.tiedot /= nil) and ((ei_ys in $vasen.$i.tiedot) or (latex in $vasen.$i.tiedot))) or
      (($oikea.tiedot    /= nil) and ((ei_ys in $oikea.tiedot)    or (latex in $oikea.tiedot)))) then
    return no;
  end;
  # Ei hyväksytä tälläisia sanoja silloinkaan, kun sijamuoto on joku muu kuin nimentö (isi+n+maa ei käy).
  #
  if (($vasen.$i.luokka = sijapääte) and ($vasen.($i-1).tiedot /= nil) and (ei_ys in $vasen.($i-1).tiedot)) then
    return no;
  end;

  if (($oikea.luokka = etuliite) and
          ($oikea.perusmuoto = "uus") and
          ($vasen.$i.perusmuoto /= nil)) then
    if ($vasen.$i.perusmuoto = "jumal") then
      return no;     # Ei jumal+uus+oppi.
    else
      return (not ($vasen.$i.perusmuoto matches ".*inen"));
    end;
  elseif (($vasen.$i.tiedot /= nil) and (ei_ysa in $vasen.$i.tiedot) and
          ($oikea.luokka in @nimisana + <nimi_laatusana>)) then
    return no;
  else
    define $taulukko := <
      <<"alas">,            <"suka", "sukia">>,
      <<"alko">,            <"iva", "ivata">>,
      <<"anti">,            <"lainata">>,
      <<"auto">,            <"nova">>,  # Auto+novien.
      <<"de">,              <"lego", "mono", "sika">>,  # Ei de+lego+iva, de+mono+logi, geo+de+siassa.
      <<"eri", "esi">,      <"koi">>,   # Eri+koi+sala, esi+kois- (esi + koi + inen).
      <<"esikoinen">,       <"uusi">>,  # Esikois+uutensa (esikois + uusi).
      <<"funktio">,   <"naali">>,
      <<"hai">,       <"talli">>,    # Hai+tallinen.
      <<"hara">,      <"voi", "voida", "vuo">>,
      <<"harras">,    <"teli">>,
      <<"heinä">,     <"tupo">>,
      <<"helve">,     <"tilli">>,
      <<"hyppy">,     <"selli">>,
      <<"hätä">,      <"katka">>,
      <<"isä">,       <"nisä">>,
      <<"itse">,      <"matto", "tie">>,
      <<"iva">,       <"nova">>,
      <<"jumal">,     <"uus">>,
      <<"kalla">,     <"vesi">>,
      <<"kapi">,      <"nalli">>,  # Kapi+nallinen.
      <<"kare">,      <"lika">>,
      <<"karri">,     <"ääri">>,
      <<"kato">,      <"lila">>,
      <<"koko">,      <"nainen">>,      # Koko+nais+valtainen.
      <<"koti", "kotiin">, <"pää">>,    # Kotiinpäin.
      <<"kulti">,          <"voi", "vuo">>,
      <<"kuu">,            <"kausittainen", "luksus", "luminen">>,
      <<"loinen">,    <"tee", "tehdä">>,
      <<"maa">,       <"nisä">>,
      <<"maagi">,     <"seksi", "silta">>,
      <<"mansi">,     <"kastaa", "koi", "koittaa">>,
      <<"moni">,      <"naida", "nainen", "tora", "tori">>,
      <<"onninen">,   <"tuttu">>, # onnistuttu
      <<"oppinen">,   <"ääntö">>, # oppisääntö
      <<"pois">,      <"pää", "taka", "tuki", "tutti", "tuttu">>,
      <<"punk">,      <"arka", "kari">>,
      <<"polvinen">,  <"tuki">>, # polvistuin
      <<"priori">,    <"soida">>,
      <<"pyro">,      <"maa">>,
      <<"pyy", "tie">, <"täi">>,  # Pyy+täisin (pyy + täi + inen), tie+täisin.
      <<"päällys">,   <"tie">>,   # Päällysteinen (päällys + tie + inen).
      <<"samainen">,  <"tuta">>,
      <<"soinen">,    <"aari">>,
      <<"sota", "säihky">, <"väkä">>,
      <<"suo">,       <"musti", "nina">>,
      <<"suu">,       <"tee", "tehdä", "teloa">>,
      <<"sävel">,     <"eittää">>,
      <<"taulu">,     <"koi", "koittaa">>,
      <<"taus">,      <"takki">>,
      <<"tie">,       <"tee", "tie", "toinen", "tokko">>,
      <<"traditio">,  <"naali">>,
      <<"ulko">,      <"naida", "nainen">>,
      <<"uus">,       <"arpoa">>,
      <<"vasta">,     <"pää">>,
      <<"viher">,     <"vuotaa">>,
      <<"viive">,     <"itä">>,
      <<"voinen">,    <"iva", "ivata">>,    # Voisivatko => vois+iva(ta).
      <<"vyö">,       <"täi">>,             # Vyö+täinen.
      <<"yhtä", "yksi">, <"toinen", "toistaa">>
    >;

    if (yhdyssanataulukko ($vasen.$i.perusmuoto,
                           $oikea.perusmuoto,
                           $taulukko)) then
      return no;
    elseif ($vasen.$i.perusmuoto /= nil) then
      if ((last($vasen.$i.perusmuoto) matches @ääntiö) and
          (first($oikea.perusmuoto) matches @ääntiö)) then
        return (last($vasen.$i.perusmuoto) /= first($oikea.perusmuoto));
      end;
    end;
  end;
  return yes;
end;


subrule yhdyssanataulukko ($eka, $toka, $taulukko):
  foreach $k in $taulukko:
    if (($eka in $k.1) and ($toka in $k.2)) then
      return yes;
    end;
  end;
  return no;
end;


#####################################################


end_rule loppu ($tietue):
  define $i := length ($tietue);
  ? loppu in $tietue.$i.jatko;

  define $n := $i;
  if ($tietue.$i.luokka in <liitesana, liitesana_s, omistusliite>) then
    $n := $i - 1;
  end;
  if (not (teonsana_ok ($tietue, $n))) then
    stop;
  end;


  define $j := $n;
  repeat while ($tietue.$j.luokka in <sijapääte, omistusliite>);
    $j :=- 1;
  end;


  # Jotkut sanat eivät voi olla yhdyssanan jälkiosana.
  #
  foreach $u in length($tietue):
    if (($u greater 1) and ($u less_equal $j) and
        ($tietue.$u.tiedot /= nil) and (ei_ysj in $tietue.$u.tiedot)) then
      stop;
    end;
  end;


  # inen-johtimella tehty sana voi olla vain yhdyssanan lopussa,
  # paitsi jos se on merkitty omaksi sanakseen.
  #
  repeat while ($tietue.$j.luokka in @johtimet_nimisana_laatusanasta + @johdin_nimisanasta - <johdin_inen>);
    $j :=- 1;
  end;
  define $k := $j;
  repeat
    if ($tietue.$k.luokka = johdin_inen) then
      if ($k = 2) then
        if (($tietue.($k-1).tiedot /= nil) and (inen in $tietue.($k-1).tiedot)) then
        else
          stop;
        end;
      end;
    end;
    $k :=- 1;
  while ($k greater 1);
  end;


  # Jotkut sanat voivat olla vain yhdyssanojen jälkiosana.
  #
  if ((($tietue.1).tiedot /= nil) and (ysj in ($tietue.1).tiedot)) then
    stop;
  end;


  # Isä-sanan monikon muodot, paitsi nimentö, eivät voi olla yhdyssanan
  # jälkiosana. Esim. valaisin ei ole vala+isin, lukuisiin ei ole luku+isiin,
  # viimeisillään ei ole viime+isillään, mutta kanta+isät on oikein.
  #
  if (($n greater 2) and
      ($tietue.($n-1).perusmuoto = "isä") and
      ($tietue.$n.luokka = sijapääte) and
      ($tietue.$n.luku = monikko) and
      ($tietue.$n.sija /= nimentö_t)) then
    stop;
  end;


  # Ei heitto+is+tu+in eli =heittois=tuki (heitto+inen + tuki).
  #
  if (($n greater 3) and
      ($tietue.($n-1).perusmuoto = "tuki") and
      ($tietue.($n-2).luokka = johdin_inen) and
      ($tietue.$n.luokka = sijapääte) and
      ($tietue.$n.sija = keinonto_in)) then
    stop;
  end;


  # Ei palo+muur+is+ääntö eli =palo=muuris=ääntö (palo + muur+inen + ääntö).
  #
  if (($n greater 2) and
      ($tietue.$n.perusmuoto = "ääntö") and
      ($tietue.($n-1).luokka = johdin_inen)) then
    stop;
  end;


  result $tietue, accept;
end;


# Tarkistetaan, että teonsana on oikein.
#
# Jos yhdyssana loppuu käskytavan yksikön toisen tai kolmannen tekijän
# kestämään [1] ja sanan alkuosa on nimisana, laatusana, etuliite tai
# seikkasana, sana ei ole oikea teonsana, eikä myöskään sana, joka loppuu
# tositavan kestämän tai kertoman yksikon ensimmäiseen tai kolmanteet tekijään,
# tai käskytavan kestämän monikon toiseen kieltomuotoon (esim. älkää äidinkielikö).
#
# [1] Toisen tekijän kestämällä ei ole päätettä, jolla se voitaisiin
#     tunnistaa, vaan se on sama kuin teonsanan vartalo.
#
# Esim. nämä eivät ole oikein:
#    alkuperäisestä -> alku=peräis=estä (alku+peräinen+estää)
#    hairahtaa      -> hai=rahtaa
#    tappion        -> tappi=on (tappi+olla)
#    ensiökäämi, ensiökäämin -> ensiö=käämiä
#    myötätuuli   -> myötä=tuulla
#    äidinkieli   -> äidin=kieliä
#    äidinkielikö -> (älkää) äidin=kielikö
#
subrule teonsana_ok ($tietue, $i):
  if (ok6 ($tietue.$i, teonsana,                käskytapa, kestämä, yksikkö, 2) or
      ok6 ($tietue.$i, teonsana,                tositapa,  kestämä, yksikkö, 3) or
      ok6 ($tietue.$i, kestämän_tekijäpääte_y1, tositapa,  kestämä, yksikkö, 1) or
      ok6 ($tietue.$i, kertoman_tekijäpääte_y1, tositapa,  kertoma, yksikkö, 1) or
      ok6 ($tietue.$i, kertoman_tekijäpääte_y3, tositapa,  kertoma, yksikkö, 3) or
      ok4 ($tietue.$i, käskytapa_kielto,        käskytapa, kestämä)) then
   foreach $k in $tietue:
      if ($k.luokka in @nimisana + <laatusana, seikkasana>
                       + @johdin_teonsanasta) then
        return no;
      end;
      if ($k.luokka = etuliite and not teonsana in $k.jatko) then
        return no;
      end;
    end;
  end;
  return yes;
end;


subrule ok4 ($tietue, $luokka, $tapaluokka, $aikamuoto):
  return (($tietue.luokka = $luokka) and
          ($tietue.tapaluokka = $tapaluokka) and
          ($tietue.aikamuoto = $aikamuoto));
end;


subrule ok6 ($tietue, $luokka, $tapaluokka, $aikamuoto, $luku, $tekijä):
  return (ok4 ($tietue, $luokka, $tapaluokka, $aikamuoto) and
          ($tietue.luku = $luku) and
          ($tietue.tekijä = $tekijä));
end;



output_filter tulosta ($tulos):
  foreach $j in length ($tulos):
    result tulostus_voikko($tulos.$j);
  end;
end;


subrule tulostus_voikko ($tietue):
  define $tuloste := "";
  define $tlen := length($tietue);
  define $tind := 0;
  foreach $t in $tietue:
    $tind :=+ 1;
    if $t.rakenne /= nil then
      $tuloste :=+ $t.rakenne;
    elseif $t.luokka in <nimisana, seikkasana, laatusana, teonsana, lukusana, huudahdussana, sidesana,
                         nimi_laatusana, asemosana, suhdesana,
                         etuliite> then
      $tuloste :=+ "=";
      foreach $i in length($t.alku):
        $tuloste :=+ "p";
      end;
    elseif $t.luokka in <etunimi, sukunimi, nimi, paikannimi> then
      $tuloste :=+ "=";
      if ($t.luokka = paikannimi and $tlen greater $tind and $tietue.($tind+1).luokka = johdin_lAinen) then
        foreach $i in length($t.alku):
	  $tuloste :=+ "p";
	end;
      else
        $tuloste :=+ "i";
        foreach $i in length($t.alku) - 1:
          $tuloste :=+ "p";
        end;
      end;
    elseif $t.luokka = lyhenne then
      $tuloste :=+ "=";
      foreach $i in length($t.alku):
        $tuloste :=+ "q";
      end;
    elseif $t.luokka = tavuviiva then
      $tuloste :=+ "-";
    elseif $t.luokka = kaksoispiste then
      $tuloste :=+ ":";
    else
      foreach $i in length($t.alku):
        $tuloste :=+ "p";
      end;
    end;
  end;
  return $tuloste;
end;


pruning_rule prune ($list):
  define $filter := <>;
  foreach $i in length($list):
    $filter :=+ <($i less_equal 20)>;
  end;
  return $filter;
end;

