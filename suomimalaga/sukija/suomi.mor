# Suomi-malaga, suomen kielen muoto-opin kuvaus.
#
# Tekijänoikeus © 2006-2008 Hannu Väisänen (Etunimi.Sukunimi@joensuu.fi)
#
# Tämä ohjelma on vapaa; tätä ohjelmaa on sallittu levittää
# edelleen ja muuttaa GNU yleisen lisenssin (GPL lisenssin)
# ehtojen mukaan sellaisina kuin Free Software Foundation
# on ne julkaissut; joko Lisenssin version 2, tai (valinnan
# mukaan) minkä tahansa myöhemmän version mukaisesti.
#
# Tätä ohjelmaa levitetään siinä toivossa, että se olisi
# hyödyllinen, mutta ilman mitään takuuta; ilman edes
# hiljaista takuuta kaupallisesti hyväksyttävästä laadusta tai
# soveltuvuudesta tiettyyn tarkoitukseen. Katso GPL
# lisenssistä lisää yksityiskohtia.
#
# Tämän ohjelman mukana pitäisi tulla kopio GPL
# lisenssistä; jos näin ei ole, kirjoita osoitteeseen Free
# Software Foundation Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
#
# Tämän ohjeman linkittäminen staattisesti tai dynaamisesti
# muihin moduuleihin on ohjelmaan perustuvan teoksen
# tekemistä, joka on siis GPL lisenssin ehtojen alainen.
#
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; see the file COPYING.  If not, write to the
# Free Software Foundation, Inc., 59 Temple Place -  Suite 330, Boston, MA
# 02111-1307 USA.
#
# Linking this program statically or dynamically with other modules is
# making a combined work based on this program.  Thus, the terms and
# conditions of the GNU General Public License cover the whole
# combination.

initial <>, rules laatusana,
                  nimi_laatusana,
                  lukusana,
#                  paikannimen_ja_sukunimen_lainen_johdos,
                  sanan_alku;

include "suomi.inc";

define @teonsana := <teonsana, kieltosana>;

define @ehtotapa := <ehtotapa, ehtotapa_ttA, ehtotapa_tA>;

define @mahtotapa := <mahtotapa_le, mahtotapa_ne, mahtotapa_re, mahtotapa_se,
                      mahtotapa_le_kielto, mahtotapa_ne_kielto,
                      mahtotapa_re_kielto, mahtotapa_se_kielto,
                      mahtotapa_ttA, mahtotapa_tA>;

define @käskytapa := <käskytapa, käskytapa_kielto, käskytapa_ttA,
                      käskytapa_tA>;

define @nimitapa_1 := <nimitapa_1_A,  nimitapa_1_dA, nimitapa_1_lA,
                       nimitapa_1_nA, nimitapa_1_rA, nimitapa_1_tA>;
define @nimitapa_2 := <nimitapa_2, nimitapa_2_ttA, nimitapa_2_tA>;
define @nimitapa_3 := <nimitapa_3, nimitapa_3_saama, nimitapa_3_ttA, nimitapa_3_tA>;

define @teonsanan_taivutusmuodot :=
       @tositapa + @ehtotapa + @mahtotapa + @käskytapa +
       @nimitapa_1 + @nimitapa_2 + @nimitapa_3 +
       @laatutapa_1 + @laatutapa_2;


define @voittoaste := <voittoaste>;
define @yliaste    := <yliaste>;

define @johdin_U := <johdin_U_arvelu>;

define @johdin_teonsanasta :=
       <johdin_tOn, johdin_UUs, johdin_Us, johdin_ntA, johdin_nti,
        johdin_Us_ksen, johdin_vA, johdin_vE, johdin_vAinen,
        johdin_lAinen, johdin_mAinen, johdin_llinen, johdin_ittAin,
        johdin_nA, johdin_O, johdin_Os,
        johdin_tAr>  # Näyttelijätär.
       + @johdin_U
       + @johdin_jA
       + @johdin_mA
       + @johdin_tAvA
       + @johdin_laatutapa;

define @johdin_nimisanasta := <johdin_ittAin, johdin_inen,
                               johdin_tOn, johdin_tAr, johdin_Us, johdin_UUs>
                              + @johdin_jA;

define @johdin_laatusanasta := <johdin_nlainen, johdin_Us, johdin_UUs>;


define @johdin := @johdin_teonsanasta + @johdin_nimisanasta + @johdin_laatusanasta;


# Sääntö sanan_alku on muokattu Harri Pitkäsen Voikko-versiosta.
#
combi_rule sanan_alku ($vasen, $oikea, $sana):
#  define $a := transmit ($vasen);
#  define $b := transmit ($oikea);
#  define $c := transmit ($sana);

  if $oikea.luokka = lyhenne then
    if (kaksoispiste in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules kaksoispiste;
    end;
    if (tavuviiva in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules tavuviiva;
    end;
    if (loppu in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules loppu;
    end;
  elseif $oikea.luokka in <huudahdussana, sidesana> then
    if (tavuviiva in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules tavuviiva;
    end;
    if (loppu in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules loppu;
    end;
    result <[alku: $sana] + $oikea>,
           rules liitesana;
  elseif $oikea.luokka = etuliite then
    define $r := <[alku: $sana] + $oikea>;
    if (kieltosana in $oikea.jatko) then
      result $r, rules kieltosana;
    end;
    if (nimisana in $oikea.jatko) then
      result $r, rules nimisana, teonsanasta_johdettu_nimisana,
                       paikannimen_ja_sukunimen_lainen_johdos;
    end;
    if (laatusana in $oikea.jatko) then
      result $r, rules laatusana, teonsanasta_johdettu_laatusana;
    end;
    if (nimi_laatusana in $oikea.jatko) then
      result $r, rules nimi_laatusana;
      if (not (nimisana in $oikea.jatko)) then
        result $r, rules nimisana, teonsanasta_johdettu_nimisana;
      end;
      if (not (laatusana in $oikea.jatko)) then
        result $r, rules laatusana, teonsanasta_johdettu_laatusana;
      end;
    end;
#    if (teonsanan_johdoksen_etuliite in $oikea.jatko) then
#      result $r, rules teonsanasta_johdettu_nimisana,
#                       teonsanasta_johdettu_laatusana;
#    end;
    if (teonsana in $oikea.jatko) then
      result $r, rules teonsana;
##                       teonsanasta_johdettu_nimisana,
##                       teonsanasta_johdettu_laatusana;
    end;
    if (etuliite in $oikea.jatko) then
      result $r, rules etuliite;
    end;
    if (lukusana in $oikea.jatko) then
      result $r, rules lukusana;
    end;
    # Tavuviiva on aina jatkossa eli sitä ei tarvitse testata.
    result $r, rules tavuviiva;
  elseif ($oikea.luokka = teonsana) then
    define $r := <[alku: $sana] + $oikea>;
    if (liitesana in $oikea.jatko) then
      result $r, rules liitesana;
    end;
    if (liitesana2 in $oikea.jatko) then
      result $r, rules liitesana2;
    end;
    if (loppu in  $oikea.jatko) then
      result $r, rules loppu;
    end;
    if (tavuviiva in  $oikea.jatko) then
      result $r, rules tavuviiva;
    end;
    result $r, rules tekijämuodot, nimitavat, laatutavat,
               teonsanan_johdos_nimisana, teonsanan_johdos_laatusana;
  elseif ($oikea.luokka = tavuviiva) then
    result <[alku: $sana] + $oikea>,
           rules nimisana, laatusana, asemosana, apusana, lyhennesääntö,
                 etuliite, teonsana, kieltosana, loppu;
  elseif $oikea.luokka = kieltosana then
    result <[alku: $sana] + $oikea>,
           rules liitesana, liitesana_kä, tavuviiva, loppu;
  elseif $oikea.luokka = asemosana then
    result <[alku: $sana] + $oikea>,
           rules asemosanan_sijapääte, liitesana, tavuviiva, voitto_yliaste, loppu;
 #          rules sijapääte, liitesana, tavuviiva, voitto_yliaste, loppu;
  elseif $oikea.luokka in <seikkasana, suhdesana> then
    if (loppu in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules loppu;
    end;
    if (liitesana in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules liitesana;
    end;
    if (omistusliite in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules omistusliite;
    end;
    if (tavuviiva in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules tavuviiva;
    end;
#define $a := transmit (<"ö"> + <$oikea> + <$sana>);
    result <[alku: $sana] + $oikea>, rules sijapääte;
  elseif $oikea.luokka = nimisana then
#    ? $oikea.tiedot = nil or not ysj in $oikea.tiedot;

    if (tavuviiva in $oikea.jatko) then
if (1 = 1) then
      result <[alku: $sana] + $oikea>, rules yhdyssana;
else
      result <[alku: $sana] + $oikea>,
             rules nimisana,
#                   yhdyssana,
                   laatusana,
                   nimi_laatusana,
                   teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
                   paikannimen_ja_sukunimen_lainen_johdos,
                   tavuviiva,
                   etuliite;
end;
    end;
    if (@nimisanasta_johdettu_laatusana * $oikea.jatko /= <>) then
      result <[alku: $sana] + $oikea>, rules nimisanasta_johdettu_laatusana;
    end;
    if (@nimisanasta_johdettu_nimisana * $oikea.jatko /= <>) then
      result <[alku: $sana] + $oikea>, rules nimisanasta_johdettu_nimisana;
    end;
    if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
      result <[alku: $sana] + $oikea>, rules nimi_laatusanan_johdin;
    end;
    if (omistusliite in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules omistusliite;
    end;
    if (liitesana in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules liitesana;
    end;
    if (liitesana2 in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules liitesana2;
    end;
    if (loppu in $oikea.jatko) then
      result <[alku: $sana] + $oikea>, rules loppu;
    end;
#define $b := transmit (<"a"> + <$oikea>);
#define $c := transmit (<"b"> + <$sana>);
    result <[alku: $sana] + $oikea>, rules sijapääte;
  elseif $oikea.luokka in @erisnimi then
    define $res := $vasen + <[alku: $sana] + $oikea>;
    if (omistusliite in $oikea.jatko) then
      result $res, rules omistusliite;
    end;
    if (liitesana in $oikea.jatko) then
      result $res, rules liitesana;
    end;
    if (tavuviiva in $oikea.jatko) then
      result $res, rules tavuviiva;
    end;
    if (loppu in $oikea.jatko) then
      result $res, rules loppu;
    end;
    if ((johdin_lAinen in $oikea.jatko) and ($oikea.luokka in <paikannimi, sukunimi>)) then
      result $res, rules paikannimen_ja_sukunimen_lainen_johdin;
    end;
    result $res, rules erisnimen_sijapääte;
  end;
#define $b := transmit (<"c"> + <$oikea>);
#define $c := transmit (<"d"> + <$sana>);
  stop;
end;


combi_rule etuliite ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = etuliite;

#define $a := transmit ($vasen);
#define $b := transmit (<$oikea> + <$sana>);

  define $i := $n - 1;

assert ($i = length($vasen));

  ? ($oikea.luokka in $vasen.$i.jatko);
  ? yhdyssana_oikein ($vasen, $oikea, $i);


  define $r := $vasen + <[alku: $sana] + $oikea>;

  if (kieltosana in $oikea.jatko) then
    result $r, rules kieltosana;
  end;
  if (nimisana in $oikea.jatko) then
    result $r, rules nimisana, teonsanasta_johdettu_nimisana,
                     paikannimen_ja_sukunimen_lainen_johdos;
  end;
  if (laatusana in $oikea.jatko) then
    result $r, rules laatusana, teonsanasta_johdettu_laatusana;
  end;
  if (nimi_laatusana in $oikea.jatko) then
    result $r, rules nimi_laatusana;
    if (not (nimisana in $oikea.jatko)) then
      result $r, rules nimisana, teonsanasta_johdettu_nimisana;
    end;
    if (not (laatusana in $oikea.jatko)) then
      result $r, rules laatusana, teonsanasta_johdettu_laatusana;
    end;
  end;
  if (teonsana in $oikea.jatko) then
    result $r, rules teonsana;
  end;
  if (etuliite in $oikea.jatko) then
    result $r, rules etuliite;
  end;
  if (lukusana in $oikea.jatko) then
    result $r, rules lukusana;
  end;

  # Tavuviiva on aina jatkossa eli sitä ei tarvitse testata.
  #
  result $r, rules tavuviiva;
end;


combi_rule nimisana ($vasen, $oikea, $sana, $n):
  define $i := $n - 1;

assert ($i = length($vasen));

  # Esim. (koillis)puolitse, (maan)teitse, (sähkö)postitse.
  #
  if ($oikea.luokka = seikkasana and $oikea.tiedot /= nil and
      ys_perusosa in $oikea.tiedot and $vasen /= <>) then

    if (($vasen.$i.luokka = sijapääte and $vasen.$i.sija = omanto_n)
        or (($vasen.$i.perusmuoto /= nil)
            and (($vasen.$i.perusmuoto = $vasen.$i.alku)
                 or (($vasen.$i.perusmuoto matches ".*inen") and ($vasen.$i.alku matches ".+is") and
                     (not ($vasen.$i.luokka in <johdin_llinen, johdin_inen>)))))) then


      if (loppu in $oikea.jatko) then
        result $vasen + <[alku: $sana] + $oikea>, rules loppu;
      end;
      if (liitesana in $oikea.jatko) then
         result $vasen + <[alku: $sana] + $oikea>, rules liitesana;
      end;
      if (omistusliite in $oikea.jatko) then
        result $vasen + <[alku: $sana] + $oikea>, rules omistusliite;
      end;
      result $vasen + <[alku: $sana] + $oikea>, rules sijapääte;
    end;
  end;

  ? $oikea.luokka = nimisana;
##  ? $oikea.luokka in <nimisana, lukusana>;

#define $n1 := transmit (<"nimisana1"> + <$vasen.$i.jatko>);
#define $n2 := transmit (<"nimisana2"> + <$oikea.luokka>);
#define $n3 := transmit (<"nimisana3"> + <$sana>);
#define $n4 := transmit (<"nimisana4"> + <($oikea.luokka in $vasen.$i.jatko)>);

  ? (($oikea.luokka in $vasen.$i.jatko) or
     (teonsanan_johdoksen_etuliite in $vasen.$i.jatko) or
     ((nimisana in $vasen.$i.jatko) and ($oikea.luokka in @nimisana + @lukusana)));

  ? yhdyssana_oikein ($vasen, $oikea, $i);

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (tavuviiva in $oikea.jatko) then
if (1 = 1) then
    result $res, rules yhdyssana;
else
    result $res,
           rules nimisana,
                 laatusana,
                 nimi_laatusana,
                 teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
                 paikannimen_ja_sukunimen_lainen_johdos,
                 tavuviiva;
    if (etuliite in $oikea.jatko) then
       result $res, rules etuliite;
    end;
end;
  end;
  if (@nimisanasta_johdettu_laatusana * $oikea.jatko /= <>) then
    result $res, rules nimisanasta_johdettu_laatusana;
  end;
  if (@nimisanasta_johdettu_nimisana * $oikea.jatko /= <>) then
    result $res, rules nimisanasta_johdettu_nimisana;
  end;
  if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
    result $res, rules nimi_laatusanan_johdin;
  end;
  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (liitesana2 in $oikea.jatko) then
    result $res, rules liitesana2;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
  result $res, rules sijapääte;
end;


combi_rule laatusana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = laatusana;

  define $i := $n - 1;

assert ($i = length($vasen));

#define $nimi := transmit (<"laatusana"> + <$vasen.$i.alku> + <$oikea> + <$sana>);
#define $n := transmit (<"luokka"> + <$vasen.$i.luokka>);

#  assert ($i greater 0);

  if ($i greater 0) then
    ? ($oikea.luokka in $vasen.$i.jatko);
    ? yhdyssana_oikein ($vasen, $oikea, $i);
  end;

#define $n := transmit (<$vasen> + <$oikea> + <$sana>);
#define $n1 := transmit ($vasen);
#define $n2 := transmit ($oikea);
#define $n3 := transmit ($sana);

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (liitesana2 in $oikea.jatko) then
    result $res, rules liitesana2;
  end;
  if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
    result $res, rules nimi_laatusanan_johdin;
  end;
  if (@voittoaste + @yliaste * $oikea.jatko /= <>) then
    result $res, rules voitto_yliaste;
  end;
  if (@johdin_laatusanasta + @johdin_nimisanasta * $oikea.jatko /= <>) then
    result $res, rules laatusanan_johdos;
###    result $res, rules laatusanasta_johdettu_nimisana, laatusanasta_johdettu_laatusana;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
if (1 = 1) then
  if (tavuviiva in $oikea.jatko) then
    result $res, rules yhdyssana;
  end;
else
  if (nimisana in $oikea.jatko) then
    result $res, rules nimisana, teonsanasta_johdettu_nimisana,
                       paikannimen_ja_sukunimen_lainen_johdos;
  end;
  if (laatusana in $oikea.jatko) then
    result $res, rules laatusana, teonsanasta_johdettu_laatusana;
  end;
  if (nimi_laatusana in $oikea.jatko) then
    result $res, rules nimi_laatusana;
  end;
  if (etuliite in $oikea.jatko) then
    result $res, rules etuliite;
  end;
  if (tavuviiva in $oikea.jatko) then
    result $res, rules tavuviiva;
  end;
end;

  result $res, rules sijapääte;
end;


combi_rule nimi_laatusana ($vasen, $oikea, $sana, $n):
#define $n1 := transmit ($vasen);
#define $n2 := transmit ($oikea);
#define $n3 := transmit ($sana);

  ? $oikea.luokka = nimi_laatusana;
  define $i := $n - 1;

assert ($i = length($vasen));

#define $n4 := transmit ($oikea.luokka);

  if ($i greater 0) then
    ? (($oikea.luokka in $vasen.$i.jatko) or
       ((nimisana in $vasen.$i.jatko) and ($oikea.luokka in @nimisana + @lukusana)));
    ? yhdyssana_oikein ($vasen, $oikea, $i);
  end;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (omistusliite in $oikea.jatko) then
     result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
     result $res, rules liitesana;
  end;
  if (liitesana2 in $oikea.jatko) then
     result $res, rules liitesana2;
  end;
  if ((voittoaste in $oikea.jatko) or (yliaste in $oikea.jatko)) then
     result $res, rules voitto_yliaste;
  end;
  if (loppu in $oikea.jatko) then
     result $res, rules loppu;
  end;
  if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
    result $res, rules nimi_laatusanan_johdin;
  end;
  if (@laatusanasta_johdettu_nimisana * $oikea.jatko /= <>) then
    result $res, rules laatusanasta_johdettu_nimisana;
  end;
  if (@laatusanasta_johdettu_laatusana * $oikea.jatko /= <>) then
    result $res, rules laatusanasta_johdettu_laatusana;
  end;
  if (@nimisanasta_johdettu_laatusana * $oikea.jatko /= <>) then
    result $res, rules nimisanasta_johdettu_laatusana;
  end;
  if (@nimisanasta_johdettu_nimisana * $oikea.jatko /= <>) then
    result $res, rules nimisanasta_johdettu_nimisana;
  end;

if (1 = 1) then
  if (tavuviiva in $oikea.jatko) then
    result $res, rules yhdyssana;
  end;
  result $res, rules sijapääte;
else
  if (tavuviiva in $oikea.jatko) then
     result $res, rules tavuviiva;
  end;
  result $res,
         rules sijapääte,
               etuliite,
               nimisana,
               laatusana,
               nimi_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
##               nimisanasta_johdettu_nimisana, ### nimisanasta_johdettu_laatusana,
               paikannimen_ja_sukunimen_lainen_johdos;
end;
end;


combi_rule asemosana ($vasen, $oikea, $sana):
  ? $oikea.luokka = asemosana;

#define $nimi := transmit (<"asemosana"> + <$oikea> + <$sana>);

  result $vasen + <[alku: $sana] + $oikea>,
         rules asemosanan_sijapääte, liitesana, tavuviiva, voitto_yliaste, loppu;
#         rules sijapääte, liitesana, tavuviiva, voitto_yliaste, loppu;
end;


combi_rule lyhennesääntö ($vasen, $oikea, $sana):
  ? $oikea.luokka in <lyhenne>;

#define $nimi := transmit (<"lyhennesääntö"> + <$vasen> + <$oikea> + <$sana>);

  result $vasen + <[alku: $sana] + $oikea>,
         rules kaksoispiste, tavuviiva, loppu;
end;


combi_rule apusana ($vasen, $oikea, $sana):
  ? $oikea.luokka in <huudahdussana, sidesana>;

#define $nimi := transmit (<"apusana"> + <$oikea> + <$sana>);

  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, tavuviiva, loppu;
end;


combi_rule kaksoispiste ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <kaksoispiste>;

#define $nimi := transmit (<":"> + <$vasen> + <$oikea> + <$sana> + <length($vasen)>);

  result $vasen + <[alku: $sana] + $oikea + [äs: $vasen.($n-1).äs]>,
         rules kaksoispisteen_sijapääte;
end;


combi_rule tavuviiva ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <tavuviiva>;
  ? $oikea.luokka in $vasen.($n-1).jatko;

#define $nimi1 := transmit (<"tavuviiva1"> + <$vasen>);
#define $nimi2 := transmit (<"tavuviiva2"> + <$oikea>);
#define $nimi3 := transmit (<"tavuviiva3"> + <$sana>);

if (1 = 1) then
  result $vasen + <[alku: $sana] + $oikea>,
         rules yhdyssana,
               asemosana, apusana, lyhennesääntö, erisnimi, loppu;
else
  result $vasen + <[alku: $sana] + $oikea>,
         rules nimisana, laatusana, nimi_laatusana, asemosana, apusana, lyhennesääntö,
               etuliite,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               kieltosana, erisnimi,
               paikannimen_ja_sukunimen_lainen_johdos,
               loppu;
end;
end;


# Tämä sääntö on muokattu Harri Pitkäsen Voikko-versiosta.
#
combi_rule kaksoispisteen_sijapääte ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = sijapääte;
  ? $oikea.sija /= keinonto_n;

  define $edellinen := $vasen.($n-1);
  ? ($edellinen.äs = aä) or ($oikea.äs = aä) or ($edellinen.äs = $oikea.äs);

  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules omistusliite, liitesana, loppu;
end;


define @sti_edeltäjä := <laatusana, nimi_laatusana, voittoaste, yliaste>
                        + @nimisanasta_johdettu_laatusana
                        + @laatusanasta_johdettu_laatusana
                        + @nimi_laatusanan_johdin
                        + @teonsanasta_johdettu_laatusana
                        + @lukusana + @laatutapa_1 + @laatutapa_2;

# Sijapääte, joka voi olla yhdyssanan alkuosassa.
#
define @yhdyssanasija := <omanto_n,           # Talonpoika.
                          omanto_en,
                          omanto_ien,
                          omanto_jen,
                          omanto_in,          # Vanhempainloma.
                          omanto_ten,         # Naistentanssit.
                          omanto_iT,          # Hampaidenhoito, hampaittenhoito.
                          sisätulento_hVn,    # Maahantulo, työhönotto.
                          sisätulento_ihin>;  # Maihinnousu.

combi_rule sijapääte ($vasen, $oikea, $sana):
  ? $oikea.luokka = sijapääte;

#define $nimi := transmit (<$vasen> + <$oikea> + <$sana>);

  define $edellinen := $vasen.length ($vasen);

#define $e := transmit ($edellinen);

  ? $oikea.sija in $edellinen.jatko;
  ? äs_ok ($edellinen,$oikea);

  ? ($oikea.sija /= sisätulento_Vn)  or sisätulento_Vn_oikein  ($oikea.sija, substring($edellinen.alku,1R), $sana);
  ? ($oikea.sija /= sisätulento_hVn) or sisätulento_hVn_oikein ($oikea.sija, substring($edellinen.alku,1R), $sana);

  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  if (($oikea.sija = kerronto_sti) and ($edellinen.luokka in @sti_edeltäjä)) then
    result $vasen + <[alku: $sana] + $oikea>,
           rules liitesana, loppu;
  elseif ($oikea.sija in @yhdyssanasija) then
    #
    # Käsitellään yhdyssanojen muodostaminen erikseen.
    #
    result $vasen + <[alku: $sana] + $oikea>,
           rules #etuliite, nimisana, laatusana, nimi_laatusana,
                 #teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
                 #tavuviiva,
                 #paikannimen_ja_sukunimen_lainen_johdos,
                 yhdyssana,
                 omistusliite, liitesana, loppu;
  else
    result $vasen + <[alku: $sana] + $oikea>,
           rules tavuviiva, omistusliite, liitesana, loppu;
  end;
end;


combi_rule asemosanan_sijapääte ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = sijapääte;

  define $edellinen := $vasen.($n-1);
  ? $oikea.sija in $edellinen.jatko;
  ? äs_ok ($edellinen,$oikea);

  ? ($oikea.sija /= sisätulento_Vn)  or sisätulento_Vn_oikein  ($oikea.sija, substring($edellinen.alku,1R), $sana);
  ? ($oikea.sija /= sisätulento_hVn) or sisätulento_hVn_oikein ($oikea.sija, substring($edellinen.alku,1R), $sana);

  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (($oikea.sija = kerronto_sti) and ($edellinen.luokka in @sti_edeltäjä)) then
    result $res, rules liitesana, loppu;
  else
    if sijamuoto_sijasta($oikea.sija) = omanto then
      result $res, rules inen_johdos_nimisanasta;
    end;
    result $res, rules liitesana, loppu;
#    result $res, rules liitesana, tavuviiva, loppu;
  end;
end;


combi_rule teonsana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <teonsana>;

#define $a := transmit ($oikea.jatko * @teonsanan_taivutusmuodot);

  define $i := $n - 1;
  ? $oikea.luokka in $vasen.$i.jatko;
  ? yhdyssana_oikein ($vasen, $oikea, $i);

#define $n1 := transmit (<"teonsana1"> + <$vasen>);
#define $n2 := transmit (<"teonsana2"> + <$oikea>);
#define $n3 := transmit (<"teonsana3"> + <$sana>);
#define $n6 := transmit (<"">);

  if (liitesana in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules liitesana;
  end;
  if (liitesana2 in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules liitesana2;
  end;
  if (loppu in  $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules loppu;
  end;
  if (tavuviiva in  $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules tavuviiva;
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules tekijämuodot, nimitavat, laatutavat;
end;


combi_rule tekijämuodot ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @kestämän_tekijäpääte + @tositavan_tekijäpääte_4
                     + @kertoman_tekijäpääte
                     + @ehtotapa + @mahtotapa + @käskytapa;

  define $i := $n - 1;
  ? $vasen.$i.luokka = teonsana;
  ? $oikea.luokka in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);
  ? yhdysteonsana_oikein ($vasen, $i);

#define $a := transmit ($vasen);
#define $b := transmit ($oikea.luokka);
#define $c := transmit ($sana);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  # Päätteessä on sama ääntiö kuin vartalon lopussa,
  # esim. puhua => puhuu, saada => saa.
  #
  if (($oikea.luokka = kestämän_tekijäpääte_y3)
      and ($vasen.$i.alku matches ".*" + @ääntiö)) then
    require (viimeinen_kirjain ($vasen.$i.alku) = $sana);
  end;

  # Vvi-päätteessä Avi hyväksytään missä tahansa sanassa, muuten päätteen alussa
  # on sama ääntiö kuin vartalon lopussa: puhuuvi, sanoovi.
  #
  # "Savu käypi sieramista,: Pöly musta pörnyävi,: Tulta Hurja henkiävi,: Säkeniä suitsuavi, ..."
  # Frans Pietari Kemelli: Höyrylaiva Oulu. Oulun Viikko-Sanomia 14.8.1841.
  # http://fi.wikisource.org/wiki/H%C3%B6yrylaiva_Oulu
  # http://digi.lib.helsinki.fi/sanomalehti/secure/showPage.html?id=122472&conversationId=1&action=entryPage
  #
  if (($oikea.luokka = kestämän_tekijäpääte_y3_Vvi)
      and ($vasen.$i.alku matches ".*" + @ääntiö)) then
    require (($sana in <"avi", "ävi">) or
             (viimeinen_kirjain ($vasen.$i.alku) = substring($sana,1,1)));
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, liitesana2, loppu;
end;


combi_rule nimitavat ($vasen, $oikea, $sana, $n):
# Neljäs nimitapa katsotaan teonsanasta johdetuksi nimisanaksi.
  ? $oikea.luokka in @nimitapa_1 + @nimitapa_2 + @nimitapa_3 + <nimitapa_5>;
# ? $oikea.luokka in @nimitapa_1 + @nimitapa_2 + @nimitapa_3 + <nimitapa_4, nimitapa_5>;

  define $i := $n - 1;
  ? $vasen.$i.luokka in <teonsana>;
#define $n1 := transmit (<"nimitavat1"> + <$vasen.$i>);
#define $n2 := transmit (<"nimitavat2"> + <$oikea>);

  ? $oikea.luokka in $vasen.$i.jatko;
#define $n3 := transmit (<"nimitavat3"> + <$vasen.$i.luokka> + <$oikea.jatko> + <$sana>);

  # Ääntiö + "iss[aä] käy mutta ei kerake + "iss[aä]".
  # Esim. puno+issa (puno+essa) käy mutta ei tull+issa (tull+essa).
  # Nyt esim. "aitoissa" on sekä aitta-sanan että aitoa-sanan muoto (aitoessa).
  #
  if (($oikea.luokka = nimitapa_2) and ($sana in <"issa", "issä">)) then
    ? ($vasen.$i.alku matches ".*[aeiouyäö]");
  end;

  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  if ($oikea.luokka in @nimitapa_1 + @nimitapa_2 + @nimitapa_3) then
    ? yhdysteonsana_oikein ($vasen, $i);
  end;

  if (nimitapa_1_pitkä in $oikea.jatko) then
    result $vasen + <[alku: $sana] + $oikea>, rules nimitapa_1_pitkä;
  end;

  if ($oikea.luokka = nimitapa_5) then
    result $vasen + <[alku: $sana] + $oikea>, rules omistusliite, loppu;
  else
    result $vasen + <[alku: $sana] + $oikea>,
           rules sijapääte, omistusliite,
                 liitesana, liitesana2,
                 loppu;
  end;
end;


combi_rule nimitapa_1_pitkä ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = nimitapa_1_pitkä;

  define $i := $n - 1;
  ? $vasen.$i.luokka in @nimitapa_1;
  ? nimitapa_1_pitkä in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

#define $nimi := transmit ("nimitapa_1_pitkä " + $vasen.$i.alku + " " + $sana);
  result $vasen + <[alku: $sana] + $oikea>,
         rules omistusliite;
end;


combi_rule laatutavat ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @laatutapa_1 + @laatutapa_2;

  define $i := $n - 1;
  ? $oikea.luokka in $vasen.$i.jatko;
  ? $vasen.$i.luokka = teonsana;
  ? äs_ok ($vasen.$i, $oikea);
  ? yhdysteonsana_oikein ($vasen, $i);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

#define $n1 := transmit (<"laatutapa1"> + <$vasen>);
#define $n2 := transmit (<"laatutapa2"> + <$oikea>);
#define $n3 := transmit (<"laatutapa3"> + <$sana>);
#define $n4 := transmit (<"laatutapa4"> + <$i>);
#define $n5 := transmit (<"">);


#define $nimi := transmit ("laatutavat " + $vasen.$i.alku + " " + $sana);

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite, liitesana,
               voitto_yliaste, loppu;
end;


combi_rule kieltosana ($vasen, $oikea, $sana):
  ? $oikea.luokka = kieltosana;

#define $nimi := transmit (<"kieltosana"> + <$sana>);
  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, liitesana_kä, tavuviiva, loppu;
end;


combi_rule liitesana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <liitesana, liitesana_s>;

  define $i := $n - 1;
  ? $oikea.luokka in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);

#define $a := transmit (<$vasen.$i.luokka> + <$vasen.$i.tapaluokka> + 
#                    <$vasen.$i.tekijä> + <$vasen.$i.luku> + <$sana> + <$oikea.luokka> + <$i>);

  if (($vasen.$i.luokka = teonsana) and
      ($vasen.$i.tapaluokka = käskytapa) and
      ($vasen.$i.tekijä = 2) and
      ($vasen.$i.luku = yksikkö)) then
    ? not ($sana in <"ko", "kö">);
  end;

#define $nimi := transmit (<"liitesana"> + <$vasen.$i> + <$sana>);
#define $nimi := transmit ("liitesana " + $vasen.$i.alku + " " + $sana);

  result $vasen + <[alku: $sana] + $oikea>,
         rules loppu;
end;


combi_rule liitesana2 ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <liitesana2>;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

#define $nimi := transmit ("liitesana2 " + $vasen.$i.alku + " " + $sana);

  result $vasen + <[alku: $sana] + $oikea>,
         rules loppu;
end;


combi_rule liitesana_kä ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = liitesana_kä;

  define $i := $n - 1;
  ? $oikea.luokka in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

#define $nimi := transmit ("liitesana_kä " + $vasen.$i.alku + " " + $sana);
  result $vasen + <[alku: $sana] + $oikea>,
         rules liitesana, loppu;
end;


combi_rule omistusliite ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = omistusliite;

  define $i := $n - 1;

  ? $oikea.luokka in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  define $result := $vasen + <[alku: $sana] + $oikea>;

  # Hyväksytään yksikön ensimmäisen omistusliite "in" eräissä
  # sijamuodoissa, esim. matkallain, matkaltain, matkallein;
  # ja myös esim. tyttärein (tyttäreni).
  #
  if (($sana = "in") and ($oikea.tekijä = 1) and äs_ok ($vasen.$i, $oikea)
      and ((($vasen.$i.luokka in <sijapääte, nimitapa_2, nimitapa_3>) and
             (not ($vasen.$i.sija in <vajanto_ttA, vajanto_ittA>)) and
             (substring (value_string ($vasen.$i.sija), 1R) in <"A", "e">))
            or (($vasen.$i.alku /= nil) and substring ($vasen.$i.alku, 1R) in <"e">))) then
    result $result,
           rules liitesana, loppu;
  end;

  # Jos sijamuoto on omanto_in, hyväksytään vain omistusliitteet "nsa", "nsä", "mme".
  # Omistusliitteet "nsa" ja "nsä" hyväksytään aina, mutta omistusliite "mme" vain, jos
  # sanan vartalon viimeinen kirjain on a tai e tai ä. Tämä algoritmi kelpaa Juhani
  # Ahon teoksille (http://www.lonnrot.net/etext.html).
  #
  # Siis hyväksytään esim. isäinsä, valainsa, kasvoinsa, pöytäimme, synteimme, mutta ei uskoimme.
  #
  if ($sana in <"ni", "si", "s", "nsa", "nsä", "mme", "nne">) then
    if ($vasen.$i.sija = omanto_in) then
      if (($sana in <"nsa", "nsä">) or
          (($sana in <"mme">) and (substring ($vasen.($i-1).alku, 1R) in <"a", "e", "ä">))) then
        result $result,
               rules liitesana, loppu;
      else
        stop;
      end;
    else
      result $result,
             rules liitesana, loppu;
    end;
  else
    if ($vasen.$i.luokka in <seikkasana, suhdesana> and omistusliite3_oikein ($vasen.$i.alku, $sana)) then
      if (not (strcat ($vasen) matches ".*(aa|ee|ii|oo|uu|yy|ää|öö)")) then
          result $result,
                 rules liitesana, loppu;
      end;
    elseif ($vasen.$i.luokka = sijapääte) then
      if (($vasen.$i.sija in <osanto_A, osanto_iA,
                              osanto_tA, osanto_ttA, osanto_itA,
                              osanto_jA,
                              olento_nA, olento_inA,
                              tulento_ksi, tulento_iksi,
                              sisäolento_ssA, sisäolento_issA,
                              sisäeronto_stA, sisäeronto_istA,
                              ulko_olento_llA, ulko_olento_illA,
                              ulkoeronto_ltA, ulkoeronto_iltA,
                              ulkotulento_lle, ulkotulento_ille,
                              vajanto_ttA, vajanto_ittA,
                              seuranto_ine>) and
          omistusliite3_oikein ($vasen.$i.alku, $sana)) then
        if (not (strcat ($vasen) matches ".*(aa|ee|ii|oo|uu|yy|ää|öö)")) then
          result $result,
                 rules liitesana, loppu;
        end;
      end;
    elseif (($vasen.$i.luokka in <nimitapa_1_pitkä, nimitapa_2, nimitapa_3, nimitapa_3_saama, nimitapa_5>) and
            omistusliite3_oikein ($vasen.$i.alku, $sana)) then
      result $result,
             rules liitesana, loppu;
    end;
  end;
end;


combi_rule voitto_yliaste ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @voittoaste + @yliaste;
  define $i := $n - 1;

#define $nimi1a := transmit ("voitto_yliaste1 " + $vasen.$i.alku + " " + $sana);
#define $nimi2a := transmit (<"voitto_yliaste2"> + <$vasen.$i>);
#define $nimi3a := transmit (<"voitto_yliaste3"> + <$oikea> + <$sana>);

  ? (voittoaste in $vasen.$i.jatko) or (yliaste in $vasen.$i.jatko);
  ? $oikea.luokka in $vasen.$i.jatko;
  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (tavuviiva in $oikea.jatko and $sana = "mpi" and $oikea.luokka = voittoaste) then
    result $res, rules tavuviiva; # Esim. vähempi-arvoinen.
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (nimisana in $oikea.jatko) then
    result $res, rules nimisana, laatusana, nimi_laatusana,
                       teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana;
  end;
  if (johdin_UUs in $oikea.jatko) then
    result $res, rules laatusanan_johdos;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;

  result $res, rules sijapääte;
end;


combi_rule nimisanan_johdos ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @johdin_nimisanasta;

#define $a := transmit (<"a"> + <$vasen>);
#define $b := transmit (<"b"> + <$oikea>);
#define $c := transmit (<"c"> + <$sana>);

  define $i := $n - 1;
  if ($i greater 0) then
    ? $oikea.luokka in $vasen.$i.jatko;
    if ($oikea.luokka in <johdin_Us, johdin_UUs>) then
      ? $vasen.$i.luokka in <laatusana, nimi_laatusana>;
    end;
  end;

  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (liitesana2 in $oikea.jatko) then
    result $res, rules liitesana2;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
  if (@voittoaste + @yliaste * $oikea.jatko /= <>) then
    result $res, rules voitto_yliaste;
  end;

if (1 = 1) then
  if (tavuviiva in $oikea.jatko) then
    result $res, rules yhdyssana;
  end;
else
  result $res,
          rules
                nimisana, laatusana,
                teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
                laatusanan_johdos,
                nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
                paikannimen_ja_sukunimen_lainen_johdos,
                etuliite,
                tavuviiva;
#                omistusliite, liitesana, liitesana2, loppu;
end;
  result $res, rules sijapääte;
end;


combi_rule laatusanan_johdos ($vasen, $oikea, $sana, $n):
# Laatusana voi olla myös nimi_laatusana, siksi @johdin_nimisanasta.
  ? $oikea.luokka in @johdin_laatusanasta + @johdin_nimisanasta;

  define $i := $n-1;

#foreach $k in $vasen:
#  define $a := transmit (<"a"> + <$k.alku> + <$k.perusmuoto> + <$k.luokka>);
#end;
#define $a := transmit (<"a"> + <$vasen.$i>);
#define $b := transmit (<"b"> + <$oikea>);
#define $c := transmit (<"c"> + <$sana>);

  ? $oikea.luokka in $vasen.$i.jatko;

  ? ($oikea.luokka = johdin_nlainen) # Hyvä+nlainen, paha+nlainen.
    or äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
#  if (liitesana2 in $oikea.jatko) then
#    result $res, rules liitesana2;
#  end;
  if (@voittoaste + @yliaste * $oikea.jatko /= <>) then
    result $res, rules voitto_yliaste;
  end;
  if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
    result $res, rules nimi_laatusanan_johdin;
  end;
  if (@johdin_laatusanasta + @johdin_nimisanasta * $oikea.jatko /= <>) then
    result $res, rules laatusanan_johdos;
  end;

if (1 = 1) then
  if (tavuviiva in $oikea.jatko) then
    result $res, rules yhdyssana;
  end;
else
  result $res,
         rules #sijapääte,
               nimisana, laatusana,
               nimi_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
##               laatusanan_johdos,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               paikannimen_ja_sukunimen_lainen_johdos,
##               nimi_laatusanan_johdin,
               etuliite,
               tavuviiva;
#               voitto_yliaste, omistusliite, liitesana, loppu;
end;
  result $res, rules sijapääte;
end;


subrule strcat ($vasen):
  define $s := "";

  foreach $k in $vasen:
    $s :=+ $k.alku;
  end;

  return $s;
end;


subrule sana1 ($tietue):
  if ($tietue.perusmuoto = nil) then
    return $tietue.alku;
  else
    return $tietue.perusmuoto;
  end;
end;


subrule sisätulento_Vn_oikein ($sija, $ed_kirjain, $sana):
  return ((($ed_kirjain matches @a) and ($sana matches "an?")) or
          (($ed_kirjain matches @e) and ($sana matches "en?")) or
          (($ed_kirjain matches @i) and ($sana matches "in?")) or
          (($ed_kirjain matches @o) and ($sana matches "on?")) or
          (($ed_kirjain matches @u) and ($sana matches "un?")) or
          (($ed_kirjain matches @y) and ($sana matches "yn?")) or
          (($ed_kirjain matches @ä) and ($sana matches "än?")) or
          (($ed_kirjain matches @ö) and ($sana matches "ön?")));
end;


subrule sisätulento_hVn_oikein ($sija, $ed_kirjain, $sana):
  return ((($ed_kirjain matches @a) and ($sana matches "han?")) or
          (($ed_kirjain matches @e) and ($sana matches "hen?")) or
          (($ed_kirjain matches @i) and ($sana matches "hin?")) or
          (($ed_kirjain matches @o) and ($sana matches "hon?")) or
          (($ed_kirjain matches @u) and ($sana matches "hun?")) or
          (($ed_kirjain matches @y) and ($sana matches "hyn?")) or
          (($ed_kirjain matches @ä) and ($sana matches "hän?")) or
          (($ed_kirjain matches @ö) and ($sana matches "hön?")) or
          (($ed_kirjain matches "'"))); # Parfait'hen yms.
end;


# Hyväksytäänkö teonsanat tyyppiä "ohjelma+listata".
#
subrule yhdysteonsana_oikein ($vasen, $i):
# Ota kommentti pois seuraavan rivin alusta, jos haluat hyväksyä yhdysteonsanat.
# return yes;

  if ($i greater 1) then
    foreach $k in $vasen:
      if (not ($k.luokka in <teonsana, etuliite>)) then
        stop;
      end;
    end;
  end;
  return yes;
end;


subrule omistusliite3_oikein ($sana, $liite):
  return ((($sana matches ".*" + @a) and ($liite = "an")) or
          (($sana matches ".*" + @e) and ($liite in <"en", "hen">)) or
          (($sana matches ".*" + @i) and ($liite = "in")) or
          (($sana matches ".*" + @o) and ($liite = "on")) or
          (($sana matches ".*" + @u) and ($liite = "un")) or
          (($sana matches ".*" + @y) and ($liite = "yn")) or
          (($sana matches ".*" + @ä) and ($liite = "än")) or
          (($sana matches ".*" + @ö) and ($liite = "ön")));
end;


subrule viimeinen_kirjain ($sana):
  if     ($sana matches ".*" + @a) then return "a";
  elseif ($sana matches ".*" + @e) then return "e";
  elseif ($sana matches ".*" + @i) then return "i";
  elseif ($sana matches ".*" + @o) then return "o";
  elseif ($sana matches ".*" + @u) then return "u";
  elseif ($sana matches ".*" + @y) then return "y";
  elseif ($sana matches ".*" + @ä) then return "ä";
  elseif ($sana matches ".*" + @ö) then return "ö";
  else
    error "Viimeinen kirjain ei ole ääntiö sanassa " + $sana;
  end;
end;


subrule yhdyssana_oikein ($vasen, $oikea, $n):
#define $a := transmit (<$oikea.luokka> + <$oikea.perusmuoto> + <$vasen.$n.luokka>);
#define $b := transmit (<$vasen.$n.perusmuoto> + <$oikea.perusmuoto>);

#define $a := transmit (<"a"> + <($vasen.1).alku> + <($vasen.1).perusmuoto> + <($vasen.1).luokka>);
#define $b := transmit (<"b"> + <($vasen.$n).alku> + <($vasen.$n).perusmuoto> + <($vasen.$n).luokka>);
#define $c := transmit (<"c"> + <$oikea.alku> + <$oikea.perusmuoto> + <$oikea.luokka>);
#define $d := transmit (<"d"> + <$n>);

  # Jotkut sanat eivät voi olla yhdyssanojen osina.
  #
  if ((($vasen.$n.tiedot /= nil) and ((ei_ys in $vasen.$n.tiedot) or (latex in $vasen.$n.tiedot))) or
      (($oikea.tiedot    /= nil) and ((ei_ys in $oikea.tiedot)    or (latex in $oikea.tiedot)))) then
    return no;
  end;

  # Ei hyväksytä tälläisia sanoja silloinkaan, kun sijamuoto on joku muu kuin nimentö (isi+n+maa ei käy).
  #
  if (($vasen.$n.luokka = sijapääte)
      and ($vasen.($n-1).tiedot /= nil)
      and ((ei_ys in $vasen.($n-1).tiedot) or (ei_ysa in $vasen.($n-1).tiedot))) then
    return no;
  end;


  if (($vasen.$n.tiedot /= nil) and (ei_ysa in $vasen.$n.tiedot) and
          ($oikea.luokka in @nimisana + <nimi_laatusana>)) then
    return no;
  elseif ($oikea.perusmuoto = "laatuisa") then
    if (not ($vasen.$n.luokka in <etuliite>)) then
      return no;
    end;
  elseif ($oikea.perusmuoto = "ilta") then
    #
    # Ei muotoja punais+ilta (punainen + ilta).
    #
    if (($vasen.$n.luokka in <laatusana, johdin_inen>) and
        ($vasen.$n.perusmuoto /= nil) and ($vasen.$n.perusmuoto matches ".*inen")) then
      return no;
    end;
  elseif ($oikea.perusmuoto = "nasta") then
    if (($n greater 1) and ($vasen.$n.luokka = sijapääte) and ($vasen.$n.sija = omanto_n)) then
      return no;   # Seurannasta ei ole seura + n + nasta.
    end;
  elseif (($vasen.$n.perusmuoto /= nil) and ($oikea.perusmuoto /= nil)) then
    if ((substring($vasen.$n.perusmuoto,1R) matches @ääntiö) and
        (substring($oikea.perusmuoto,1) matches @ääntiö)) then
      return (substring($vasen.$n.perusmuoto,1R) /= substring($oikea.perusmuoto,1));
    end;
  end;

  return yes;
end;


subrule yhdyssanataulukko ($eka, $toka):
  define $taulukko := <
#      <<"">,    <"">>,
      <<"aasi">,   <"ala", "alainen">>,  # Aasi+ala+inen, aasi+alainen.
      <<"aateli">, <"soikea">>,          # Ei aateli+soike+us.
      <<"agra">,   <"ari", "arka">>,
      <<"ala">,             <"härmä", "inen">>,
      <<"alas">,            <"pää", "suka", "sukia", "suu">>,
      <<"ali">,             <"soida">>,
      <<"alko">,            <"iva", "ivata">>,
      <<"alli">,            <"suu">>,
      <<"amfi">,            <"bio">>,
      <<"amfibi">,          <"olento">>, # Amfibi+olennon.
      <<"ampu">,            <"vaisu">>,
      <<"anti">,            <"lainata">>,
      <<"apu">,             <"olka">>,
      <<"arja">,            <"lainen">>,
      <<"aro">,             <"matti", "misu">>,
      <<"askar">,           <"re">>,
      <<"auki">,            <"naida", "nainen", "naisinen", "olla", "olo">>,
      <<"auto">,            <"nova">>,   # Auto+novien.
      <<"avo">,             <"nainen">>,
      <<"avoke">,           <"lassa", "loka">>, # Avoke+lassa, avoke+loilla.
      <<"belgi">,           <"alainen">>,
      <<"bodi">,            <"anus">>,
      <<"botti">,           <"cell">>,
      <<"dia">,             <"midi", "nasta", "spora">>,
      <<"ele">,             <"itä">>,
      <<"elo">,             <"tee">>,
      <<"ensi">,            <"sää">>,
      <<"esikoinen">,       <"uusi">>,  # Esikois+uutensa (esikois + uusi).
      <<"eura">,            <"joki">>,  # On omana sanana.
      <<"euro">,            <"palanen">>,
      <<"funktio">,   <"naali">>,
      <<"futu">,      <"rissa", "risti">>,
      <<"hai">,       <"talli">>,    # Hai+tallinen.
      <<"halti">,     <"joki">>,     # Halti+joiksi.
      <<"hara">,      <"voi", "voida", "vuo">>,
      <<"harras">,    <"teli">>,
      <<"heinä">,     <"tupo">>,
      <<"helve">,     <"tilli">>,
      <<"hima">,      <"lapsi", "lassa", "lasta", "lika">>,
      <<"hurri">,     <"kaani">>,
      <<"hyppy">,     <"selli">>,
      <<"hypo">,      <"tee", "teesi">>,
      <<"hätä">,      <"katka">>,
      <<"ikä">,       <"vihi", "vihki">>,
      <<"isä">,       <"nisä">>,
      <<"itse">,      <"matto", "tie">>,
      <<"iva">,       <"nova">>,
      <<"jumal">,     <"uus">>,
      <<"järkky">,    <"mättö">>,
      <<"kalla">,     <"vesi">>,
      <<"kalsa">,     <"reisi", "risa">>,
      <<"kali">,      <"umpi">>,   # Kali+umpi+toinen.
      <<"kana">,      <"voida">>,  # Kana+voitu.
      <<"kannus">,    <"tavi">>,
      <<"kapi">,      <"nalli", "tali">>,  # Kapi+nallinen, kapi+talis+tien (kapi+talinen+tie).
      <<"kare">,      <"lika">>,
      <<"karja">,     <"lainen", "lassa">>,
      <<"karri">,     <"ääri">>,
      <<"kato">,      <"lila">>,
      <<"kemi">,      <"alli", "joki">>,
      <<"Kim">,       <"maa">>,
      <<"kimma">,     <"huti">>,
      <<"kiri">,      <"tiikki">>,  # Luultavasti väärin kirjoitettu "kritiikki".
      <<"kirjo">,     <"rasta">>,
      <<"kiska">,     <"istua", "seka">>,   # Kiska+istuja.
      <<"kita">,      <"risti">>,
      <<"koiras">,    <"euro">>,
      <<"koko">,      <"nainen">>,      # Koko+nais+valtainen.
      <<"koli">,      <"brie">>,        # Koli+brie+n.
      <<"komi">,      <"teko">>,        # Komi+teoille.
      <<"korni">,     <"lovi">>,        # Korni+lovi(n).
      <<"korsi">,     <"kaksi", "kalla">>,
      <<"krim">,      <"naali">>,  # Krimi+naali.
      <<"kulti">,     <"voi", "vuo">>,
      <<"kuokka">,    <"lassa", "lasta">>,
      <<"kuu">,       <"kausittainen", "lakka", "luja", "luksus", "luminen", "luu">>,
      <<"kure">,      <"misu">>,
      <<"kyllänen">,  <"tie">>,
      <<"käsin">,     <"oja", "ukki">>,
      <<"lainen">,    <"kala", "kasti", "kistu", "kuri", "kuu">>,
      <<"lakka">,     <"rissa">>,
      <<"lehdes">,    <"säkä", "sää">>,
      <<"liehu">,     <"koko", "maa", "maha", "massa", "vana", "vasta", "villa">>,
      <<"liukas">,    <"tie">>,
      <<"loinen">,    <"kaksi", "tee", "tehdä">>,
      <<"lukunen">,   <"alin">>,
      <<"luokka">,    <"istua">>,
      <<"luu">,       <"hattu", "kutu">>,
      <<"läinen">,    <"käki">>,
      <<"länsi">,     <"maistua">>,
      <<"läpi">,      <"käydä">>,
      <<"läsnä">,     <"olija", "olo", "olonen", "Olos">>,   # 2 ekaa ovat sanastossa.
      <<"maa">,       <"gis", "ilma", "ilmainen", "nisä">>,  # "Maailma" on sanastossa.
      <<"maagi">,     <"seksi", "silta">>,
      <<"mainen">,    <"emä">>,
      <<"maini">,     <"ostaa", "ottaa", "tukea", "tuki">>,
      <<"makro">,     <"bioottinen">>,  # On sanastossa.
      <<"mansi">,     <"kastaa", "koi", "koittaa">>,
      <<"mehiläinen">, <"pesis">>,  # Mehiläis+pesis+tä.
      <<"meno">,      <"paussi">>,
      <<"mensu">,     <"rae">>,
      <<"metaani">,   <"rikastaa">>,
      <<"mieli">,     <"syy", "syys">>,
      <<"mielinen">,  <"ikä", "säkä", "sää", "tele", "teli", "tikki">>, # Mielis+ikä(än).
      <<"mikro">,     <"filmata">>,
      <<"misu">,      <"osittaa">>,
      <<"moni">,      <"naida", "nainen", "tora", "tori">>,
      <<"musti">,     <"koi", "koettaa">>,
      <<"muta">,      <"geeni", "geeninen">>,
      <<"mäki">,      <"tupalainen">>,
      <<"nomi">,      <"suu">>,
      <<"omatunto">,  <"vapa">>,
      <<"onninen">,   <"tuttu">>,
      <<"ope">,       <"reti">>,
      <<"oppinen">,   <"ääntö">>,
      <<"Osta">,      <"maa">>,
      <<"osto">,      <"skeida">>,
      <<"paita">,     <"silla">>,
      <<"palava">,    <"kivi">>,
      <<"peli">,      <"manni">>,
      <<"perin">,     <"tee">>,   # Perin+tee(llinen).
      <<"peru">,      <"noki">>,  # Peru+noissa.
      <<"perus">,     <"korjata">>,  # On oma sanansa.
      <<"perso">,     <"nalli">>,
      <<"perään">,    <"pää">>,
      <<"pihla">,     <"joki">>,
      <<"pitkis">,    <"täkki">>,
      # "Poisjännin" ei ole "poisjännä"-sanan muoto, vaan väärin kirjoitettu "poisjäännin".
      <<"pois">,      <"jännä", "pää", "taka", "tuki", "tutti", "tuttu">>,
      <<"polvinen">,  <"tuki">>,     # Polvis+tuen.
      <<"poli">,      <"klinikka">>, # On oma sanansa.
      <<"porno">,     <"graafi">>,   # On oma sanansa.
      <<"puh">,       <"tie">>,
      <<"punk">,      <"arka", "kari">>,
      <<"puoli">,     <"nainen">>,  # On oma sanansa.
      <<"puu">,       <"hai">>,
      <<"priori">,    <"soida">>,
      <<"psyko">,     <"fyysinen">>, # On sanastossa.
      <<"pyy">,       <"tee">>,   # Pyy+tee+llinen.
      <<"päällys">,   <"tie">>,   # Päällysteinen (päällys + tie + inen).
      <<"rad">,       <"kaali">>,
      <<"raju">,      <"ilma">>,
      <<"ratsas">,    <"tapa", "tavi", "tele", "teli", "tikki">>,
      <<"rauni">,     <"ottaa">>,      # Rauni+ota.
      <<"re">,        <"aktio", "väkä">>,
      <<"reva">,      <"niksi">>,
      <<"romani">,    <"alainen">>,
      <<"rondo">,     <"letto">>,
      <<"rusi">,      <"koi", "noita">>,
      <<"rulla">,     <"lautailla">>,     # On oma sanansa.
      <<"sala">,      <"kuljettaa", "manner">>,  # Ei sala+mantereista.
      <<"salme">,     <"kivi", "oja", "perä", "suu">>,
      <<"Sam">,       <"maa">>,
      <<"samainen">,  <"tuta">>,
      <<"samu">,      <"raita">>,
      <<"sankar">,    <"ien", "itara", "naida">>,  # Sankar+itarina. Sankar+nainen ei ole sankar+naida.
      <<"sivula">,    <"usea">>,
      <<"snellman">,  <"laki">>,
      <<"soinen">,    <"aari">>,
      <<"sose">,      <"uros">>,
      <<"sportti">,   <"suu">>,
      <<"suo">,       <"mensu", "misu", "musti", "nina">>,
      <<"suu">,       <"tee", "tehdä", "teloa">>,
      <<"syy">,       <"tee">>,
      <<"sävel">,     <"eittää">>,
      <<"taide">,     <"luomi", "luomu">>,
      <<"tanhu">,     <"villa">>,
      <<"tasa">,      <"uni">>,
      <<"taulu">,     <"koi", "koittaa">>,
      <<"taus">,      <"takki">>,
      <<"tee">,       <"maa">>,    # Tee+maa+n.
      <<"terve">,     <"yty">>,
      <<"terä">,      <"sportti">>,
      <<"tie">,       <"naukaista", "tee", "tie", "toinen", "tokko">>,
      <<"tiu">,       <"kimma">>,
      <<"timo">,      <"tie">>,
      <<"tois">,      <"tapa">>,
      <<"toivo">,     <"malla">>,
      <<"toti">,      <"sennäköinen">>,
      <<"tove">,      <"re", "reisi">>,
      <<"traditio">,  <"naali">>,
      <<"ulko">,      <"naida", "nainen">>,
      <<"ulos">,      <"pää", "tavi", "tuma", "tupa", "tuttava">>,
      <<"ura">,       <"naukaista">>,  # Ura+naukaisija.
      <<"us">,        <"Neuvonen">>,
      <<"uus">,       <"arpoa">>,
      <<"vaja">,      <"nainen">>,
      <<"varus">,     <"tee">>,    # Varus+tee+t.
      <<"vasta">,     <"pää">>,
      <<"vastaan">,   <"otto">>,
      <<"viher">,     <"ikä", "iäinen", "vuotaa">>,
      <<"viive">,     <"itä">>,
      <<"vik">,       <"koko", "oja", "uros">>,
      <<"voi">,       <"manne", "paha">>,
      <<"voinen">,    <"iva", "ivata">>,    # Voisivatko => vois+iva(ta).
      <<"vuo">,       <"leski", "vuosilta", "tee">>,    # Vuo+leske+llen, vuos+ilta, vuo+tee+n.
      <<"vuoro">,     <"vaikuttaa">>,       # On sanastossa.
      <<"vyö">,       <"täi">>,             # Vyö+täinen.
      <<"väli">,      <"syys">>,            # Kansain+väli+syys.
      <<"yhdys">,     <"valta">>,           # Yhdysvalta on oma sanansa.
      <<"yli">,       <"härmä", "voi">>,    # Yli+voi+mainen (johdin_mAinen).
      <<"ylä">,       <"hää", "silla">>,
      <<"ylänen">,    <"ilta">>,
      <<"äiti">,      <"nisä">>,

      <<"ahne", "verta", "vuos">,       <"ilta">>, # Ahne+ilta, verta+illakseen, vuos+ilta.
      <<"aisti", "kanta", "rikko", "kerto", "kutsu", "toivo", "usko">,   <"muksia">>,
      <<"alainen", "alanen">,                                            <"pää", "suka", "sukia", "suu", "tulo">>,
      <<"alli", "pasi">,                                                 <"anssi">>,
      <<"eri", "esi">,                  <"koi">>,   # Eri+koi+sala, esi+kois- (esi + koi + inen).
      <<"euronen", "pikkunen">,                                          <"portti">>, # Euros+portti, pikkus+portti.
      <<"hauta", "kuiva", "ohja", "pala", "raitti", "sauma", "talo">,    <"usma">>,
      <<"jako", "keno", "mura">,       <"biitti">>,
      <<"into", "pyyntö">,             <"hima">>,
      <<"iso", "kanta">,               <"isä">>,
      <<"koti", "kotiin">,             <"pää">>,    # Kotiinpäin.
      <<"kuuma", "kylmä">,             <"valssata">>,
      <<"kymmen", "kymmenen">,         <"vuota">>,
      <<"manga", "toka", "vino">,      <"reva">>,
      <<"mari", "valin", "vero">,      <"nasta">>,
      <<"Pal", "Pál">,                 <"joki", "maa", "meri">>,
      <<"pyy", "tie">,                 <"täi">>,  # Pyy+täisin (pyy + täi + inen), tie+täisin.
      <<"sota", "säihky">,             <"väkä">>,
      <<"yhtä", "yksi">,               <"toinen", "toistaa">>
    >;

#define $a := transmit ($eka + " " + $toka);

  foreach $k in $taulukko:
    if (($eka in $k.1) and ($toka in $k.2)) then
      return yes;
    end;
  end;
  return no;
end;


subrule yhdyssanataulukko2 ($eka, $toka, $kolmas):
  define $taulukko := <
#      <<"">,    <"">,    <"">>,
      <<"heitto">,   <"inen">,  <"tuki">>,    # Heitto+is+tu+in. Heitto + johdin_inen + tuki + keinonto_n.
      <<"hima">,     <"n">,     <"kala", "kalaisa">>,    # Hima+n+kalainen.
      <<"kani">,     <"n">,     <"manne">>,
      <<"luokka">,   <"inen">,  <"tupa">>,
      <<"muuri">,    <"inen">,  <"ääntö">>,   # Palo+muur+is+ääntö. Palo + johdin_inen + ääntö.
      <<"oma">,      <"tunto">, <"vapa">>,
      <<"palaa">,    <"va">,    <"kivi">>,
      <<"pari">,     <"inen">,  <"hissa">>,
      <<"pari">,     <"inen">,  <"printti">>, # Par+is+printti. Pari + johdin_inen + printti.
      <<"suoda">,    <"ma">,    <"lainen", "laki">>,  # Suo+ma+lainen. Suoda + johdin_mA_saama + johdin_lainen.
                                                      # Suo+ma+laista. Suoda + johdin_mA_saama + laki.
                                                      # "Suomalainen" on sanastossa.
      <<"tukka">,  <"nuotta">, <"silla">>,
      <<"väsky">,  <"n">,      <"yö">>               # Väsky+n+öitä.
  >;

  foreach $k in $taulukko:
#define $a := transmit (<$eka> + <$toka> + <$kolmas>);
    if (($eka in $k.1) and ($toka in $k.2) and ($kolmas in $k.3)) then
      return yes;
    end;
  end;
  return no;
end;


#####################################################


end_rule loppu ($tietue):
  define $i := length ($tietue);
  ? loppu in $tietue.$i.jatko;

#foreach $k in $tietue:
#  define $n := transmit ($k);
#end;
#foreach $k in $tietue:
#  define $n := transmit (<"loppu "> + <$k.luokka> + <$k.alku>);
#end;

  define $n := $i;
  if ($tietue.$i.luokka in <liitesana, liitesana_s, omistusliite>) then
    $n := $i - 1;
  end;


  if (not (teonsana_ok ($tietue, $n))) then
    stop;
  end;


  define $j := $n;
  repeat while ($tietue.$j.luokka in <sijapääte, omistusliite>);
    $j :=- 1;
  end;


  # Jotkut sanat eivät voi olla yhdyssanan jälkiosana.
  #
  # Ei työ+tyytyvä+isyys.
  #
  # Sukija-versiossa tällaiset sanat kuitenkin hyväksytään,
  # jos niiden edellä on yhdysviiva (esim. kirkko-isä hyväksytään,
  # mutta eri-ikäisyys ei ole eri-ikä+isyys). Sivuvaikutus: jos
  # sanaan kuuluu yhdysviiva, se tunnistetaan Sukija-versiossa
  # kahdesti (esim. esi-isä), ellei niitä ole merkitty sanastoon
  # lipulla ei_sukija.
  #
  define $malli := switch (malli);

  foreach $u in length($tietue):
    if (($u greater 1) and ($u less_equal $j) and
        ($tietue.$u.tiedot /= nil) and (ei_ysj in $tietue.$u.tiedot)) then
      if (($malli = voikko) or ($tietue.($u-1).luokka /= tavuviiva)) then
        stop;
      end;
    end;
  end;


  # inen-johtimella tehty sana voi olla vain yhdyssanan lopussa,
  # paitsi jos se on merkitty omaksi sanakseen.
  #
  repeat while ($tietue.$j.luokka in @johdin_laatusanasta + @johdin_nimisanasta - <johdin_inen>);
    $j :=- 1;
  end;
  define $k := $j;
  repeat
    if ($tietue.$k.luokka = johdin_inen) then
      if ($k = 2) then
        if (($tietue.($k-1).tiedot /= nil) and (inen in $tietue.($k-1).tiedot)) then
        else
          stop;
        end;
      end;
    end;
    $k :=- 1;
  while ($k greater 0);
  end;



  # Jotkut sanat voivat olla vain yhdyssanojen jälkiosana.
  # Paitsi jos niissä on kirjoitusvirhe.
  #
#  if ((($tietue.1).tiedot /= nil) and (ysj in ($tietue.1).tiedot)) then
#    stop;
#  end;


  if ($i greater 1) then
    foreach $p in $i-1:
      if (yhdyssanataulukko ($tietue.$p.perusmuoto, $tietue.($p+1).perusmuoto)) then
        stop;
      end;
    end;
  end;
  if ($i greater 2) then
    foreach $p in $i-2:
      if (yhdyssanataulukko2 ($tietue.$p.perusmuoto,
                              sana1($tietue.($p+1)),
                              $tietue.($p+2).perusmuoto)) then
        stop;
      end;
    end;
  end;

  result $tietue, accept;
end;


# Tarkistetaan, että teonsana on oikein.
#
# Jos yhdyssana loppuu käskytavan yksikön toisen tai kolmannen tekijän
# kestämään [1] ja sanan alkuosa on nimisana, laatusana, etuliite tai
# seikkasana, sana ei ole oikea teonsana, eikä myöskään sana, joka loppuu
# tositavan kestämän tai kertoman yksikon ensimmäiseen tai kolmanteet tekijään,
# tai käskytavan kestämän monikon toiseen kieltomuotoon (esim. älkää äidinkielikö).
#
# [1] Toisen tekijän kestämällä ei ole päätettä, jolla se voitaisiin
#     tunnistaa, vaan se on sama kuin teonsanan vartalo.
#
# Esim. nämä eivät ole oikein:
#    alkuperäisestä -> alku=peräis=estä (alku+peräinen+estää)
#    hairahtaa      -> hai=rahtaa
#    tappion        -> tappi=on (tappi+olla)
#    ensiökäämi, ensiökäämin -> ensiö=käämiä
#    myötätuuli   -> myötä=tuulla
#    äidinkieli   -> äidin=kieliä
#    äidinkielikö -> (älkää) äidin=kielikö
#
subrule teonsana_ok ($tietue, $i):
  if (ok6 ($tietue.$i, teonsana,                käskytapa, kestämä, yksikkö, 2) or
      ok6 ($tietue.$i, teonsana,                tositapa,  kestämä, yksikkö, 3) or
      ok6 ($tietue.$i, kestämän_tekijäpääte_y1, tositapa,  kestämä, yksikkö, 1) or
      ok6 ($tietue.$i, kertoman_tekijäpääte_y1, tositapa,  kertoma, yksikkö, 1) or
      ok6 ($tietue.$i, kertoman_tekijäpääte_y3, tositapa,  kertoma, yksikkö, 3) or
      ok4 ($tietue.$i, käskytapa_kielto,        käskytapa, kestämä)) then
    foreach $k in $tietue:
#     if ($k.luokka in @nimisana + <laatusana, etuliite, seikkasana>
      if ($k.luokka in @nimisana + <laatusana, seikkasana>
                      + @johdin_teonsanasta) then
        return no;
      end;
    end;
  end;
  return yes;
end;


subrule ok4 ($tietue, $luokka, $tapaluokka, $aikamuoto):
  return (($tietue.luokka = $luokka) and
          ($tietue.tapaluokka = $tapaluokka) and
          ($tietue.aikamuoto = $aikamuoto));
end;


subrule ok6 ($tietue, $luokka, $tapaluokka, $aikamuoto, $luku, $tekijä):
  return (ok4 ($tietue, $luokka, $tapaluokka, $aikamuoto) and
          ($tietue.luku = $luku) and
          ($tietue.tekijä = $tekijä));
end;


output_filter tulosta ($tulos):
  foreach $j in $tulos:
    result perusmuoto ($j);
  end;
end;


subrule alku2 ($tietue, $alku):
  define $n := length ($alku);
  if ((substring ($tietue.perusmuoto, $n+1) = "i") and (substring ($tietue.alku, $n+1) /= "i")) then
    return (substring ($tietue.alku,1,$n) + "i" + substring ($tietue.alku, $n+1, length($tietue.alku)));
  end;
  if ((substring ($tietue.perusmuoto, $n+1) /= "i") and (substring ($tietue.alku, $n+1) = "i")) then
    return (substring ($tietue.alku,1,$n) + substring ($tietue.alku, $n+2, length($tietue.alku)));
  end;
  return $tietue.alku;
end;


# Kirjo(i)ttaa-, ammo(i)ttaa, nuola(i)sta-tyyppiset sanat ja niiden johdokset ja taivutusmuodot.
#
subrule alku ($tietue):
#define $a := transmit ($tietue);
  #
  # Kirjo(i)ttaa, kirjo(i)tella, kirjo(i)ttua, kirjo(i)ttautua.
  #
  if ($tietue.perusmuoto matches (".+[oö]" : $alku, "i?(tt(aa|ää)|tell[aä]|tt(ua|yä)|tt(au|äy)t(ua|yä))" : $loppu)) then
    return alku2 ($tietue, $alku);
  end;
  #
  # Nuola(i)sta.
  #
  if ($tietue.perusmuoto matches ("..+[^aeiouyäö][aä]" : $alku, "i?st[aä]" : $loppu)) then
    if (not ($tietue.perusmuoto in <"rangaista", "rankaista">)) then
      return alku2 ($tietue, $alku);
    end;
  end;
  #
  # Antautua/antauta. Antau(du)ttu-muodolle tulee väärä perusmuoto antaututtu.
  #
#  if ($tietue.perusmuoto matches ("...*(au|äy)" : $alku, "t(ua|yä)" : $loppu)) then
#    define $n := length ($alku);
#    return substring($tietue.perusmuoto,1,$n+2);
#  end;
  return $tietue.alku;
end;


# Muutetaan sana perusmuotoon: Väisäsillemmekö -> Väisänen.
#
subrule perusmuoto ($tietue):
  define $k := length ($tietue);
  define $luokka := $tietue.$k.luokka;
  define $edellinen_luokka := $luokka;
  define $t := <>;
  define $on_yhdyssana := no;
  define $on_ollut_johdin_UUs := no;
  define $on_aito_lukusana := yes;  # Sanassa on vain lukusanoja ja sijapäätteitä.

#define $aa := transmit ($tietue);

  # Käydään sana läpi lopusta alkuun ja poistetaan
  # sanan lopussa olevat johtimet.
  #
  repeat
    $luokka := $tietue.$k.luokka;

#    if (not ($tietue.$k.luokka = lukusana or $tietue.$k.luokka = sijapääte)) then
#      $on_aito_lukusana := no;
#    end;

    if ($on_yhdyssana) then
      $t :=+ <$tietue.$k.alku>;
#      if ($tietue.$k.luokka in @lukusana) and ($on_aito_lukusana = yes) then
#        $t :=+ <$tietue.$k.perusmuoto>;
#      elseif ($tietue.$k.luokka = sijapääte) and
#             ($tietue.$k.lukutyyppi in <perusluku, järjestysluku>) then
#        $t :=+ <"">;
#      else
#        $t :=+ <$tietue.$k.alku>;
#      end;
    elseif ($tietue.$k.luokka = etuliite) then
      $t :=+ <$tietue.$k.alku>;
    elseif ($edellinen_luokka in @johdin + <nimitapa_4>) then
      if (($tietue.$k.luokka in @johdin_nUt) and ($tietue.$k.alku matches "[lnrs][uy]nn[aä]")) then
        $t :=+ <$tietue.$k.perusmuoto>;
      else
#        $t :=+ <$tietue.$k.alku>;
         $t :=+ <alku ($tietue.$k)>;
      end;
    elseif (not ($luokka in <sijapääte, liitesana, liitesana2, liitesana_s,
                             omistusliite, kaksoispiste,
                             voittoaste, yliaste,
                             liitesana_kä, nimitapa_5>
                            + @tositapa + @ehtotapa + @mahtotapa + @käskytapa
                            + @nimitapa_1 + @nimitapa_2 + @nimitapa_3
                            + @laatutapa_1 + @laatutapa_2)) then
      if ($on_ollut_johdin_UUs) then
        $t :=+ <$tietue.$k.alku>;
      else
        $t :=+ <sana1($tietue.$k)>;
      end;
    else
      $t := <"">;
    end;
    $edellinen_luokka := $luokka;
    if ($tietue.$k.luokka in @nimisana
                             + <lukusanan_jälkiliite, seikkasana, laatusana,
                                teonsana> + @lukusana) then
      $on_yhdyssana := yes;
    end;
    if ($tietue.$k.luokka in <johdin_Us, johdin_UUs>) then
      $on_ollut_johdin_UUs := yes;
    end;
    $k :=- 1;
  while ($k greater 0);
  end;


  # Käännetään tulos oikeinpäin.
  #
  define $s := "";

  define $m := length ($t);

#define $q := transmit ($t);

  repeat
  while ($m greater 0);
#define $p := transmit (<$s> + <$t.$m> + <$tietue.$m.perusmuoto>);
    $s :=+ $t.$m;
    $m :=- 1;
  end;

  return $s;
end;


###===========================================


combi_rule teonsanasta_johdettu_nimisana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = teonsana;

#define $a := transmit ($vasen.($n-1).jatko);
#define $b := transmit ($oikea.jatko);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  ? $oikea.jatko * (@teonsanasta_johdettu_nimisana) /= <>;

#define $e := transmit (@teonsanasta_johdettu_nimisana);

  if ($n greater 1) then
    ? yhdyssana_oikein ($vasen, $oikea, $n-1);
    ? $vasen.($n-1).jatko * <nimisana, nimi_laatusana, laatusana, etuliite> /= <>;
  end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules teonsanan_johdos_nimisana;
end;


combi_rule teonsanan_johdos_nimisana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @teonsanasta_johdettu_nimisana;
  ? $oikea.luokka in $vasen.($n-1).jatko;

  if ($oikea.luokka in <johdin_O>) then
    #
    # Vaihdetaan ääntiösointu oikeaksi Pitää -> pito.
    #
    if (not ($vasen.($n-1).alku matches ".*" + @yäö + ".*")) then
      $vasen.($n-1) :=+ [alkuperäinen_äs: $vasen.($n-1).äs];
      $vasen.($n-1).äs := a;
    end;
  end;

  ? äs_ok ($vasen.($n-1), $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite, tavuviiva, etuliite,
               liitesana, liitesana2,
               nimisana, laatusana, nimi_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               nimi_laatusanan_johdin,
               loppu;
end;


#################################################

combi_rule teonsanasta_johdettu_laatusana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = teonsana;
  ? $oikea.jatko * @teonsanasta_johdettu_laatusana /= <>;

#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  if ($n greater 1) then
    ? yhdyssana_oikein ($vasen, $oikea, $n-1);
    ? $vasen.($n-1).jatko * <nimisana, nimi_laatusana, laatusana, etuliite> /= <>;
  end;

  define $r := $vasen + <[alku: $sana] + $oikea>;
  result $vasen + <[alku: $sana] + $oikea>,
         rules teonsanan_johdos_laatusana;
end;


combi_rule teonsanan_johdos_laatusana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @teonsanasta_johdettu_laatusana;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);
  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite, tavuviiva, etuliite,
               liitesana, liitesana2,
               nimisana, laatusana, nimi_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               laatusanasta_johdettu_nimisana, laatusanasta_johdettu_laatusana,
               nimi_laatusanan_johdin,
               voitto_yliaste,
               loppu;
end;


#################################################

combi_rule nimisanasta_johdettu_nimisana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @nimisanasta_johdettu_nimisana;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

  # Johtimella johdin_UUs voidaan johtaa nimisana nimisanasta vain johtimista
  # johdin_jA_* ja johdin_tAr, esim. tekijä => tekijyys, tekijätär => tekijättäryys.
  #
  ? ($oikea.luokka /= johdin_UUs) or ($vasen.($n-1).luokka in @johdin_jA +<johdin_tAr>);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite, tavuviiva, etuliite,
               liitesana, liitesana2,
               nimisana, laatusana, nimi_laatusana,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               loppu;
end;


#################################################


combi_rule nimisanasta_johdettu_laatusana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @nimisanasta_johdettu_laatusana;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules laatusanasta_johdettu_nimisana,
               sijapääte, omistusliite,
               nimisana, laatusana, nimi_laatusana,
               nimi_laatusanan_johdin,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               paikannimen_ja_sukunimen_lainen_johdos,
               voitto_yliaste,
               etuliite, tavuviiva, liitesana, loppu;
end;


#################################################


combi_rule laatusanasta_johdettu_nimisana ($vasen, $oikea, $sana, $n):
#define $a := transmit ("laatusanasta_johdettu_nimisana");
  ? $oikea.luokka in @laatusanasta_johdettu_nimisana;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

#define $a := transmit ($vasen.($n-1));
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
               liitesana, liitesana2, tavuviiva, etuliite,
               nimisana, laatusana, nimi_laatusana,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               nimi_laatusanan_johdin,
               paikannimen_ja_sukunimen_lainen_johdos,
               loppu;
end;


combi_rule laatusanasta_johdettu_laatusana ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @laatusanasta_johdettu_laatusana;
  ? $oikea.luokka in $vasen.($n-1).jatko;

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
               liitesana, liitesana2,
               voitto_yliaste, tavuviiva,
               laatusanasta_johdettu_nimisana,  # Hyvänlaisuus.
# etuliite,
#               nimisana, laatusana, nimi_laatusana,
#               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
#               nimi_laatusanan_johdin,
               loppu;
end;


###########################################################################33

# Erisnimien käsittely on muokattu Harri Pitkäsen Voikko-versiosta.

combi_rule erisnimi ($vasen, $oikea, $sana):
  ? $oikea.luokka in @erisnimi;

  define $res := $vasen + <[alku: $sana] + $oikea>;
  if (omistusliite in $oikea.jatko) then
    result $res, rules omistusliite;
  end;
  if (liitesana in $oikea.jatko) then
    result $res, rules liitesana;
  end;
  if (tavuviiva in $oikea.jatko) then
    result $res, rules tavuviiva;
  end;
  if (loppu in $oikea.jatko) then
    result $res, rules loppu;
  end;
  result $res, rules erisnimen_sijapääte;
end;


# Paikannimestä johdettu kansallisuuden nimi tai
# sukunimestä johdettu aatteen kannattajan nimi.
#
combi_rule paikannimen_ja_sukunimen_lainen_johdos ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <paikannimi, sukunimi>;
#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  ? ($n = 1) or ($vasen.($n-1).jatko * <nimisana, nimi_laatusana, laatusana, etuliite> /= <>);
  ? johdin_lAinen in $oikea.jatko;

  result $vasen + <[alku: $sana] + $oikea>, rules paikannimen_ja_sukunimen_lainen_johdin;
end;


combi_rule paikannimen_ja_sukunimen_lainen_johdin ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = johdin_lAinen;
  ? äs_ok ($vasen.($n-1), $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

  define $res := $vasen + <[alku: $sana] + $oikea>;
  if tavuviiva in $oikea.jatko then
    result $res,
           rules nimisana, laatusana, ##### inen_johdos_nimisanasta,
                 nimi_laatusana, nimi_laatusanan_johdin,
                 yhdysmerkki_lainen_johdoksen_jälkeen,
                 teonsanasta_johdettu_nimisana;
  end;

#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;

  if liitesana in $oikea.jatko then
    result $res, rules liitesana;
  end;

   result $res,
          rules sijapääte, laatusanan_johdos,
          voitto_yliaste,  # Helsinkiläisempi, helsinkiläisin.
          paikannimen_ja_sukunimen_lainen_johdos,
          loppu;
end;


combi_rule erisnimen_sijapääte ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = sijapääte;

#define $nimi := transmit (<$vasen> + <$oikea> + <$sana>);

  define $edellinen := $vasen.length ($vasen);

#define $e := transmit ($edellinen);

  ? $oikea.sija in $edellinen.jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

  ? ($oikea.sija /= sisätulento_Vn)  or sisätulento_Vn_oikein  ($oikea.sija, substring($edellinen.alku,1R), $sana);
  ? ($oikea.sija /= sisätulento_hVn) or sisätulento_hVn_oikein ($oikea.sija, substring($edellinen.alku,1R), $sana);

  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($oikea.sija = kerronto_sti) then
    ? $edellinen.luokka in @sti_edeltäjä;
  elseif ($oikea.sija = omanto_n) then
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    result $res, rules yhdysmerkki_erisnimen_jälkeen, johdettu_paikannimi, johdettu_etunimi;
  else
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    if tavuviiva in $oikea.jatko then
      result $res, rules yhdysmerkki_erisnimen_jälkeen;
    end;
  end;
  result $res, rules liitesana, loppu;
end;


combi_rule yhdysmerkki_erisnimen_jälkeen ($vasen, $oikea, $sana):
  ? $oikea.luokka = tavuviiva;

  define $res := $vasen + <[alku: $sana] + $oikea>;
  result $res, rules
         nimisana, erisnimi, laatusana, nimi_laatusana,
         etuliite,
         teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
         paikannimen_ja_sukunimen_lainen_johdos;
end;


combi_rule johdettu_paikannimi ($vasen, $oikea, $sana):
  ? $oikea.paikannimen_jälkiliite = yes;
  define $res := $vasen + <[alku: $sana] + $oikea>;
  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  result $res, rules
         erisnimen_sijapääte,
         liitesana,
         yhdysmerkki_erisnimen_jälkeen;
end;


combi_rule johdettu_etunimi ($vasen, $oikea, $sana):
  ? $oikea.etunimen_jälkiliite = yes;
  define $res := $vasen + <[alku: $sana] + $oikea>;
  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  result $res, rules
         erisnimen_sijapääte,
         liitesana,
         yhdysmerkki_erisnimen_jälkeen;
end;


# inen-johdinta edeltävä nimisana.
# Sääntö ei voi olla sanan alussa.
#
combi_rule inen_johdos_nimisanasta ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in <nimisana, nimi_laatusana>;
  ? johdin_inen in $oikea.jatko;
  ? $oikea.tiedot = nil or
    (not ei_ys in $oikea.tiedot and not ei_ysj in $oikea.tiedot);

#  define $a := transmit ($vasen);
#  define $b := transmit ($oikea.jatko);
#  define $c := transmit ($sana);
#  define $d := transmit ($n);

  result $vasen + <[alku: $sana] + $oikea>, rules johdin_inen;
end;


combi_rule johdin_inen ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka = johdin_inen;

#  define $ö := transmit ("AAAAAAAAAAAAAAA");
#  define $a := transmit ($vasen);
#  define $b := transmit ($oikea.jatko);
#  define $c := transmit ($sana);
#  define $d := transmit ($n);
#  define $x := transmit ("BBBBBBBBBBBBBBB");

  define $i := $n - 1;
  ? äs_ok ($vasen.$i, $oikea);

  if $oikea.äs = aä then $oikea.äs := $vasen.$i.äs; end;

##  $oikea.äs := $vasen.i.äs;
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if tavuviiva in $oikea.jatko then
    result $res, rules nimisana, laatusana,
           tavuviiva, etuliite;
  end;
  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  if liitesana in $oikea.jatko then
    result $res, rules liitesana;
  end;
  result $vasen + <[alku: $sana] + $oikea>,
#        rules laatusanan_sijapääte,
        rules sijapääte,
        laatusanan_johdos,
        voitto_yliaste;
end;


combi_rule yhdysmerkki_lainen_johdoksen_jälkeen ($vasen, $oikea, $sana):
  ? $oikea.luokka = tavuviiva;

#  define $res := $vasen + <[vokaaliehto: no, alku: "-"] + $oikea>;
  define $res := $vasen + <[alku: $sana] + $oikea>;
  result $res, rules loppu;
  result $res,
         rules erisnimi, paikannimen_ja_sukunimen_lainen_johdos,
               nimisana, laatusana, nimi_laatusana;
##             inen_päätteinen_laatusana;
end;


subrule äs_ok ($edellinen, $oikea):
#define $a := transmit (<"äs_ok"> + <$edellinen.äs> + <$oikea.äs>);
#define $b := transmit (<"äs_ok"> + <$edellinen>);
#define $c := transmit (<"äs_ok"> + <$oikea>);

  return ($oikea.äs = aä or $edellinen.äs = aä or $oikea.äs = $edellinen.äs);
end;


combi_rule nimi_laatusanan_johdin ($vasen, $oikea, $sana, $n):
  ? $oikea.luokka in @nimi_laatusanan_johdin;
  ? $oikea.luokka in $vasen.($n-1).jatko;
  ? äs_ok ($vasen.($n-1), $oikea);

#define $b := transmit ($oikea.luokka);
#define $a := transmit ($vasen);
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  if $oikea.äs = aä then $oikea.äs := $vasen.($n-1).äs; end;

  result $vasen + <[alku: $sana] + $oikea>,
         rules sijapääte, omistusliite,
               liitesana, liitesana2,
               nimisana,
               laatusana,
               nimi_laatusana,
               laatusanan_johdos,
               teonsanasta_johdettu_nimisana, teonsanasta_johdettu_laatusana,
               nimisanasta_johdettu_nimisana, nimisanasta_johdettu_laatusana,
               nimi_laatusanan_johdin,
               paikannimen_ja_sukunimen_lainen_johdos,
               voitto_yliaste,
               tavuviiva,
               etuliite,
               loppu;
end;


##########################################

# Lukusanojen käsittely on muokattu Harri Pitkäsen Voikko-versiosta.

combi_rule lukusana ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  define $sijamuoto := nil;
  define $luku := nil;

  if ($oikea.alaluokka = erikoisluku) then
    define $erikoisres := $vasen + <[alku: $sana] + $oikea>;
    if (loppu in $oikea.jatko) then
      result $erikoisres, rules loppu;
    end;
    if (liitesana in $oikea.jatko) then
      result $erikoisres, rules liitesana;
    end;
    if (lukusanan_jälkiliite in $oikea.jatko) then
      result $erikoisres, rules lukusanan_jälkiliite;
    end;
    stop;
  end;

  if ($index greater 1) then
    define $edellinen := $vasen.($index - 1);
    ? $edellinen.lukutyyppi = nil or $edellinen.lukutyyppi = $oikea.lukutyyppi;
    $sijamuoto := $edellinen.sijamuoto;
    $luku := $edellinen.luku;
    # Estetään peräkkäiset "satasata", "tuhattuhat" jne.
    if ($edellinen.luokka = lukusana) then
      ? not ($oikea.alaluokka = $edellinen.alaluokka);
    end;
  end;
  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, luku: $luku] + $oikea>;

  if ($oikea.alaluokka = yksiyhdeksän) then
    if (nimentö in $oikea.jatko) then
      if loppu in $oikea.jatko then
        result $res, rules loppu;
      end;
      result $res, rules lukusana_toista, lukusana_sisäkerroin,
                   liitesana, lukusanan_jälkiliite,
                   nimisana, laatusana;
##                   nimisana, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
    end;
    if (omistusliite in $oikea.jatko) then
      result $res, rules omistusliite;
    end;
  elseif ($oikea.alaluokka in <kymmenen, sata>) then
    if (nimentö in $oikea.jatko) then
      if omistusliite in $oikea.jatko then
        result $res, rules omistusliite;
      end;
      if loppu in $oikea.jatko then
        result $res, rules loppu;
      end;
      result $res, rules lukusana_sisäkerroin,
                   liitesana, lukusanan_jälkiliite,
                   nimisana, laatusana;
##                   nimisana, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
    end;
  elseif ($oikea.alaluokka in <tuhat, miljoona, miljardi, biljoona, triljoona> and nimentö in $oikea.jatko) then
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    if loppu in $oikea.jatko then
      result $res, rules loppu;
    end;
    result $res, rules liitesana, lukusanan_jälkiliite,
                 nimisana, laatusana;
##                 nimisana, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
   elseif ($oikea.alaluokka = numeromerkki) then
     $res := $vasen + <[alku: $sana, pilkku: no, rakenne: "=q"] + $oikea>;
     result $res, rules lukusana_numero; ##, yhdysmerkki_numeron_jälkeen;
     result $res, rules loppu;
  end;
  if ($oikea.alaluokka in <sata, tuhat>) then
    if (nimentö in $oikea.jatko) then
      result $res, rules lukusana;
    end;
  end;
  result $res, rules lukusana_sijapääte;

end;

combi_rule lukusana_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? äs_ok($edellinen, $oikea);
  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  define $sijamuoto := sijamuoto_sijasta($oikea.sija);
  ? $edellinen.sijamuoto = nil or $edellinen.sijamuoto = $sijamuoto;
  ? $edellinen.luku = nil or $edellinen.luku = $oikea.luku;

  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  if ($edellinen.alaluokka = yksiyhdeksän) then
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    result $res, rules lukusana_toista, lukusana_sisäkerroin,
                 liitesana, lukusanan_jälkiliite,
                 nimisana, laatusana;
##                 nimisana, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
  elseif ($edellinen.alaluokka in <kymmenen, sata>) then
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    result $res, rules lukusana_sisäkerroin, lukusanan_jälkiliite,
                 liitesana;
  else
    result $res, rules nimisana, lukusanan_jälkiliite, laatusana;
                       ##, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  if ($edellinen.alaluokka in <sata, tuhat>) then
    result $res, rules lukusana;
  end;
end;

combi_rule lukusana_sisäkerroin ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  ? $oikea.alaluokka in <kymmenen, sata, tuhat, miljoona, miljardi, biljoona>;

  define $edellinen := $vasen.($index - 1);
  ? $edellinen.lukutyyppi = $oikea.lukutyyppi;
  define $res := $vasen + <[alku: $sana, sijamuoto: $edellinen.sijamuoto, luku: $edellinen.luku] + $oikea>;

  if ($edellinen.sijamuoto in <nil, nimentö>) then
    result $res, rules lukusana_sisäkerroin_osanto;
  else
    result $res, rules lukusana_sisäkerroin_sijapääte;
  end;
  
  if ($oikea.lukutyyppi = järjestysluku) then
    result $res, rules lukusana;
  end;
  
  if ($oikea.lukutyyppi = järjestysluku and loppu in $oikea.jatko) then
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    if loppu in $oikea.jatko then
      result $res, rules loppu;
    end;
    result $res, rules liitesana;
  end;
end;

combi_rule lukusana_sisäkerroin_sijapääte ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? äs_ok($edellinen, $oikea);
  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  define $sijamuoto := sijamuoto_sijasta($oikea.sija);
  ? $edellinen.sijamuoto = nil or $edellinen.sijamuoto = $sijamuoto;
  ? $edellinen.luku = nil or $edellinen.luku = $oikea.luku;

  define $res := $vasen + <[alku: $sana, sijamuoto: $sijamuoto, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  result $res, rules lukusana,
               liitesana, lukusanan_jälkiliite,
               nimisana, laatusana;
##               nimisana, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
end;

combi_rule lukusana_sisäkerroin_osanto ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = sijapääte;
  define $edellinen := $vasen.($index - 1);
  ? $oikea.sija in $edellinen.jatko;
  ? sijamuoto_sijasta($oikea.sija) = osanto;
  ? äs_ok($edellinen, $oikea);
  if $oikea.äs = aä then $oikea.äs := $edellinen.äs; end;

  define $res := $vasen + <[alku: $sana, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  if omistusliite in $oikea.jatko then
    result $res, rules omistusliite;
  end;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  result $res, rules lukusana, lukusanan_jälkiliite,
               liitesana,
               nimisana, laatusana; ##, inen_johdos_nimisanasta, inen_päätteinen_laatusana;
end;

combi_rule lukusana_toista ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;
  ? $oikea.alaluokka = toista;
  define $edellinen := $vasen.($index - 1);
  define $res := $vasen + <[alku: $sana, sijamuoto: $edellinen.sijamuoto,
                            luku: $edellinen.luku, lukutyyppi: $edellinen.lukutyyppi] + $oikea>;
  if loppu in $oikea.jatko then
    result $res, rules loppu;
  end;
  result $res, rules lukusana_sisäkerroin,
                     liitesana, lukusanan_jälkiliite,
##                   inen_johdos_nimisanasta, inen_päätteinen_laatusana;
                     nimisana, laatusana;
end;

combi_rule lukusanan_jälkiliite ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka in <lukusanan_jälkiliite, tavuviiva>;
  # FIXME: yhdysviivan käyttöä ei vielä tueta lukusanan ja sen jälkiliitteen välissä
  define $res := $vasen + <[alku: $sana] + $oikea>;
  if $oikea.luokka = tavuviiva then
    result $res, rules loppu;
  else
    define $edellinen := $vasen.($index - 1);
    ? $edellinen.lukutyyppi = nil or $edellinen.lukutyyppi = $oikea.lukutyyppi;
    if omistusliite in $oikea.jatko then
      result $res, rules omistusliite;
    end;
    if tavuviiva in $oikea.jatko then
      result $res, rules tavuviiva;
    end;
    if loppu in $oikea.jatko then
      result $res, rules loppu;
    end;
    result $res,
           rules ## ei_ys_sijapääte,
                 sijapääte,
                 nimisana, etuliite,
                 liitesana,
                 nimisanan_johdos;
##                 nimisana_laatusanasta, nimisana_teonsanasta;
  end;
end;

combi_rule lukusana_numero ($vasen, $oikea, $sana, $index):
  ? $oikea.luokka = lukusana;

  define $edellinen := $vasen.($index - 1);
  $oikea :=+ [pilkku: $edellinen.pilkku];
  define $res := $vasen + <[alku: $sana] + $oikea>;

  if $oikea.alaluokka = numeromerkki then
    result $res, rules lukusana_numero; ##, yhdysmerkki_numeron_jälkeen;
    result $res, rules loppu;
  elseif $oikea.alaluokka = pilkku and $oikea.pilkku /= yes then
    $oikea :=+ [pilkku: yes];
    result $vasen + <[alku: $sana] + $oikea>, rules lukusana_numero;
  end;
end;


# Aputaulukko funktiolle sijamuoto_sijasta
define @sijamuoto_sijasta_muunnostaulu :=
[nimentö:     nimentö,
 nimentö_t:   nimentö,
 nimentö_tkA: nimentö,
 omanto_n:    omanto,
 omanto_nkA:  omanto,
 omanto_ien:  omanto,
 omanto_jen:  omanto,
 omanto_en:   omanto,
 omanto_in:   omanto,
 omanto_ten:  omanto,
 omanto_iT:   omanto,
 omanto_idän: omanto,
 kohdanto_idät: kohdanto,
 kohdanto_t: kohdanto,
 osanto_A:    osanto,
 osanto_AA:   osanto,
 osanto_iA:   osanto,
 osanto_jA:   osanto,
 osanto_ttA:  osanto,
 osanto_itA:  osanto,
 osanto_tA:   osanto,
 olento_nA:   olento,
 olento_inA:  olento,
 tulento_ksi: tulento,
 tulento_iksi: tulento,
 sisäolento_ssA: sisäolento,
 sisäolento_issA: sisäolento,
 sisäolento_nA: sisäolento,
 sisäeronto_stA: sisäeronto,
 sisäeronto_istA: sisäeronto,
 sisäeronto_tA: sisäeronto,
 sisätulento_Vn: sisätulento,
 sisätulento_VVn: sisätulento,
 sisätulento_hVn: sisätulento,
 sisätulento_iin: sisätulento,
 sisätulento_ihin: sisätulento,
 sisätulento_seen: sisätulento,
 sisätulento_isiin: sisätulento,
 sisätulento_sen: sisätulento,
 sisätulento_isin: sisätulento,
 sisätulento_nne: sisätulento,
 ulko_olento_llA: ulko_olento,
 ulko_olento_illA: ulko_olento,
 ulkoeronto_ltA: ulkoeronto,
 ulkoeronto_iltA: ulkoeronto,
 ulkotulento_lle: ulkotulento,
 ulkotulento_ille: ulkotulento,
 vajanto_ttA: vajanto,
 vajanto_ittA: vajanto,
 seuranto_ine: seuranto,
 keinonto_n: keinonto,
 keinonto_in: keinonto,
 tulento_s: tulento
];

# Palauttaa annettua sijasymbolia vastaavan sijamuodon
subrule sijamuoto_sijasta($sija):
  return @sijamuoto_sijasta_muunnostaulu.$sija;
end;


#robust_rule robust ($surface, $remain_input):
#  define $a := transmit (<"a"> + <$surface>);
#  define $b := transmit (<"b"> + <$remain_input>);
#end;


combi_rule yhdyssana ($vasen, $oikea, $sana, $n):
  define $i := $n - 1;

assert ($n-1 = length($vasen));

  if ($oikea.luokka = seikkasana and $oikea.tiedot /= nil and
      ys_perusosa in $oikea.tiedot and $vasen /= <>) then

    if (($vasen.$i.luokka = sijapääte and $vasen.$i.sija = omanto_n)
        or (($vasen.$i.perusmuoto /= nil)
            and (($vasen.$i.perusmuoto = $vasen.$i.alku)
                 or (($vasen.$i.perusmuoto matches ".*inen") and ($vasen.$i.alku matches ".+is") and
                     (not ($vasen.$i.luokka in <johdin_llinen, johdin_inen>)))))) then


      if (loppu in $oikea.jatko) then
        result $vasen + <[alku: $sana] + $oikea>, rules loppu;
      end;
      if (liitesana in $oikea.jatko) then
         result $vasen + <[alku: $sana] + $oikea>, rules liitesana;
      end;
      if (omistusliite in $oikea.jatko) then
        result $vasen + <[alku: $sana] + $oikea>, rules omistusliite;
      end;
      result $vasen + <[alku: $sana] + $oikea>, rules sijapääte;
    end;
  end;

#foreach $z in $vasen: define $a := transmit (<$z.alku> + <$z.perusmuoto> + <$z.luokka>); end;
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);

  ? ($oikea.luokka in <etuliite, nimisana, laatusana, nimi_laatusana, teonsana, tavuviiva,
                       paikannimi, sukunimi>);
#    (($oikea.luokka = seikkasana and $oikea.tiedot /= nil) and ys_perusosa in $oikea.tiedot);

#  foreach $p in $n: define $q := transmit (<$p> + <$vasen.$p>); end;
#  foreach $ö in $vasen: define $a := transmit (<$ö.alku> + <$ö.perusmuoto> + <$ö.luokka>); end;
#  define $b := transmit ($oikea);
#  define $c := transmit ($sana);
#  define $d := transmit ($n);
#  define $e := transmit ($oikea.luokka);
#  define $f := transmit ($vasen.$i.jatko);
#  define $g := transmit (<$i> + <length($vasen)>);

  ? ($oikea.luokka in $vasen.$i.jatko) or
    ($oikea.luokka in <paikannimi, sukunimi, teonsana>);

#define $e := transmit ("eeeee");
#foreach $ö in $vasen: define $a := transmit (<$ö.alku> + <$ö.perusmuoto> + <$ö.luokka>); end;
#define $ä := transmit ($oikea);
#define $ž := transmit ($sana);
#define $š := transmit ($n);

  ? ($oikea.luokka = tavuviiva) or yhdyssana_oikein ($vasen, $oikea, $i);
#define $f := transmit ("fffff");
#foreach $ö in $vasen: define $a := transmit (<$ö.alku> + <$ö.perusmuoto> + <$ö.luokka>); end;
#define $ä := transmit ($oikea);
#define $ž := transmit ($sana);
#define $š := transmit ($n);

  define $res := $vasen + <[alku: $sana] + $oikea>;

  if ($oikea.luokka in <nimisana, laatusana, nimi_laatusana>) then
    if (tavuviiva in $oikea.jatko) then
      result $res, rules yhdyssana;
    end;
    if (omistusliite in $oikea.jatko) then
      result $res, rules omistusliite;
    end;
    if (liitesana in $oikea.jatko) then
      result $res, rules liitesana;
    end;
    if (liitesana2 in $oikea.jatko) then
      result $res, rules liitesana2;
    end;
    if (loppu in $oikea.jatko) then
      result $res, rules loppu;
    end;
    result $res, rules sijapääte;
  end;

  if ($oikea.luokka = nimisana) then
    if (@nimisanasta_johdettu_laatusana * $oikea.jatko /= <>) then
      result $res, rules nimisanasta_johdettu_laatusana;
    end;
    if (@nimisanasta_johdettu_nimisana * $oikea.jatko /= <>) then
      result $res, rules nimisanasta_johdettu_nimisana;
    end;
    if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
      result $res, rules nimi_laatusanan_johdin;
    end;

  elseif ($oikea.luokka = laatusana) then
    if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
      result $res, rules nimi_laatusanan_johdin;
    end;
    if (@voittoaste + @yliaste * $oikea.jatko /= <>) then
      result $res, rules voitto_yliaste;
    end;
#    if (@johdin_laatusanasta + @johdin_nimisanasta * $oikea.jatko /= <>) then
#      result $res, rules laatusanan_johdos;
#    end;
    if (@laatusanasta_johdettu_nimisana * $oikea.jatko /= <>) then
      result $res, rules laatusanasta_johdettu_nimisana;
    end;
  elseif ($oikea.luokka = nimi_laatusana) then
#foreach $z in $vasen: define $a := transmit (<$z.alku> + <$z.perusmuoto> + <$z.luokka>); end;
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);
    if ((voittoaste in $oikea.jatko) or (yliaste in $oikea.jatko)) then
      result $res, rules voitto_yliaste;
    end;
    if (@laatusanasta_johdettu_nimisana * $oikea.jatko /= <>) then
      result $res, rules laatusanasta_johdettu_nimisana;
    end;
    if (@nimisanasta_johdettu_nimisana * $oikea.jatko /= <>) then
      result $res, rules nimisanasta_johdettu_nimisana;
    end;
    if (@nimi_laatusanan_johdin * $oikea.jatko /= <>) then
      result $res, rules nimi_laatusanan_johdin;
    end;
    if (@nimisanasta_johdettu_laatusana * $oikea.jatko /= <>) then
      result $res, rules nimisanasta_johdettu_laatusana;
    end;

  elseif ($oikea.luokka = teonsana) then
#foreach $z in $vasen: define $a := transmit (<$z.alku> + <$z.perusmuoto> + <$z.luokka>); end;
#define $b := transmit ($oikea);
#define $c := transmit ($sana);
#define $d := transmit ($n);
    ? $oikea.jatko * (@teonsanasta_johdettu_nimisana + @teonsanasta_johdettu_laatusana) /= <>;
    ? yhdyssana_oikein ($vasen, $oikea, $i);
    ? $vasen.$i.jatko * <nimisana, nimi_laatusana, laatusana, etuliite> /= <>;
    result $res, rules teonsanan_johdos_nimisana, teonsanan_johdos_laatusana;

  elseif ($oikea.luokka = etuliite) then
    ? (<nimisana, nimi_laatusana, laatusana> * $oikea.jatko /= <>);
    result $res, rules yhdyssana;

  elseif ($oikea.luokka = tavuviiva) then
    result $res, rules yhdyssana, erisnimi, asemosana, apusana, lyhennesääntö, loppu;

  elseif ($oikea.luokka in <paikannimi, sukunimi>) then
    ? johdin_lAinen in $oikea.jatko;
    result $res, rules paikannimen_ja_sukunimen_lainen_johdin;
  end;
  stop;
end;
