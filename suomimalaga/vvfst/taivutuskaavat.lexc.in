LEXICON NOUN Valo
<WC>Valo_s_<A>	;
<O>:<O>	<WC>Valo_sl_<A>	;
<WC>Valo_w_<A>	;

LEXICON NOUN Ruoko
k<O>:k<O>	<WC>Valo_s_<A>	;
k<O><O>:k<O><O>	<WC>Valo_sl_<A>	;
'<O>:'<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Alku
k<U>:k<U>	<WC>Valo_s_<A>	;
k<U><U>:k<U><U>	<WC>Valo_sl_<A>	;
<U>:<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Luku
k<U>:k<U>	<WC>Valo_s_<A>	;
k<U><U>:k<U><U>	<WC>Valo_sl_<A>	;
v<U>:v<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Tiuku
k<U>:k<U>	<WC>Valo_s_<A>	;
k<U><U>:k<U><U>	<WC>Valo_sl_<A>	;
'<U>:'<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Aalto
t<O>:t<O>	<WC>Valo_s_<A>	;
t<O><O>:t<O><O>	<WC>Valo_sl_<A>	;
l<O>:l<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Anto
t<O>:t<O>	<WC>Valo_s_<A>	;
t<O><O>:t<O><O>	<WC>Valo_sl_<A>	;
n<O>:n<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Lintu
t<U>:t<U>	<WC>Valo_s_<A>	;
t<U><U>:t<U><U>	<WC>Valo_sl_<A>	;
n<U>:n<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Hanko
k<O>:k<O>	<WC>Valo_s_<A>	;
k<O><O>:k<O><O>	<WC>Valo_sl_<A>	;
g<O>:g<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Hattu
t<U>:t<U>	<WC>Valo_s_<A>	;
t<U><U>:t<U><U>	<WC>Valo_sl_<A>	;
<U>:<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Liitto
t<O>:t<O>	<WC>Valo_s_<A>	;
t<O><O>:t<O><O>	<WC>Valo_sl_<A>	;
<O>:<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Hinku
k<U>:k<U>	<WC>Valo_s_<A>	;
k<U><U>:k<U><U>	<WC>Valo_sl_<A>	;
g<U>:g<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Hoppu
p<U>:p<U>	<WC>Valo_s_<A>	;
p<U><U>:p<U><U>	<WC>Valo_sl_<A>	;
<U>:<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Kaarto
t<O>:t<O>	<WC>Valo_s_<A>	;
t<O><O>:t<O><O>	<WC>Valo_sl_<A>	;
r<O>:r<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Kippo
p<O>:p<O>	<WC>Valo_s_<A>	;
p<O><O>:p<O><O>	<WC>Valo_sl_<A>	;
<O>:<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Sampo
p<O>:p<O>	<WC>Valo_s_<A>	;
p<O><O>:p<O><O>	<WC>Valo_sl_<A>	;
m<O>:m<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Kumpu
p<U>:p<U>	<WC>Valo_s_<A>	;
p<U><U>:p<U><U>	<WC>Valo_sl_<A>	;
m<U>:m<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Laatu
t<U>:t<U>	<WC>Valo_s_<A>	;
t<U><U>:t<U><U>	<WC>Valo_sl_<A>	;
d<U>:d<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Apu
p<U>:p<U>	<WC>Valo_s_<A>	;
p<U><U>:p<U><U>	<WC>Valo_sl_<A>	;
v<U>:v<U>	<WC>Valo_w_<A>	;

LEXICON NOUN Lepo
p<O>:p<O>	<WC>Valo_s_<A>	;
p<O><O>:p<O><O>	<WC>Valo_sl_<A>	;
v<O>:v<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Leuto
t<O>:t<O>	<WC>Valo_s_<A>	;
t<O><O>:t<O><O>	<WC>Valo_sl_<A>	;
d<O>:d<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Verkko
k<O>:k<O>	<WC>Valo_s_<A>	;
k<O><O>:k<O><O>	<WC>Valo_sl_<A>	;
<O>:<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Vihko
k<O>:k<O>	<WC>Valo_s_<A>	;
k<O><O>:k<O><O>	<WC>Valo_sl_<A>	;
<O>:<O>	<WC>Valo_w_<A>	;

LEXICON NOUN Valo_w
[Sn][Nm]t:t	Liitesana_<A>	;
[Sg][Ny]n:n	<WC>NormaaliYsJatko_<A>	;
<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]tt<A>:tt<A>	Liitesana_<A>	;
?Nimisana	Johdin_tOn_<A>	;
?Laatusana	Voittoaste_<A>	;
?Laatusana	Yliaste_<A>	;

LEXICON NOUN Valo_s
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
?NotLaatusana	[Sn][Ny]:	Omistusliite_<A>	;
[Sg][Nm]jen:jen	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A>:<A>	Liitesana_<A>	;
[Sp][Nm]j<A>:j<A>	<WC>LiOlV_<A>	;
[Sill][Nm]ihin:ihin	Liitesana_<A>	;
[Ses][Ny]n<A>:n<A>	<WC>LiOlV_<A>	;

LEXICON NOUN Valo_sl
[Sill][Ny]n:n	Liitesana_<A>	;

LEXICON NOUN Arvelu
<WC>Arvelu_s_<A>	;
[[L]]:[[L]]	<WC>Arvelu_sl_<A>	;
<WC>Arvelu_w_<A>	;

LEXICON NOUN Alanko
k<O>:k<O>	<WC>Arvelu_s_<A>	;
k<O><O>:k<O><O>	<WC>Arvelu_sl_<A>	;
g<O>:g<O>	<WC>Arvelu_w_<A>	;

LEXICON NOUN Avanto
t<O>:t<O>	<WC>Arvelu_s_<A>	;
t<O><O>:t<O><O>	<WC>Arvelu_sl_<A>	;
n<O>:n<O>	<WC>Arvelu_w_<A>	;

LEXICON NOUN Arvelu_s
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]jen:jen	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A>:<A>	Liitesana_<A>	;
[Sp][Nm]j<A>:j<A>	Liitesana_<A>	;
[Sill][Nm]ihin:ihin	Liitesana_<A>	;
[Ses][Ny]n<A>:n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;

LEXICON NOUN Arvelu_sl
[Sill][Ny]n:n	Liitesana_<A>	;

LEXICON NOUN Arvelu_w
[Sn][Nm]t:t	Liitesana_<A>	;
[Sg][Ny]n:n	<WC>NormaaliYsJatko_<A>	;
[Sp][Nm]it<A>:it<A>	Liitesana_<A>	;
<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]tt<A>:tt<A>	Liitesana_<A>	;

LEXICON NOUN Laatikko
[Sn][Ny]k<O>:k<O>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]<O>t:<O>t	Liitesana_<A>	;
[Sg][Ny]<O>n:<O>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]k<O>jen:k<O>jen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]k<O><A>:k<O><A>	Liitesana_<A>	;
[Sp][Nm]k<O>j<A>:k<O>j<A>	Liitesana_<A>	;
[Sp][Nm]<O>it<A>:<O>it<A>	Liitesana_<A>	;
[Ses][Ny]k<O>n<A>:k<O>n<A>	Liitesana_<A>	;
[Ses][Nm]k<O>in<A>:k<O>in<A>	Liitesana_<A>	;
[Sill][Ny]k<O><O>n:k<O><O>n	Liitesana_<A>	;
[Sill][Nm]k<O>ihin:k<O>ihin	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
<O>:<O>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<O>tt<A>:<O>tt<A>	Liitesana_<A>	;
?Laatusana	s:s	Johdin_UUs_<A>	;

LEXICON NOUN Paperi
<WC>Paperi_s_<A>	;
<WC>Paperi_w_<A>	;

LEXICON NOUN Kajakki
k:k	<WC>Paperi_s_<A>	;
<WC>Paperi_w_<A>	;

LEXICON NOUN Paperi_s
[Sn][Ny]i:i	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]i<A>:i<A>	Liitesana_<A>	;
[Sp][Nm]ej<A>:ej<A>	Liitesana_<A>	;
[Sill][Ny]iin:iin	Liitesana_<A>	;
[Sill][Nm]eihin:eihin	Liitesana_<A>	;
[Ses][Ny]in<A>:in<A>	Liitesana_<A>	;
[Ses][Nm]ein<A>:ein<A>	Liitesana_<A>	;

LEXICON NOUN Paperi_w
[Sn][Nm]it:it	Liitesana_<A>	;
[Sg][Ny]in:in	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eiden:eiden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eitten:eitten	<WC>NormaaliYsJatko_<A>	;
[Sp][Nm]eit<A>:eit<A>	Liitesana_<A>	;
i:i	<WC>YhteisetYksikönPaikallissijat_<A>	;
e:e	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]itt<A>:itt<A>	Liitesana_<A>	;

LEXICON NOUN Autio
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]t:t	Liitesana_<A>	;
[Sg][Ny]n:n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]iden:iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]itten:itten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]t<A>:t<A>	Liitesana_<A>	;
[Sp][Nm]it<A>:it<A>	Liitesana_<A>	;
[Ses][Ny]n<A>:n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[[L]][Sill][Ny]n:[[L]]n	Liitesana_<A>	;
[Sill][Nm]ihin:ihin	Liitesana_<A>	;
<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]tt<A>:tt<A>	Liitesana_<A>	;
?Laatusana	Voittoaste_<A>	;
?Laatusana	Yliaste_<A>	;

LEXICON NOUN Risti
<WC>Risti_s_<A>	;
<WC>Risti_w_<A>	;

LEXICON NOUN Takki
k:k	<WC>Risti_s_<A>	;
<WC>Risti_w_<A>	;

LEXICON NOUN Tatti
t:t	<WC>Risti_s_<A>	;
<WC>Risti_w_<A>	;

LEXICON NOUN Tunti
t:t	<WC>Risti_s_<A>	;
n:n	<WC>Risti_w_<A>	;

LEXICON NOUN Keppi
p:p	<WC>Risti_s_<A>	;
<WC>Risti_w_<A>	;

LEXICON NOUN Vati
t:t	<WC>Risti_s_<A>	;
d:d	<WC>Risti_w_<A>	;

LEXICON NOUN Pelti
t:t	<WC>Risti_s_<A>	;
l:l	<WC>Risti_w_<A>	;

LEXICON NOUN Renki
k:k	<WC>Risti_s_<A>	;
g:g	<WC>Risti_w_<A>	;

LEXICON NOUN Hupi
p:p	<WC>Risti_s_<A>	;
v:v	<WC>Risti_w_<A>	;

LEXICON NOUN Risti_s
[Sn][Ny]i:i	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]i<A>:i<A>	Liitesana_<A>	;
[Sp][Nm]ej<A>:ej<A>	Liitesana_<A>	;
[Sill][Ny]iin:iin	Liitesana_<A>	;
[Sill][Nm]eihin:eihin	Liitesana_<A>	;
[Ses][Ny]in<A>:in<A>	Liitesana_<A>	;
?Nimisana	i:i	Johdin_mAinen_<A>	;
?Nimisana	Johdin_inen_vain_lipulla_<A>	;

LEXICON NOUN Risti_w
[Sn][Nm]it:it	Liitesana_<A>	;
[Sg][Ny]in:in	<WC>NormaaliYsJatko_<A>	;
i:i	<WC>YhteisetYksikönPaikallissijat_<A>	;
e:e	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]itt<A>:itt<A>	Liitesana_<A>	;
?Nimisana	i:i	Johdin_tOn_<A>	;
?Laatusana	i:i	Kerronto_sti_<A>	;

LEXICON NOUN Kalsium
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]it:it	Liitesana_<A>	;
[Sg][Ny]in:in	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]i<A>:i<A>	Liitesana_<A>	;
[Sp][Nm]ej<A>:ej<A>	Liitesana_<A>	;
[Ses][Ny]in<A>:in<A>	Liitesana_<A>	;
[Ses][Nm]ein<A>:ein<A>	Liitesana_<A>	;
[Sill][Ny]iin:iin	Liitesana_<A>	;
[Sill][Nm]eihin:eihin	Liitesana_<A>	;
i:i	<WC>YhteisetYksikönPaikallissijat_<A>	;
e:e	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]itt<A>:itt<A>	Liitesana_<A>	;

LEXICON NOUN Lovi
<WC>Lovi_s_<A>	;
<WC>Lovi_w_<A>	;

LEXICON NOUN Kaikki
k:k	<WC>Lovi_s_<A>	;
<WC>Lovi_w_<A>	;

LEXICON NOUN Happi
p:p	<WC>Lovi_s_<A>	;
<WC>Lovi_w_<A>	;

LEXICON NOUN Hanki
k:k	<WC>Lovi_s_<A>	;
g:g	<WC>Lovi_w_<A>	;

LEXICON NOUN Lampi
! TODO yksikkö "Lammin"
p:p	<WC>Lovi_s_<A>	;
m:m	<WC>Lovi_w_<A>	;

LEXICON NOUN Sampi
p:p	<WC>Lovi_s_<A>	;
m:m	<WC>Lovi_w_<A>	;

LEXICON NOUN Kylki
k:k	<WC>Lovi_s_<A>	;
j:j	<WC>Lovi_w_<A>	;

LEXICON NOUN Lahti
t:t	<WC>Lovi_s_<A>	;
d:d	<WC>Lovi_w_<A>	;

LEXICON NOUN Lovi_s
[Sn][Ny]i:i	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]e<A>:e<A>	Liitesana_<A>	;
[Sp][Nm]i<A>:i<A>	Liitesana_<A>	;
[Ses][Ny]en<A>:en<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]een:een	Liitesana_<A>	;
[Sill][Nm]iin:iin	Liitesana_<A>	;
[Ses][Ny]in<A>:in<A>	Liitesana_<A>	;
?Nimisana	i:i	Johdin_mAinen_<A>	;

LEXICON NOUN Lovi_w
[Sn][Nm]et:et	Liitesana_<A>	;
[Sg][Ny]en:en	<WC>NormaaliYsJatko_<A>	;
e:e	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]ett<A>:ett<A>	Liitesana_<A>	;
?Nimisana	e:e	Johdin_tOn_<A>	;

LEXICON NOUN Kala
<WC>Kala_s_<A>	;
<WC>Kala_w_<A>	;

LEXICON NOUN Aitta
t:t	<WC>Kala_s_<A>	;
<WC>Kala_w_<A>	;

LEXICON NOUN Kappa
p:p	<WC>Kala_s_<A>	;
<WC>Kala_w_<A>	;

LEXICON NOUN Napa
p:p	<WC>Kala_s_<A>	;
v:v	<WC>Kala_w_<A>	;

LEXICON NOUN Kampa
p:p	<WC>Kala_s_<A>	;
m:m	<WC>Kala_w_<A>	;

LEXICON NOUN Hanka
k:k	<WC>Kala_s_<A>	;
g:g	<WC>Kala_w_<A>	;

LEXICON NOUN Raaka
k:k	<WC>Kala_s_<A>	;
':'	<WC>Kala_w_<A>	;

LEXICON NOUN Kanta
t:t	<WC>Kala_s_<A>	;
n:n	<WC>Kala_w_<A>	;

LEXICON NOUN Parta
t:t	<WC>Kala_s_<A>	;
r:r	<WC>Kala_w_<A>	;

LEXICON NOUN Valta
t:t	<WC>Kala_s_<A>	;
l:l	<WC>Kala_w_<A>	;

LEXICON NOUN Kala_s
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>jen:<O>jen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<A>in:<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]<O>j<A>:<O>j<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
?Nimisana	<A>:<A>	Johdin_mAinen_<A>	;
?Nimisana	<A>:<A>	Johdin_inen_vain_lipulla_<A>	;

LEXICON NOUN Kala_w
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;
?Nimisana	<A>:<A>	Johdin_tOn_<A>	;
?Laatusana	e:e	Voittoaste_<A>	;
?Laatusana	Yliaste_<A>	;
?Laatusana	<A>:<A>	Kerronto_sti_<A>	;

LEXICON NOUN Koira
<WC>Koira_s_<A>	;
<WC>Koira_w_<A>	;

LEXICON NOUN Juhta
t:t	<WC>Koira_s_<A>	;
d:d	<WC>Koira_w_<A>	;

LEXICON NOUN Kenttä
t:t	<WC>Koira_s_<A>	;
<WC>Koira_w_<A>	;

LEXICON NOUN Honka
k:k	<WC>Koira_s_<A>	;
g:g	<WC>Koira_w_<A>	;

LEXICON NOUN Kompa
p:p	<WC>Koira_s_<A>	;
m:m	<WC>Koira_w_<A>	;

LEXICON NOUN Suunta
t:t	<WC>Koira_s_<A>	;
n:n	<WC>Koira_w_<A>	;

LEXICON NOUN Tolppa
p:p	<WC>Koira_s_<A>	;
<WC>Koira_w_<A>	;

LEXICON NOUN Turta
t:t	<WC>Koira_s_<A>	;
r:r	<WC>Koira_w_<A>	;

LEXICON NOUN Kulta
t:t	<WC>Koira_s_<A>	;
l:l	<WC>Koira_w_<A>	;

LEXICON NOUN Hoikka
k:k	<WC>Koira_s_<A>	;
<WC>Koira_w_<A>	;

LEXICON NOUN Huopa
p:p	<WC>Koira_s_<A>	;
v:v	<WC>Koira_w_<A>	;

LEXICON NOUN Ikä
k:k	<WC>Koira_s_<A>	;
<WC>Koira_w_yksikko_<A>	;
':'	<WC>Koira_w_monikko_<A>	;

LEXICON NOUN Koira_w
<WC>Koira_w_yksikko_<A>	;
<WC>Koira_w_monikko_<A>	;

LEXICON NOUN Koira_w_yksikko
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;
?Laatusana	e:e	Voittoaste_<A>	;

LEXICON NOUN Koira_w_monikko
<WC>YhteisetMonikonPaikallissijat_<A>	;

LEXICON NOUN Koira_s
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<A>in:<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]i<A>:i<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]iin:iin	Liitesana_<A>	;
?Laatusana	Johdin_UUs_<A>	;

LEXICON NOUN Asema
<WC>Asema_s_<A>	;
<WC>Asema_w_<A>	;

LEXICON NOUN Emäntä
t:t	<WC>Asema_s_<A>	;
n:n	<WC>Asema_w_<A>	;

LEXICON NOUN Asema_s
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]i<A>:i<A>	Liitesana_<A>	;
[Sill][Ny]iin:iin	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;

LEXICON NOUN Asema_w
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;
?Nimisana	<A>:<A>	Johdin_tOn_<A>	;
?Laatusana	<A>:<A>	Voittoaste_<A>	;
?Laatusana	Yliaste_<A>	;
?Laatusana	<A>:<A>	Kerronto_sti_<A>	;
?Laatusana	Johdin_UUs_<A>	;

LEXICON NOUN Apila
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<A>in:<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]<O>it<A>:<O>it<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;
?Laatusana	Johdin_UUs_<A>	;

LEXICON NOUN Karahka
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>jen:<O>jen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<A>in:<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]<O>it<A>:<O>it<A>	Liitesana_<A>	;
[Sp][Nm]<O>j<A>:<O>j<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;

LEXICON NOUN Solakka
k:k	<WC>Solakka_s_<A>	;
<WC>Solakka_w_<A>	;

LEXICON NOUN Ulappa
p:p	<WC>Solakka_s_<A>	;
<WC>Solakka_w_<A>	;

LEXICON NOUN Savotta
t:t	<WC>Solakka_s_<A>	;
<WC>Solakka_w_<A>	;

LEXICON NOUN Solakka_w
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sp][Nm]<O>it<A>:<O>it<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;

LEXICON NOUN Solakka_s
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>jen:<O>jen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<A>in:<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]<O>j<A>:<O>j<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;

! Harhaanjohtava nimi, oikea esimerkkisana "hunaja"
LEXICON NOUN Apaja
[Sn][Ny]<A>:<A>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<A><A>:<A><A>	Liitesana_<A>	;
[Sp][Nm]i<A>:i<A>	Liitesana_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]<A><A>n:<A><A>n	Liitesana_<A>	;
[Sill][Nm]iin:iin	Liitesana_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;
?Laatusana	Johdin_UUs_<A>	;
?Nimisana	<A>:<A>	Johdin_inen_vain_lipulla_<A>	;

LEXICON NOUN Korkea
[Sn][Ny]e<A>:e<A>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]e<A>t:e<A>t	Liitesana_<A>	;
[Sg][Ny]e<A>n:e<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eiden:eiden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eitten:eitten	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]e<A>in:e<A>in	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]e<A><A>:e<A><A>	Liitesana_<A>	;
[Sp][Nm]eit<A>:eit<A>	Liitesana_<A>	;
[Ses][Ny]e<A>n<A>:e<A>n<A>	Liitesana_<A>	;
[Ses][Nm]ein<A>:ein<A>	Liitesana_<A>	;
[Sill][Ny]e<A><A>n:e<A><A>n	Liitesana_<A>	;
[Sill][Nm]eihin:eihin	Liitesana_<A>	;
[Sill][Nm]eisiin:eisiin	Liitesana_<A>	;
e<A>:e<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
e:e	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]e<A>tt<A>:e<A>tt<A>	Liitesana_<A>	;
?Laatusana	e<A>:e<A>	Voittoaste_<A>	;
?Laatusana	e:e	Yliaste_<A>	;
?Laatusana	e:e	Johdin_Us_<A>	;
?Nimisana	e<A>:e<A>	Johdin_tOn_<A>	;

LEXICON NOUN Suo
[Sn][Ny]<U><O>:<U><O>	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]<U><O>t:<U><O>t	Liitesana_<A>	;
[Sg][Ny]<U><O>n:<U><O>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>iden:<O>iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]<O>itten:<O>itten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<U><O>t<A>:<U><O>t<A>	Liitesana_<A>	;
[Sp][Nm]<O>it<A>:<O>it<A>	Liitesana_<A>	;
[Ses][Ny]<U><O>n<A>:<U><O>n<A>	Liitesana_<A>	;
[Ses][Nm]<O>in<A>:<O>in<A>	Liitesana_<A>	;
[Sill][Ny]<U><O>h<O>n:<U><O>h<O>n	Liitesana_<A>	;
[Sill][Nm]<O>ihin:<O>ihin	Liitesana_<A>	;
<U><O>:<U><O>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<O>:<O>	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<U><O>tt<A>:<U><O>tt<A>	Liitesana_<A>	;
?Nimisana	<U><O>:<U><O>	Johdin_tOn_<A>	;

LEXICON NOUN Tuohi
<WC>Lohi_<A>	;
[Sg][Nm]ten:ten	<WC>NormaaliYsJatko_<A>	;

LEXICON NOUN Lohi
[Sn][Ny]i:i	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]et:et	Liitesana_<A>	;
[Sg][Ny]en:en	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]t<A>:t<A>	Liitesana_<A>	;
[Sp][Nm]i<A>:i<A>	Liitesana_<A>	;
[Ses][Ny]en<A>:en<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]een:een	Liitesana_<A>	;
[Sill][Nm]iin:iin	Liitesana_<A>	;
e:e	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]ett<A>:ett<A>	Liitesana_<A>	;

LEXICON NOUN Susi
<WC>Tosi_<A>	;
[Sg][Nm]tten:tten	<WC>NormaaliYsJatko_<A>	;

LEXICON NOUN Tosi
[Sn][Ny]si:si	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]det:det	Liitesana_<A>	;
[Sg][Ny]den:den	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]sien:isen	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]tt<A>:tt<A>	Liitesana_<A>	;
[Sp][Nm]si<A>:si<A>	Liitesana_<A>	;
[Ses][Ny]ten<A>:ten<A>	Liitesana_<A>	;
[Ses][Nm]sin<A>:sin<A>	Liitesana_<A>	;
[Sill][Ny]teen:teen	Liitesana_<A>	;
[Sill][Nm]siin:siin	Liitesana_<A>	;
de:de	<WC>YhteisetYksikönPaikallissijat_<A>	;
s:s	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]dett<A>:dett<A>	Liitesana_<A>	;

LEXICON NOUN Tytär
[Sn][Ny]<A>r:<A>r	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]t<A>ret:t<A>ret	Liitesana_<A>	;
[Sine][Ny]t<A>ress<A>:t<A>ress<A>	Liitesana_<A>	;
[Sela][Ny]t<A>rest<A>:t<A>rest<A>	Liitesana_<A>	;
[Sill][Ny]t<A>reen:t<A>reen	Liitesana_<A>	;

LEXICON NOUN Nainen
[Sn][Ny]nen:nen	Liitesana_<A>	;
[Sn][Nm]set:set	Liitesana_<A>	;
[Sg][Ny]sen:sen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]sten:sten	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]sien:sien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]st<A>:st<A>	Liitesana_<A>	;
[Sp][Nm]si<A>:si<A>	Liitesana_<A>	;
[Ses][Ny]sen<A>:sen<A>	Liitesana_<A>	;
[Ses][Nm]sin<A>:sin<A>	Liitesana_<A>	;
[Sill][Ny]seen:seen	Liitesana_<A>	;
[Sill][Nm]siin:siin	Liitesana_<A>	;
se:se	<WC>YhteisetYksikönPaikallissijat_<A>	;
s:s	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]sett<A>:sett<A>	Liitesana_<A>	;
s:s	YhdyssanaJatkuu	;
?Laatusana	se:se	Voittoaste_<A>	;
?Laatusana	s:s	Yliaste_<A>	;
?Laatusana	s:s	Johdin_UUs_<A>	;
?Laatusana	se:se	Kerronto_sti_<A>	;
?Nimisana	s:s	Johdin_mAinen_<A>	;
?Nimisana	se:se	Johdin_tOn_<A>	;
?Sukunimi	s:s	SukunimiYsJatko	;
?Sukunimi	nen:nen	SukunimiYsJatko	;

LEXICON NOUN Vastaus
[Sn][Ny]s:s	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]kset:kset	Liitesana_<A>	;
[Sg][Ny]ksen:ksen	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ksien:ksien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]st<A>:st<A>	Liitesana_<A>	;
[Sp][Nm]ksi<A>:ksi<A>	Liitesana_<A>	;
[Ses][Ny]ksen<A>:ksen<A>	Liitesana_<A>	;
[Ses][Nm]ksin<A>:ksin<A>	Liitesana_<A>	;
[Sill][Ny]kseen:kseen	Liitesana_<A>	;
[Sill][Nm]ksiin:ksiin	Liitesana_<A>	;
kse:kse	<WC>YhteisetYksikönPaikallissijat_<A>	;
ks:ks	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]ksett<A>:ksett<A>	Liitesana_<A>	;

LEXICON NOUN Kalleus
[Sn][Ny]s:s	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]det:det	Liitesana_<A>	;
[Sg][Ny]den:den	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ksien:ksien	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]tt<A>:tt<A>	Liitesana_<A>	;
[Sp][Nm]ksi<A>:ksi<A>	Liitesana_<A>	;
[Ses][Ny]ten<A>:ten<A>	Liitesana_<A>	;
[Ses][Nm]ksin<A>:ksin<A>	Liitesana_<A>	;
[Sill][Ny]teen:teen	Liitesana_<A>	;
[Sill][Nm]ksiin:ksiin	Liitesana_<A>	;
de:de	<WC>YhteisetYksikönPaikallissijat_<A>	;
ks:ks	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]dett<A>:dett<A>	Liitesana_<A>	;

LEXICON NOUN Uistin
n:n	<WC>Uistin_n_<A>	;
m:m	<WC>Uistin_m_<A>	;

LEXICON NOUN Vaimennin
nin:nin	<WC>Uistin_n_<A>	;
tim:tim	<WC>Uistin_m_<A>	;

LEXICON NOUN Sivellin
lin:lin	<WC>Uistin_n_<A>	;
tim:tim	<WC>Uistin_m_<A>	;

LEXICON NOUN Kiharrin
rin:rin	<WC>Uistin_n_<A>	;
tim:tim	<WC>Uistin_m_<A>	;

LEXICON NOUN Kaadin
din:din	<WC>Uistin_n_<A>	;
tim:tim	<WC>Uistin_m_<A>	;

LEXICON NOUN Kaavin
vin:vin	<WC>Uistin_n_<A>	;
pim:pim	<WC>Uistin_m_<A>	;

LEXICON NOUN Suodatin
in:in	<WC>Uistin_n_<A>	;
tim:tim	<WC>Uistin_m_<A>	;

LEXICON NOUN Puin
in:in	<WC>Uistin_n_<A>	;
kim:kim	<WC>Uistin_m_<A>	;

LEXICON NOUN Poljin
jin:jin	<WC>Uistin_n_<A>	;
kim:kim	<WC>Uistin_m_<A>	;

LEXICON NOUN Uistin_n
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ten:ten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]t<A>:t<A>	Liitesana_<A>	;

LEXICON NOUN Uistin_m
[Sn][Nm]et:et	Liitesana_<A>	;
[Sg][Ny]en:en	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]ien:ien	<WC>NormaaliYsJatko_<A>	;
[Ses][Ny]en<A>:en<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]een:een	Liitesana_<A>	;
[Sill][Nm]iin:iin	Liitesana_<A>	;
e:e	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]ett<A>:ett<A>	Liitesana_<A>	;

LEXICON NOUN Alaston
<O>:<O>	<WC>Onneton_w_<A>	;
<O>:<O>	<WC>Onneton_s_<A>	;

LEXICON NOUN Onneton
<O>:<O>	<WC>Onneton_w_<A>	;
t<O>:t<O>	<WC>Onneton_s_<A>	;

LEXICON NOUN Onneton_w
[Sn][Ny]n:n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]nten:nten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]nt<A>:nt<A>	Liitesana_<A>	;

LEXICON NOUN Onneton_s
[Sn][Nm]m<A>t:m<A>t	Liitesana_<A>	;
[Sg][Ny]m<A>n:m<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]mien:mien	<WC>NormaaliYsJatko_<A>	;
[Ses][Ny]m<A>n<A>:m<A>n<A>	Liitesana_<A>	;
[Ses][Nm]min<A>:min<A>	Liitesana_<A>	;
[Sill][Ny]m<A><A>n:m<A><A>n	Liitesana_<A>	;
[Sill][Nm]miin:miin	Liitesana_<A>	;
m<A>:m<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
m:m	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]m<A>tt<A>:m<A>tt<A>	Liitesana_<A>	;

LEXICON NOUN Asiakas
<A>:<A>	<WC>Vieras_w_<A>	;
k<A>:k<A>	<WC>Vieras_s_<A>	;

LEXICON NOUN Kuollut
[Sn][Ny]<U>t:<U>t	<WC>NormaaliYsJatko_<A>	;
[Sn][Nm]eet:eet	Liitesana_<A>	;
[Sg][Ny]een:een	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eiden:eiden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]eitten:eitten	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]<U>tt<A>:<U>tt<A>	Liitesana_<A>	;
[Sp][Nm]eit<A>:eit<A>	Liitesana_<A>	;
[Ses][Ny]een<A>:een<A>	Liitesana_<A>	;
[Ses][Nm]ein<A>:ein<A>	Liitesana_<A>	;
[Sill][Ny]eeseen:eeseen	Liitesana_<A>	;
[Sill][Nm]eisiin:eisiin	Liitesana_<A>	;
ee:ee	<WC>YhteisetYksikönPaikallissijat_<A>	;
e:e	<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]eett<A>:eett<A>	Liitesana_<A>	;

LEXICON NOUN Hame
<WC>Hame_w_<A>	;
<WC>Hame_s_<A>	;

LEXICON NOUN Enne
ne:ne	<WC>Hame_w_<A>	;
te:te	<WC>Hame_s_<A>	;

LEXICON NOUN Helle
le:le	<WC>Hame_w_<A>	;
te:te	<WC>Hame_s_<A>	;

LEXICON NOUN Kierre
re:re	<WC>Hame_w_<A>	;
te:te	<WC>Hame_s_<A>	;

LEXICON NOUN Lumme
me:me	<WC>Hame_w_<A>	;
pe:pe	<WC>Hame_s_<A>	;

LEXICON NOUN Lahje
je:je	<WC>Hame_w_<A>	;
ke:ke	<WC>Hame_s_<A>	;

LEXICON NOUN Lape
e:e	<WC>Hame_w_<A>	;
pe:pe	<WC>Hame_s_<A>	;

LEXICON NOUN Sade
de:de	<WC>Hame_w_<A>	;
te:te	<WC>Hame_s_<A>	;

LEXICON NOUN Taive
ve:ve	<WC>Hame_w_<A>	;
pe:pe	<WC>Hame_s_<A>	;

LEXICON NOUN Tarvike
e:e	<WC>Hame_w_<A>	;
ke:ke	<WC>Hame_s_<A>	;

LEXICON NOUN Vaate
e:e	<WC>Hame_w_<A>	;
te:te	<WC>Hame_s_<A>	;

LEXICON NOUN Hame_w
[Sn][Ny]:	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]tt<A>:tt<A>	Liitesana_<A>	;

LEXICON NOUN Hame_s
[Sn][Nm]et:et	Liitesana_<A>	;
[Sg][Ny]en:en	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]iden:iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]itten:itten	<WC>NormaaliYsJatko_<A>	;
[Sp][Nm]it<A>:it<A>	Liitesana_<A>	;
[Ses][Ny]en<A>:en<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]eseen:eseen	Liitesana_<A>	;
[Sill][Nm]ihn:ihn	Liitesana_<A>	;
[Sill][Nm]isiin:isiin	Liitesana_<A>	;
e:e	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]ett<A>:ett<A>	Liitesana_<A>	;
?Laatusana	Johdin_Us_<A>	;
?Nimisana	e:e	Johdin_tOn_<A>	;

LEXICON NOUN Vieras
<WC>Vieras_w_<A>	;
<WC>Vieras_s_<A>	;

LEXICON NOUN Harras
r<A>:r<A>	<WC>Vieras_w_<A>	;
t<A>:t<A>	<WC>Vieras_s_<A>	;

LEXICON NOUN Vieras_w
[Sn][Ny]s:s	<WC>NormaaliYsJatko_<A>	;
[Sp][Ny]st<A>:st<A>	Liitesana_<A>	;

LEXICON NOUN Vieras_s
[Sn][Nm]<A>t:<A>t	Liitesana_<A>	;
[Sg][Ny]<A>n:<A>n	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]iden:iden	<WC>NormaaliYsJatko_<A>	;
[Sg][Nm]itten:itten	<WC>NormaaliYsJatko_<A>	;
[Ses][Ny]<A>n<A>:<A>n<A>	Liitesana_<A>	;
[Ses][Nm]in<A>:in<A>	Liitesana_<A>	;
[Sill][Ny]<A>seen:<A>seen	Liitesana_<A>	;
[Sill][Nm]<A>ihn:<A>ihn	Liitesana_<A>	;
<A>:<A>	<WC>YhteisetYksikönPaikallissijat_<A>	;
<WC>YhteisetMonikonPaikallissijat_<A>	;
[Sab][Ny]<A>tt<A>:<A>tt<A>	Liitesana_<A>	;

LEXICON NOUN NormaaliYsJatko
Liitesana_<A>	;
<WC>YsJatko	;

LEXICON NOUN YhteisetYksikönPaikallissijat
[Str][Ny]ksi:ksi	<WC>LiOlI_<A>	;
[Sine][Ny]ss<A>:ss<A>	<WC>LiOlV_<A>	;
[Sela][Ny]st<A>:st<A>	<WC>LiOlV_<A>	;
[Sade][Ny]ll<A>:ll<A>	<WC>LiOlV_<A>	;
[Sabl][Ny]lt<A>:lt<A>	<WC>LiOlV_<A>	;
[Sall][Ny]lle:lle	<WC>LiOlV_<A>	;

LEXICON NOUN YhteisetMonikonPaikallissijat
[Str][Nm]iksi:iksi	<WC>LiOlI_<A>	;
[Sine][Nm]iss<A>:iss<A>	<WC>LiOlV_<A>	;
[Sela][Nm]ist<A>:ist<A>	<WC>LiOlV_<A>	;
[Sade][Nm]ill<A>:ill<A>	<WC>LiOlV_<A>	;
[Sabl][Nm]ilt<A>:ilt<A>	<WC>LiOlV_<A>	;
[Sall][Nm]ille:ille	<WC>LiOlV_<A>	;
[Sab][Nm]itt<A>:itt<A>	<WC>LiOlV_<A>	;
[Sin][Nm]in:in	Liitesana_<A>	;

LEXICON NOUN LiOlV
Liitesana_<A>	;
?NotLaatusana	Omistusliite_<A>	;
?NotLaatusana	[[L]]n:[[L]]n	Liitesana_<A>	;

LEXICON NOUN LiOlI
Liitesana_<A>	;
?NotLaatusana	[[IE]]:[[IE]]	Omistusliite_<A>	;
?NotLaatusana	[[IE]][[L]]n:[[IE]][[L]]n	Liitesana_<A>	;

LEXICON Punoa
<WC>Punoa_w_<A>	;
<WC>Punoa_s_<A>	;

LEXICON Tempoa
m<O>:m<O>	<WC>Punoa_w_<A>	;
p<O>:p<O>	<WC>Punoa_s_<A>	;

LEXICON Asettua
<U>:<U>	<WC>Punoa_w_<A>	;
t<U>:t<U>	<WC>Punoa_s_<A>	;

LEXICON Kaatua
d<U>:d<U>	<WC>Punoa_w_<A>	;
t<U>:t<U>	<WC>Punoa_s_<A>	;

LEXICON Viittoa
<O>:<O>	<WC>Punoa_w_<A>	;
t<O>:t<O>	<WC>Punoa_s_<A>	;

LEXICON Aikoa
<O>:<O>	<WC>Punoa_w_<A>	;
k<O>:k<O>	<WC>Punoa_s_<A>	;

LEXICON Kiekua
<U>:<U>	<WC>Punoa_w_<A>	;
k<U>:k<U>	<WC>Punoa_s_<A>	;

LEXICON Harppoa
<O>:<O>	<WC>Punoa_w_<A>	;
p<O>:p<O>	<WC>Punoa_s_<A>	;

LEXICON Kieppua
<U>:<U>	<WC>Punoa_w_<A>	;
p<U>:p<U>	<WC>Punoa_s_<A>	;

LEXICON Jakaantua
n<U>:n<U>	<WC>Punoa_w_<A>	;
t<U>:t<U>	<WC>Punoa_s_<A>	;

LEXICON Kertoa
r<O>:r<O>	<WC>Punoa_w_<A>	;
t<O>:t<O>	<WC>Punoa_s_<A>	;

LEXICON Kumartua
r<U>:r<U>	<WC>Punoa_w_<A>	;
t<U>:t<U>	<WC>Punoa_s_<A>	;

LEXICON Mankua
g<U>:g<U>	<WC>Punoa_w_<A>	;
k<U>:k<U>	<WC>Punoa_s_<A>	;

LEXICON Penkoa
g<O>:g<O>	<WC>Punoa_w_<A>	;
k<O>:k<O>	<WC>Punoa_s_<A>	;

LEXICON Paleltua
l<U>:l<U>	<WC>Punoa_w_<A>	;
t<U>:t<U>	<WC>Punoa_s_<A>	;

LEXICON Tahtoa
d<O>:d<O>	<WC>Punoa_w_<A>	;
t<O>:t<O>	<WC>Punoa_s_<A>	;

LEXICON Leipoa
v<O>:v<O>	<WC>Punoa_w_<A>	;
p<O>:p<O>	<WC>Punoa_s_<A>	;

LEXICON Saapua
v<U>:v<U>	<WC>Punoa_w_<A>	;
p<U>:p<U>	<WC>Punoa_s_<A>	;

LEXICON Liukua
'<U>:'<U>	<WC>Punoa_w_<A>	;
k<U>:k<U>	<WC>Punoa_s_<A>	;

LEXICON Punoa_w
KestamanTekijapaateHeikko_<A>	;
KertomanTekijapaateHeikko_<A>	;

LEXICON Punoa_s
[Tn1]<A>:<A>	Liitesana_<A>	;
[Tt][Ap][P3][Ny][[L]]:[[L]]	Liitesana_<A>	;
[Tt][Ap][P3][Nm]v<A>t:v<A>t	Liitesana_<A>	;
KertomanTekijapaateVahva_<A>	;
Ehtotapa_<A>	;
Johdin_nimitapa4_<A>	;
Johdin_ja_kantaja_<A>	;
Johdin_nUt_<A>	;
Johdin_vA_<A>	;

LEXICON Aavistaa
<WC>MuistaaMuistella_w_<A>	;
<WC>MuistaaMuistella_s_<A>	;
[Ln]<U>:<U>	NimisanaVastaus_<A>	;

LEXICON Alittaa
<WC>MuistaaMuistella_w_<A>	;
t:t	<WC>MuistaaMuistella_s_<A>	;
Johdin_Us_ksen_<A>	;

LEXICON Astahtaa
d:d	<WC>MuistaaMuistella_w_<A>	;
t:t	<WC>MuistaaMuistella_s_<A>	;
d:d	Johdin_Us_ksen_<A>	;

LEXICON Hidastaa
<WC>MuistaaMuistella_w_<A>	;
<WC>MuistaaMuistella_s_<A>	;
[Ln]<U>:<U>	NimisanaVastaus_<A>	;
<U>:<U>	Punoa_<A>	;

LEXICON Heittää
@C.V_SALLITTU@:@C.V_SALLITTU@	<WC>MuistaaMuistella_w_<A>	;
@C.V_SALLITTU@t:@C.V_SALLITTU@t	<WC>MuistaaMuistella_s_<A>	;
@R.V_SALLITTU.E@@C.V_SALLITTU@:@R.V_SALLITTU.E@@C.V_SALLITTU@	NimisanaLiitto_ä	;
@R.V_SALLITTU.T@@C.V_SALLITTU@:@R.V_SALLITTU.T@@C.V_SALLITTU@	NimisanaLiitto_a	;
!TODO heitäntä

LEXICON Muistaa
@C.V_SALLITTU@:@C.V_SALLITTU@	<WC>MuistaaMuistella_w_<A>	;
@C.V_SALLITTU@:@C.V_SALLITTU@	<WC>MuistaaMuistella_s_<A>	;
@R.V_SALLITTU.E@@C.V_SALLITTU@ö:@R.V_SALLITTU.E@@C.V_SALLITTU@ö	NimisanaValo_ä	;
@R.V_SALLITTU.T@@C.V_SALLITTU@o:@R.V_SALLITTU.T@@C.V_SALLITTU@o	NimisanaValo_a	;
!TODO muistua

LEXICON Kirjoittaa
it:it	<WC>MuistaaMuistella_w_<A>	;
itt:itt	<WC>MuistaaMuistella_s_<A>	;
it:it	Johdin_Us_ksen_<A>	;
!TODO kirjoittautua

LEXICON Ammottaa
t:t	<WC>Muistaa_w_<A>	;
tt:tt	<WC>Muistaa_s_<A>	;
t:t	Johdin_Us_ksen_<A>	;
!TODO ammottautua
!TODO ammotella

LEXICON MuistaaMuistella_w
<WC>Muistaa_w_<A>	;
el:el	<WC>Katsella_w_<A>	;

LEXICON MuistaaMuistella_s
<WC>Muistaa_s_<A>	;
el:el	<WC>Katsella_s_<A>	;

LEXICON Muistaa_w
<A>:<A>	KestamanTekijapaateHeikko_<A>	;
KertomanTekijapaateHeikko_<A>	;
e:e	TositavanKestamaTaan_<A>	;
e:e	Johdin_ttAvA_<A>	;

LEXICON Muistaa_s
[Tn1]<A><A>:<A><A>	Liitesana_<A>	;
[Tt][Ap][P3][Nm]<A>v<A>t:<A>v<A>t	Liitesana_<A>	;
KertomanTekijapaateVahva_<A>	;
<A>:<A>	Ehtotapa_<A>	;
<A>:<A>	Johdin_nimitapa3_<A>	;
<A>:<A>	Johdin_nimitapa4_<A>	;
<A>:<A>	Johdin_ja_kantaja_<A>	;
<A>:<A>	Johdin_mA_<A>	;

LEXICON Huutaa
d:d	<WC>Huutaa_w_<A>	;
t:t	<WC>Huutaa_s_<A>	;
del:del	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Juontaa
@C.V_SALLITTU@n:@C.V_SALLITTU@n	<WC>Huutaa_w_<A>	;
@C.V_SALLITTU@t:@C.V_SALLITTU@t	<WC>Huutaa_s_<A>	;
@C.V_SALLITTU@nel:@C.V_SALLITTU@nel	<WC>Katsella_w_<A>	;
@C.V_SALLITTU@tel:@C.V_SALLITTU@tel	<WC>Katsella_s_<A>	;
@R.V_SALLITTU.E@@C.V_SALLITTU@[Ln]:@R.V_SALLITTU.E@@C.V_SALLITTU@	NimisanaAnto_ä	;
@R.V_SALLITTU.T@@C.V_SALLITTU@[Ln]:@R.V_SALLITTU.T@@C.V_SALLITTU@	NimisanaAnto_a	;

LEXICON Pyörtää
r:r	<WC>Huutaa_w_<A>	;
t:t	<WC>Huutaa_s_<A>	;
rel:rel	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;
[Ln]t<O>:t<O>	NimisanaValo_s_<A>	;
[Ln]t<O><O>:t<O><O>	NimisanaValo_sl_<A>	;
[Ln]r<O>:r<O>	NimisanaValo_w_<A>	;

LEXICON Pahentaa
n:n	<WC>Huutaa_w_<A>	;
t:t	<WC>Huutaa_s_<A>	;
nel:nel	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;
n:n	Johdin_Us_ksen_<A>	;
Jakaantua_<A>	;

LEXICON Huutaa_w
<A>:<A>	KestamanTekijapaateHeikko_<A>	;
e:e	Johdin_ttU_<A>	;

LEXICON Huutaa_s
[Tn1]<A><A>:<A><A>	Liitesana_<A>	;
<A>:<A>	Ehtotapa_<A>	;
<A>:<A>	Johdin_ja_kantaja_<A>	;

LEXICON Kaivaa
<WC>Kaivaa_w_<A>	;
<WC>Kaivaa_s_<A>	;
!TODO kaivella
!TODO kaivanta

LEXICON Lappaa
<WC>Kaivaa_w_<A>	;
p:p	<WC>Kaivaa_s_<A>	;

LEXICON Jakaa
<WC>Kaivaa_w_<A>	;
k:k	<WC>Kaivaa_s_<A>	;

LEXICON Saattaa
<WC>Kaivaa_w_<A>	;
t:t	<WC>Kaivaa_s_<A>	;
!TODO saatella

LEXICON Antaa
n:n	<WC>Kaivaa_w_<A>	;
t:t	<WC>Kaivaa_s_<A>	;
!TODO annattaa
!TODO annella

LEXICON Raataa
d:d	<WC>Kaivaa_w_<A>	;
t:t	<WC>Kaivaa_s_<A>	;
!TODO raadella
!TODO raadanta

LEXICON Kaivaa_w
<A>:<A>	KestamanTekijapaateHeikko_<A>	;
e:e	Johdin_ttU_<A>	;

LEXICON Kaivaa_s
[Tn1]<A><A>:<A><A>	Liitesana_<A>	;
<A>:<A>	Johdin_ja_kantaja_<A>	;

LEXICON Laskea
LaskeaLaskeskella_s_<A>	;
LaskeaLaskeskella_w_<A>	;

LEXICON Hakea
k:k	LaskeaLaskeskella_s_<A>	;
LaskeaLaskeskella_w_<A>	;

LEXICON LaskeaLaskeskella_s
[Tn1]e<A>:e<A>	Liitesana_<A>	;
e:e	Johdin_mA_<A>	;

LEXICON LaskeaLaskeskella_w
e:e	KestamanTekijapaateHeikko_<A>	;

LEXICON Sallia
<WC>Sallia_w_<A>	;
<WC>Sallia_s_<A>	;

LEXICON Onkia
g:g	<WC>Sallia_w_<A>	;
k:k	<WC>Sallia_s_<A>	;

LEXICON Empiä
m:m	<WC>Sallia_w_<A>	;
p:p	<WC>Sallia_s_<A>	;

LEXICON Kontia
n:n	<WC>Sallia_w_<A>	;
t:t	<WC>Sallia_s_<A>	;

LEXICON Oppia
<WC>Sallia_w_<A>	;
p:p	<WC>Sallia_s_<A>	;

LEXICON Loikkia
<WC>Sallia_w_<A>	;
k:k	<WC>Sallia_s_<A>	;

LEXICON Sättiä
<WC>Sallia_w_<A>	;
t:t	<WC>Sallia_s_<A>	;

LEXICON Laatia
d:d	<WC>Sallia_w_<A>	;
t:t	<WC>Sallia_s_<A>	;

LEXICON Kaapia
v:v	<WC>Sallia_w_<A>	;
p:p	<WC>Sallia_s_<A>	;

LEXICON Hylkiä
j:j	<WC>Sallia_w_<A>	;
k:k	<WC>Sallia_s_<A>	;

LEXICON Poikia
':'	<WC>Sallia_w_<A>	;
k:k	<WC>Sallia_s_<A>	;

LEXICON Sallia_w
i:i	KestamanTekijapaateHeikko_<A>	;
i:i	Johdin_ttU_<A>	;
i:i	Johdin_ntA_<A>	;

LEXICON Sallia_s
[Tn1]i<A>:i<A>	Liitesana_<A>	;
[Tt][Ap][P3][Ny]ii:ii	Liitesana_<A>	;
[Tt][Ap][P3][Nm]iv<A>t:iv<A>t	Liitesana_<A>	;
Ehtotapa_<A>	;
i:i	Johdin_vA_<A>	;
i:i	Johdin_ja_kulkija_<A>	;

LEXICON Kanavoida
[Tn1]id<A>:id<A>	Liitesana_<A>	;
id:id	Johdin_nimitapa2_<A>	;
i:i	KestamanTekijapaateHeikko_<A>	;
KertomanTekijapaateHeikko_<A>	;
KertomanTekijapaateVahva_<A>	;
[Tt][Ap][P3][Ny]i:i	Liitesana_<A>	;
[Tt][Ap][P3][Nm]iv<A>t:iv<A>t	Liitesana_<A>	;
Ehtotapa_<A>	;
i:i	Johdin_nimitapa3_<A>	;
i:i	Johdin_nimitapa4_<A>	;
i:i	Johdin_ja_myyjä_<A>	;
i:i	Johdin_nti_<A>	;

LEXICON Katsella
<WC>Katsella_w_<A>	;
<WC>Katsella_s_<A>	;

LEXICON Kohdella
del:del	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Nakella
el:el	<WC>Katsella_w_<A>	;
kel:kel	<WC>Katsella_s_<A>	;

LEXICON Tapella
el:el	<WC>Katsella_w_<A>	;
pel:pel	<WC>Katsella_s_<A>	;

LEXICON Aatella
el:el	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Askarrella
rel:rel	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Ommella
mel:mel	<WC>Katsella_w_<A>	;
pel:pel	<WC>Katsella_s_<A>	;

LEXICON Pienennellä
nel:nel	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Takellella
lel:lel	<WC>Katsella_w_<A>	;
tel:tel	<WC>Katsella_s_<A>	;

LEXICON Katsella_w
[Tn1]l<A>:l<A>	Liitesana_<A>	;
l:l	Johdin_nimitapa2_<A>	;

LEXICON Katsella_s
e:e	KestamanTekijapaateHeikko_<A>	;
KertomanTekijapaateHeikko_<A>	;
KertomanTekijapaateVahva_<A>	;
[Tt][P3][Ny]ee:ee	Liitesana_<A>	;
[Tt][P3][Nm]ev<A>t:ev<A>t	Liitesana_<A>	;
Ehtotapa_<A>	;
e:e	Johdin_nimitapa3_<A>	;
e:e	Johdin_nimitapa4_<A>	;
i:i	Johdin_ja_kulkija_<A>	;
[Ln]<U>:<U>	NimisanaArvelu_<A>	;

LEXICON Arvailla
[Tn1]l<A>:l<A>	Liitesana_<A>	;
e:e	KestamanTekijapaateHeikko_<A>	;
KertomanTekijapaateHeikko_<A>	;
KertomanTekijapaateVahva_<A>	;
[Tt][Ap][P3][Ny]ee:ee	Liitesana_<A>	;
[Tt][Ap][P3][Nm]ev<A>t:ev<A>t	Liitesana_<A>	;
Ehtotapa_<A>	;

LEXICON Valita
[Tn1]t<A>:t<A>	Liitesana_<A>	;
t:t	Johdin_nimitapa2_<A>	;
tse:tse	KestamanTekijapaateHeikko_<A>	;
ts:ts	KertomanTekijapaateHeikko_<A>	;
ts:ts	KertomanTekijapaateVahva_<A>	;
[Tt][Ap][P3][Ny]tsee:tsee	Liitesana_<A>	;
[Tt][Ap][P3][Nm]tsev<A>t:tsev<A>t	Liitesana_<A>	;
ts:ts	Ehtotapa_<A>	;
tse:tse	Johdin_nimitapa3_<A>	;
tse:tse	Johdin_nimitapa4_<A>	;
tsi:tsi	Johdin_ja_kulkija_<A>	;
Johdin_ntA_<A>	;

LEXICON Salata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
<A>:<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Kammata
m<A>:m<A>	<WC>SalataSalailla_w_<A>	;
p<A>:p<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Suunnata
n<A>:n<A>	<WC>SalataSalailla_w_<A>	;
t<A>:t<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Hangata
g<A>:g<A>	<WC>SalataSalailla_w_<A>	;
k<A>:k<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Kerrata
r<A>:r<A>	<WC>SalataSalailla_w_<A>	;
t<A>:t<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Vallata
l<A>:l<A>	<WC>SalataSalailla_w_<A>	;
t<A>:t<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Lobata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
b<A>:b<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Kelvata
v<A>:v<A>	<WC>SalataSalailla_w_<A>	;
p<A>:p<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Kuitata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
t<A>:t<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Ladata
d<A>:d<A>	<WC>SalataSalailla_w_<A>	;
t<A>:t<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Peljätä
j<A>:j<A>	<WC>SalataSalailla_w_<A>	;
k<A>:k<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Pakata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
k<A>:k<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Pompata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
p<A>:p<A>	<WC>SalataSalailla_s_<A>	;

LEXICON Taata
<A>:<A>	<WC>SalataSalailla_w_<A>	;
k<A>:k<A>	<WC>SalataSalailla_s_<A>	;

LEXICON SalataSalailla_s
Salata_s_<A>	;
il:il	Arvailla_<A>	;

LEXICON SalataSalailla_w
Salata_w_<A>	;

LEXICON Salata_s
<A>:<A>	KestamanTekijapaateHeikko_<A>	;
s:s	KertomanTekijapaateHeikko_<A>	;
s:s	KertomanTekijapaateVahva_<A>	;
[Tt][P3][Ny]<A>:<A>	Liitesana_<A>	;
[Tt][P3][Nm]<A>v<A>t:<A>v<A>t	Liitesana_<A>	;
Ehtotapa_<A>	;
<A>:<A>	Johdin_nimitapa3_<A>	;
<A>:<A>	Johdin_nimitapa4_<A>	;
[Ln]<A>:<A>	Johdin_ja_kantaja_<A>	;
Johdin_Us_ksen_<A>	;

LEXICON Salata_w
[Tn1]t<A>:t<A>	Liitesana_<A>	;
t:t	Johdin_nimitapa2_<A>	;

LEXICON KestamanTekijapaateHeikko
[Tt][Ap][P1][Ny]n:n	Liitesana_<A>	;
[Tt][Ap][P2][Ny]t:t	Liitesana_<A>	;
[Tt][Ap][P1][Nm]mme:mme	Liitesana_<A>	;
[Tt][Ap][P2][Nm]tte:tte	Liitesana_<A>	;

LEXICON KertomanTekijapaateHeikko
[Tt][Ai][P1][Ny]in:in	Liitesana_<A>	;
[Tt][Ai][P2][Ny]it:it	Liitesana_<A>	;
[Tt][Ai][P1][Nm]imme:imme	Liitesana_<A>	;
[Tt][Ai][P2][Nm]itte:itte	Liitesana_<A>	;

LEXICON KertomanTekijapaateVahva
[Tt][Ai][P3][Ny]i:i	Liitesana_<A>	;
[Tt][Ai][P3][Nm]iv<A>t:iv<A>t	Liitesana_<A>	;

LEXICON TositavanKestamaTaan
[Tt][Ai][P4]t<A><A>n:t<A><A>n	Liitesana_<A>	;

LEXICON Ehtotapa
[Te][P1][Ny]isin:isin	Liitesana_<A>	;
[Te][P2][Ny]isit:isit	Liitesana_<A>	;
[Te][P3][Ny]isi:isi	Liitesana_<A>	;
[Te][P1][Nm]isimme:isimme	Liitesana_<A>	;
[Te][P2][Nm]isitte:isitte	Liitesana_<A>	;
[Te][P3][Nm]isiv<A>t:isiv<A>t	Liitesana_<A>	;

LEXICON Johdin_nimitapa2
[Tn2]ess<A>:ess<A>	Liitesana_<A>	;
[Tn2]en:en	Liitesana_<A>	;

LEXICON Johdin_nimitapa3
[Tn3][Ny][Sine]m<A>ss<A>:m<A>ss<A>	Liitesana_<A>	;
[Tn3][Ny][Sela]m<A>st<A>:m<A>st<A>	Liitesana_<A>	;
[Tn3][Ny][Sill]m<A><A>n:m<A><A>n	Liitesana_<A>	;
[Tn3][Ny][Sade]m<A>ll<A>:m<A>ll<A>	Liitesana_<A>	;
[Tn3][Ny][Sab]m<A>tt<A>:m<A>tt<A>	Liitesana_<A>	;

LEXICON Johdin_nimitapa4
mi:mi	NimisanaNainen_<A>	;

LEXICON Johdin_Us_ksen
[Ln]<U>:<U>	NimisanaVastaus_<A>	;

LEXICON Johdin_Us
[Ln]<U>:<U>	NimisanaKalleus_<A>	;

LEXICON Johdin_UUs
[Ln]<U><U>:<U><U>	NimisanaKalleus_<A>	;

LEXICON Johdin_mAinen
[Ll]m<A>i:m<A>i	LaatusanaNainen_<A>	;

LEXICON Johdin_ttAvA
[Ll]tt<A>v:tt<A>v	LaatusanaAsema_<A>	;

LEXICON Johdin_nUt
[Ll]n:n	LaatusanaKuollut_<A>	;

LEXICON Johdin_vA
[Ll]v:v	LaatusanaAsema_<A>	;

LEXICON Johdin_mA
[Ln]m:m	NimisanaAsema_<A>	;

LEXICON Johdin_ntA
[Ln]n:n	NimisanaKanta_<A>	;

LEXICON Johdin_inen_vain_lipulla
@R.INEN_SALLITTU@:@R.INEN_SALLITTU@	Johdin_inen_<A>	;
@R.INEN_VAADITTU@:@R.INEN_VAADITTU@	Johdin_inen_<A>	;

LEXICON Johdin_inen
[Ll]i@C.INEN_VAADITTU@:i@C.INEN_VAADITTU@	LaatusanaNainen_<A>	;

LEXICON Johdin_tOn
[Ll]t:t	LaatusanaOnneton_<A>	;
tt<O>m:tt<O>m	Johdin_UUs_<A>	;
tt<O>m<A>:tt<O>m<A>	Voittoaste_<A>	;
tt<O>m:tt<O>m	Yliaste_<A>	;
[Ll]lli:lli	LaatusanaNainen_<A>    ;

LEXICON Johdin_ttU
[Ll]t:t	LaatusanaHattu_<A>	;

LEXICON Kerronto_sti
[Ssti]sti:sti	Liitesana_<A>	;

LEXICON Johdin_ja_kantaja
Johdin_ja_kantaja_myyjä_<A>	;
Johdin_ja_kulkija_<A>	;

LEXICON Johdin_ja_kulkija
[Sg][Nm]j<O>iden@C.JOHDETTAVA_NIMISANAKSI@:j<O>iden@C.JOHDETTAVA_NIMISANAKSI@	NimisanaNormaaliYsJatko_<A>	;
[Sg][Nm]j<O>itten@C.JOHDETTAVA_NIMISANAKSI@:j<O>itten@C.JOHDETTAVA_NIMISANAKSI@	NimisanaNormaaliYsJatko_<A>	;
[Sine][Nm]j<O>iss<A>@C.JOHDETTAVA_NIMISANAKSI@:j<O>iss<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sela][Nm]j<O>ist<A>@C.JOHDETTAVA_NIMISANAKSI@:j<O>ist<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sill][Nm]j<O>ihin@C.JOHDETTAVA_NIMISANAKSI@:j<O>ihin@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sade][Nm]j<O>ill<A>@C.JOHDETTAVA_NIMISANAKSI@:j<O>ill<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sabl][Nm]j<O>ilt<A>@C.JOHDETTAVA_NIMISANAKSI@:j<O>ilt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sall][Nm]j<O>ille@C.JOHDETTAVA_NIMISANAKSI@:j<O>ille@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sab][Nm]j<O>itt<A>@C.JOHDETTAVA_NIMISANAKSI@:j<O>itt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sin][Nm]j<O>in@C.JOHDETTAVA_NIMISANAKSI@:j<O>in@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
Johdin_ja_kulkija_myyjä_<A>	;

LEXICON Johdin_ja_myyjä
Johdin_ja_kantaja_myyjä_<A>	;
Johdin_ja_kulkija_myyjä_<A>	;

LEXICON Johdin_ja_kulkija_myyjä
[Sn][Nm]j<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>@C.JOHDETTAVA_NIMISANAKSI@	NimisanaNormaaliYsJatko_<A>	;
[Sn][Nm]j<A>t@C.JOHDETTAVA_NIMISANAKSI@:j<A>t@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sg][Ny]j<A>n@C.JOHDETTAVA_NIMISANAKSI@:j<A>n@C.JOHDETTAVA_NIMISANAKSI@	NimisanaNormaaliYsJatko_<A>	;
[Ses][Ny]j<A>n<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>n<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sine][Ny]j<A>ss<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>ss<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sela][Ny]j<A>st<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>st<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sill][Ny]j<A><A>n@C.JOHDETTAVA_NIMISANAKSI@:j<A><A>n@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sade][Ny]j<A>ll<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>ll<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sabl][Ny]j<A>lt<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>lt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sall][Ny]j<A>lle@C.JOHDETTAVA_NIMISANAKSI@:j<A>lle@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sab][Ny]j<A>tt<A>@C.JOHDETTAVA_NIMISANAKSI@:j<A>tt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;

LEXICON Johdin_ja_kantaja_myyjä
[Sg][Nm]jien@C.JOHDETTAVA_NIMISANAKSI@:jien@C.JOHDETTAVA_NIMISANAKSI@	NimisanaNormaaliYsJatko_<A>	;
[Ses][Nm]jin<A>@C.JOHDETTAVA_NIMISANAKSI@:jin<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sine][Nm]jiss<A>@C.JOHDETTAVA_NIMISANAKSI@:jiss<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sela][Nm]jist<A>@C.JOHDETTAVA_NIMISANAKSI@:jist<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sill][Nm]jiin@C.JOHDETTAVA_NIMISANAKSI@:jiin@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sade][Nm]jill<A>@C.JOHDETTAVA_NIMISANAKSI@:jill<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sabl][Nm]jilt<A>@C.JOHDETTAVA_NIMISANAKSI@:jilt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sall][Nm]jille@C.JOHDETTAVA_NIMISANAKSI@:jille@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sab][Nm]jitt<A>@C.JOHDETTAVA_NIMISANAKSI@:jitt<A>@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;
[Sin][Nm]jin@C.JOHDETTAVA_NIMISANAKSI@:jin@C.JOHDETTAVA_NIMISANAKSI@	Liitesana_<A>	;

LEXICON Johdin_nti
n:n	NimisanaTunti_<A>	;

LEXICON Voittoaste
[Sn][Ny]mpi:mpi	Liitesana_<A>	;
[Sn][Nm]mm<A>t:mm<A>t	Liitesana_<A>	;
[Sg][Ny]mm<A>n:mm<A>n	Liitesana_<A>	;
[Sg][Nm]mpien:mpien	Liitesana_<A>	;
[Sp][Ny]mp<A><A>:mp<A><A>	Liitesana_<A>	;
[Sp][Nm]mpi<A>:mpi<A>	Liitesana_<A>	;
[Str][Ny]mm<A>ksi:mm<A>ksi	Liitesana_<A>	;
[Str][Nm]mmiksi:mmiksi	Liitesana_<A>	;
[Ses][Ny]mp<A>n<A>:mp<A>n<A>	Liitesana_<A>	;
[Ses][Nm]mpin<A>:mpin<A>	Liitesana_<A>	;
[Sine][Ny]mm<A>ss<A>:mm<A>ss<A>	Liitesana_<A>	;
[Sine][Nm]mmiss<A>:mmiss<A>	Liitesana_<A>	;
[Sela][Ny]mm<A>st<A>:mm<A>st<A>	Liitesana_<A>	;
[Sela][Nm]mmist<A>:mmist<A>	Liitesana_<A>	;
[Sill][Ny]mp<A><A>n:mp<A><A>n	Liitesana_<A>	;
[Sill][Nm]mpiin:mpiin	Liitesana_<A>	;
[Sade][Ny]mm<A>ll<A>:mm<A>ll<A>	Liitesana_<A>	;
[Sade][Nm]mmill<A>:mmill<A>	Liitesana_<A>	;
[Sabl][Ny]mm<A>lt<A>:mm<A>lt<A>	Liitesana_<A>	;
[Sabl][Nm]mmilt<A>:mmilt<A>	Liitesana_<A>	;
[Sall][Ny]mm<A>lle:mm<A>lle	Liitesana_<A>	;
[Sall][Nm]mmille:mmille	Liitesana_<A>	;
[Sab][Ny]mm<A>tt<A>:mm<A>tt<A>	Liitesana_<A>	;
[Sab][Nm]mmitt<A>:mmitt<A>	Liitesana_<A>	;

LEXICON Yliaste
[Sn][Ny]in:in	Liitesana_<A>	;
[Sn][Nm]imm<A>t:imm<A>t	Liitesana_<A>	;
[Sg][Ny]imm<A>n:imm<A>n	Liitesana_<A>	;
[Sg][Nm]impien:impien	Liitesana_<A>	;
[Sp][Ny]int<A>:int<A>	Liitesana_<A>	;
[Sp][Ny]imp<A><A>:imp<A><A>	Liitesana_<A>	;
[Sp][Nm]impi<A>:impi<A>	Liitesana_<A>	;
[Str][Ny]imm<A>ksi:imm<A>ksi	Liitesana_<A>	;
[Str][Nm]immiksi:immiksi	Liitesana_<A>	;
[Ses][Ny]imp<A>n<A>:imp<A>n<A>	Liitesana_<A>	;
[Ses][Nm]impin<A>:impin<A>	Liitesana_<A>	;
[Sine][Ny]imm<A>ss<A>:imm<A>ss<A>	Liitesana_<A>	;
[Sine][Nm]immiss<A>:immiss<A>	Liitesana_<A>	;
[Sela][Ny]imm<A>st<A>:imm<A>st<A>	Liitesana_<A>	;
[Sela][Nm]immist<A>:immist<A>	Liitesana_<A>	;
[Sill][Ny]imp<A><A>n:imp<A><A>n	Liitesana_<A>	;
[Sill][Nm]impiin:impiin	Liitesana_<A>	;
[Sade][Ny]imm<A>ll<A>:imm<A>ll<A>	Liitesana_<A>	;
[Sade][Nm]immill<A>:immill<A>	Liitesana_<A>	;
[Sabl][Ny]imm<A>lt<A>:imm<A>lt<A>	Liitesana_<A>	;
[Sabl][Nm]immilt<A>:immilt<A>	Liitesana_<A>	;
[Sall][Ny]imm<A>lle:imm<A>lle	Liitesana_<A>	;
[Sall][Nm]immille:immille	Liitesana_<A>	;
[Sab][Ny]imm<A>tt<A>:imm<A>tt<A>	Liitesana_<A>	;
[Sab][Nm]immitt<A>:immitt<A>	Liitesana_<A>	;
